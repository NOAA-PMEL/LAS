!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! $RCSfile: xy_compare_vectors.jnl
! 
! $Author: ansley $
! $Date: 2010/06/23
! 10/10/2012 Reverse the sense of the difference; ticket 1377

! xy_compare_vectors.jnl creates an XY plot for use with the 
! Back End Server code that sits behind a Live Access Server (LAS). 
! It produces a difference plot of 2 vector pairs variables which 
! are on the same xy grid
!
! Set any Ferret modes
!
! NOTE:  Should we support any of the following Ferrt modes?
! NOTE:    ASCII_FONT, CALENDAR, DEPTH_LABEL, LABELS, LATIT_LABEL, LONG_LABEL 

IF ($ferret_interpolate_data%0|false>0|true>1|1|0|*>1%) THEN SET MODE INTERPOLATE

DEFINE SYMBOL itsa_vector_plot = 1
DEFINE SYMBOL fview = `UPCASE("($ferret_view)")`

! If levels werent sent in, set to use centered levels.

IF `($ferret_contour_levels"0|*>1") EQ 0` THEN DEFINE SYMBOL ferret_contour_levels = c
IF `($ferret_fill_levels"0|*>1") EQ 0` THEN DEFINE SYMBOL ferret_fill_levels = c

! Define symbols associated with the regign and data and perform any
! initialization needed for this dataset.
!
! NOTE:  Adding support for a 'base layer' variable would mean passing
! NOTE:  some other argument.  For now, though, we always use 'data_0'
! NOTE:  for the base layer and region.

GO LAS_initialize_region 0

GO LAS_initialize_data 0

DEFINE SYMBOL ferret_vector_x1 ($ferret_plot_var)
DEFINE SYMBOL ferret_plot_title_x1 "($data_title) ($data_units)"

GO LAS_initialize_data 1

DEFINE SYMBOL ferret_vector_y1 ($ferret_plot_var)
DEFINE SYMBOL ferret_title_0 "($ferret_plot_title_x1), ($data_title) ($data_units)"

! Check for errors (They often occur during dataset initialization.)

IF ($error_status"0|ERROR>1") THEN
  MESSAGE/ERROR **ERROR ($error_string)
  EXIT/PROGRAM
ENDIF

! Initialize next dataset. 
! (The interface guarantees that the match in X and Y)

! Apply any expression only to the first variable not to this one.
CANCEL SYMBOL ferret_expression 

GO LAS_initialize_region 2
DEFINE SYMBOL region_1_t_lo ($region_2_t_lo)  ! so that LAS_set_diff_label can be used.

GO LAS_initialize_data 2

DEFINE SYMBOL ferret_vector_x2 ($ferret_plot_var)
DEFINE SYMBOL ferret_plot_title_x2 "($data_title) ($data_units)"

GO LAS_initialize_data 3
DEFINE SYMBOL ferret_vector_y2 ($ferret_plot_var)
DEFINE SYMBOL ferret_title_1 "($ferret_plot_title_x2), ($data_title) ($data_units)"

! Check whether the region is too small to make a 2D plot on this grid.
GO LAS_check_2d_region

! Check for errors 

IF ($error_status"0|*>1") THEN
   MESSAGE/ERROR **ERROR ($error_string)
   EXIT/PROGRAM
ENDIF

! If we are using index strides then apply the correct ones to 
! the first variable.
IF ($set_ndx_strides_0"0|*>1") THEN
   DEFINE SYMBOL ferret_var_0 = ($data_0_var)[d=($data_0_num),($set_ndx_strides_0)]
ENDIF

! Compare the X and Y axes of the two variables. They must be the same.

GO LAS_compare_axes "($data_0_var)[d=($data_0_num)]" "($data_2_var)[d=($data_2_num)]" ($fview)

! Check for errors 

IF ($error_status"0|*>1") THEN   
   DEFINE SYMBOL error_string = ($fview) grid of ($data_2_var) must match ($fview) grid of ($data_0_var)
   MESSAGE/ERROR **ERROR ($error_string)
   EXIT/PROGRAM
ENDIF

! Define the plot variable 

DEFINE SYMBOL ferret_vector_xdiff = ($ferret_vector_x2)-($ferret_vector_x1)
DEFINE SYMBOL ferret_vector_ydiff = ($ferret_vector_y2)-($ferret_vector_y1)
DEFINE SYMBOL ferret_diff_var = ($ferret_vector_xdiff), ($ferret_vector_ydiff)

! For defining annotations
DEFINE SYMBOL ferret_var_0 = ($ferret_vector_x1), ($ferret_vector_y1)
DEFINE SYMBOL ferret_var_1 = ($ferret_vector_x2), ($ferret_vector_y2)

IF ($native_curvilinear_xy"0|*>1") THEN 
  DEFINE SYMBOL ferret_vector_base_xdiff = ($ferret_plot_base_var_0) - ($ferret_plot_base_var_2)
  DEFINE SYMBOL ferret_vector_base_ydiff = ($ferret_plot_base_var_1) - ($ferret_plot_base_var_3)
  DEFINE SYMBOL ferret_diff_var = ($ferret_vector_base_xdiff),($ferret_vector_base_ydiff), xcoord, ycoord
  DEFINE SYMBOL ferret_diff_var_m360 = ($ferret_vector_base_xdiff),($ferret_vector_base_ydiff), xcoord-360, ycoord
  DEFINE SYMBOL ferret_diff_var_p360 = ($ferret_vector_base_xdiff),($ferret_vector_base_ydiff), xcoord+360, ycoord
ENDIF

! Set the title, and the labels for the upper left listing the locations
! and time in directions normal to the plot, and in the upper right listing
! urls if dataset are opendap.

GO LAS_set_diff_labels ($fview) ($pencolor)
DEFINE SYMBOL ferret_plot_title ($ferret_diff_title)

! set difference-vector color to red. Ignore incoming value of ferret_line_color
DEFINE SYMBOL pencolor = 2
DEFINE SYMBOL ferret_line_color = ($pencolor)

! Use Ferret properties to set up the plot qualifiers
GO LAS_set_plot_qualifiers

! Curvilinear plots may have more qualifiers. 
IF ($native_curvilinear_xy"0|*>1") THEN
  DEFINE SYMBOL qualifiers = ($qualifiers)($ferret_curvi_quals)
ENDIF

! Open the window, apply size, scale options
GO LAS_open_window

! Draw  the plot. 

IF ($native_curvilinear_xy"0|*>1") THEN 
   IF ($ferret_vector_base_xdiff"0|*>1") THEN \
    DEFINE SYMBOL ferret_curvi_base_var = ($ferret_vector_base_xdiff),($ferret_vector_base_ydiff)
ELSE
   IF ($ferret_diff_var"0|*>1") THEN \
    DEFINE SYMBOL ferret_vector_variable ($ferret_diff_var)
ENDIF

GO LAS_draw_xy_vector_plot

! Overall header at the very top
GO LAS_ferret_las_version_header

! Put the labels for time/location in the upper left

GO labels_above_plot

! Overlay additional cartography data (land mask, outlines, rivers, ...)

GO land_overlay

! Mark grid points. Options are no, subsample (tests if subsampling needed)
! or all to force marking all points.

IF ($ferret_mark_grid"0|no>0|all>1|subsample>1|*>0") THEN GO mark_grid

! Save the results
GO LAS_results box

! End of $RCSfile -----------------------------------------------------

