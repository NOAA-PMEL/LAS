\CANCEL MODE VERIFY 
! Description:  define FERRET variables for regression along the X axis
! Version to run under LAS with new variable names. Avoids clash with
! LAS variable pvar.

let pq = p*q
let pqmask = pq-pq  ! 0 or "missing" so all variables share the same missing
let pmasked = p + pqmask
let qmasked = q + pqmask

let pave = pmasked[x=@ave]
let qave = qmasked[x=@ave]

let pdev = pmasked-pave
let qdev = qmasked-qave
let pdevqdev = pdev*qdev
let pqvar = pdevqdev[x=@ave]
let LAS_pvar = pmasked[x=@var]
let LAS_qvar = qmasked[x=@var] 

let slope = pqvar / LAS_pvar
let intercep = qave - slope*pave
let qhat = slope*p + intercep
let rsquare = (pqvar*pqvar) / (LAS_pvar*LAS_qvar)

SET MODE/LAST VERIFY