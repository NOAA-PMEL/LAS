!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! LAS_fland.jnl
! $Revision: 1.1 
! 
! Author: ansley
! Date: 2006/07/06
!      11/2017  Add topo015 as an option 
!      11/2018  (Ferret v7.44) add the /AXES qualifier to draw axis and graticule lines
!
! LAS_fland.jnl automatically adjusts the resolution
!
! $1 = palette

DEFINE SYMBOL fland_palette ($1"gray")

LET lon_range = ($region_x_range)
LET lat_range = ($region_y_range)
LET fland_area = `ABS(lon_range * lat_range) / (360 * 180)`
DEFINE SYMBOL fland_area = `fland_area`


! If a tiny region then use the etopo2 or etopo1 from PMEL server

CANCEL SYMBOL topo_url
GO LAS_define_etopo

DEFINE SYMBOL topo_url = ($topo20_url)

! For thumbnail plots, which we want to run extra-fast, draw a coarser
! map for all but the tiniest regions.

LET fland_cutoff = 0.05
IF `($ferret_thumb_qc2"0|*>1") AND (fland_area LT 0.05)` THEN LET fland_cutoff = 0.008

IF `fland_area LT fland_cutoff` THEN
  
   SET DATA/SAVE

   DEF SYM xlo_hack ($xaxis_min%0%)
   DEF SYM xhi_hack ($xaxis_max%360%)
   DEF SYM ylo_hack ($yaxis_min%-90%)
   DEF SYM yhi_hack ($yaxis_max%90%)

   DEF SYM map_reg = [x=($xlo_hack):($xhi_hack),y=($ylo_hack):($yhi_hack)]

   DEFINE SYMBOL land_levels = (0,10000,10000)


   IF ($topo5_url"0|*>1") THEN 
      DEFINE SYMBOL topo_url = "($topo5_url)"
      DEFINE SYMBOL fland_resolution = 5
   ENDIF

   IF ($topo2_url"0|*>1") THEN 
      DEFINE SYMBOL topo_url = "($topo2_url)"
      DEFINE SYMBOL fland_resolution = 2
   ENDIF

   IF `fland_area LT 0.005 AND ($topo1_url"0|*>1")` THEN
      DEFINE SYMBOL topo_url = "($topo1_url)"
      DEFINE SYMBOL fland_resolution = 1
   ENDIF

   IF `fland_area LT 0.0005 AND ($topo_015_url"0|*>1")` THEN 
      DEFINE SYMBOL topo_url = "($topo_015_url)"
      DEFINE SYMBOL fland_resolution = 015
   ENDIF
   IF `($topo_url"0|*>1") EQ 0` THEN DEFINE SYMBOL topo_url = etopo5
   USE ($topo_url)

   SHADE/NOLAB/NOKEY/LEV=($land_levels)/PAL=($fland_palette)/OVERLAY rose($map_reg)
   DEFINE SYMBOL toponame = `rose,return=dset`

   CAN DATA ($toponame)
   SET DATA/RESTORE
   DEFINE SYMBOL fland_done = 1

ELIF `fland_area LT 0.25` THEN
  DEFINE SYMBOL fland_resolution 05
ELSE
  DEFINE SYMBOL fland_resolution 20
ENDIF

IF `($fland_done"0|*>1") EQ 0` THEN \
GO fland ($fland_resolution) ($fland_palette) overlay " " " " " " axes


! End of LAS_fland.jnl --------------------------------------------------
