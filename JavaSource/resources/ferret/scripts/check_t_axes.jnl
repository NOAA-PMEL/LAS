! check_t_axes.jnl
! ACM 10/2007
! For comparison plots involving T, if there is a modulo T axis in the first variable and
! a non-modulo T axis for the second, the result of the regridding will be on the modulo axis,
! In general this results in an all-missing regridded result. Check for this case and issue 
! an error message.

! If its not a modulo axis, then we dont need this check.

LET first_var = ($ferret_var_0)
DEFINE SYMBOL time_mod = "`first_var,RETURN=tmod`"
IF `STRCMP(($time_mod), " ") EQ 0` THEN
   EXIT/SCRIPT
ENDIF
   
! Get the name of the original data variable from dataset 1

LET all_attrs = ($data_1_var).attnames
DEFINE SYMBOL orig_var_1 = ($data_1_var)
IF `IS_ELEMENT_OF_STR (all_attrs, "ferret_definition") EQ 1` THEN
   DEFINE SYMBOL the_def= `($data_1_var).ferret_definition`
   LET bracket = `STRINDEX("($the_def)", "[") - 1`
   IF `bracket GT 0` THEN DEFINE SYMBOL orig_var_1 = `SUBSTRING("($the_def)", 1, bracket)`
ENDIF

! See if that variable had a modulo time axis
SET DATA/SAVE
SET DATA 1
DEFINE SYMBOL mod_len = "`($orig_var_1),RETURN=tmod`"
SET DATA/RESTORE

! Now see if the variable ferret_var_1 has any non-missing data. 
IF `STRCMP(($mod_len), " ") EQ 0` THEN 
  STAT ($ferret_var_1)

  IF `($stat_ngood"0") EQ 0` THEN
     MESSAGE/ERROR **ERROR: This request requires regridding the second argument to the grid of the first. (i.e. B gets regrdidded to A in A-B).  Since A has a climatological time axis this is not possible.  Try reversing the order of the variables you are differencing. 
     EXIT/PROGRAM
  ENDIF
ENDIF
