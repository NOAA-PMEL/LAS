! LAS_auto_levels.jnl

! ACM 8/22/2007
! acm 9/11/07 If its a descriptor file Ferret wont allow striding. 
!             check for that case and just set num levels.
! acm* 2/2010 Set up a few things and then Use Ferret-s open-ended levels 
!             if the levs havent been set by the user or configuration.
!             Allow for old algorithm if older Ferret is in place.
! acm  4/2010 Consolidate so that we use only ferret_fill_levels

! Based on a variables value over its entire range, set strides
! if needed to quickly get stats on the variable. Use this to set levels.

! Argument: the variable to use in setting the levels.

! The symbol ferret_center_levels, if set to 1, forces the levels
! to be 2 std above and below zero. Otherwise use 2 std above 
! and below the data mean

! This script should be done BEFORE any other striding is done.
! It operates on the variable on its entire native unstrided grid.

! input_fill_levels is defined in LAS_initialize_variable.jnl, telling 
! whether settings for levels was sent in

! Determine striding for speed in operating the STAT command
LET memsize = 0.5* 1.e6

IF `($FERRET_VERSION"0") GE 6.2` THEN KEYMARK 1  ! annotate plot keys with the min and max
IF `($ferret_key_annotate"1") EQ 0` THEN KEYMARK 0  ! override annotation with Ferret property

! Make sure its a 2D plot operation.

DEFINE SYMBOL op = `UPCASE("($operation_ID)")`

IF `STRLEN("($ferret_view)") LT 2` THEN EXIT/SCRIPT
IF `STRINDEX("($op)", "VECTOR") GT 0` THEN EXIT/SCRIPT

IF `STRINDEX("($op)", "PLOT") EQ 0 AND \
  STRINDEX ("($op)", "POLY") EQ 0 AND \
  STRINDEX ("($op)", "ZOOM") EQ 0 AND \
  STRINDEX ("($op)", "CRUISE_SUMMARY") EQ 0` THEN EXIT/SCRIPT

! Levels for animations set in LAS_auto_levels_animate, 
! called in list_frames.jnl
IF `STRINDEX("($op)", "ANIMATION") GT 0`  THEN EXIT/SCRIPT

! If its a compare plot, and if the ($ferret_diff_var) is not yet defined, 
! just return.

IF `($ferret_diff_var"1|*>0") AND STRINDEX("($op)", "COMPARE_PLOT") NE 0` THEN
   EXIT/SCRIPT
ENDIF 
IF `($ferret_diff_var"1|*>0") AND ($its_gridded_scattered"0|*>1")` THEN
   EXIT/SCRIPT
ENDIF 

! If requested with posinteger, create levels that are integers or multiples of 
! integers, suitable for COUNTS.

IF ($ferret_fill_levels"0|posinteger>1|*>0" ) THEN
   
   IF `($ferret_plot_var"0|*>1") EQ 0` THEN EXIT/SCRIPT  !e.g. for insitu plots may not have a plot_var

   IF `($do_curvi_xy"0|*>1") EQ 0` THEN 
      LET pvar = ($ferret_xyz_var)
   ELSE
      DEFINE SYMBOL ferret_plot_var = pvar
   ENDIF

   IF ($ferret_plot_var"0|sampled_var>1|*>0) THEN LET pvar = ($ferret_plot_var)

   IF `pvar[i=@ngd,j=@ngd,k=@ngd,L=@ngd] EQ 0` THEN  ! no valid data
      DEFINE SYMBOL ferret_fill_levels = (0, 0, 1)
      EXIT/SCRIPT
   ENDIF

   LET pminmax = MINMAX(pvar)
   LET pmin = `INT(pminmax[i=1])`
   LET pmax = `INT(pminmax[i=2]+0.5)`
   IF `pmin LE 0` THEN 
      LET pmin = 0.5
   ELSE 
      LET pmin = `pmin - 0.5`
   ENDIF
   IF `pmax GT 1` THEN
      LET pmax = `pmax + 0.5`
   ELSE 
      LET pmax = 1.5
   ENDIF
   LET pdel = 1
   IF `pmax - pmin GT 30` THEN LET pdel = 5
   IF `pmax - pmin GT 100` THEN LET pdel = 10
   IF `pmax - pmin GT 30` THEN 
      LET pmin = 1
      LET pmax = `pmax - 0.5`
   ENDIF
   DEFINE SYMBOL ferret_fill_levels = (`pmin`, `pmax`, `pdel`)
   EXIT/SCRIPT
ENDIF

DEFINE SYMBOL ferret_center_levels ($ferret_center_levels"0|1|TRUE>1|*>0")

! See if centered levels were requested, either by setting the symbol ferret_center_levels
! or with fill_levels that is centered.

! If contour levels requested with levels of nC, e.g. 20C, then get the value to
! set num_fine_levels in the %RANGE command below

! If other levels spec is given, return.

IF ($ferret_fill_levels"0|*>1") THEN

   PPL AXNMTC 0,0 ! Turn on PPL so we can do the edit to remove any spaces (can occur in animations)
   PPL SET nospaces $EDIT (ferret_fill_levels, COLLAPSE)
   DEFINE SYMBOL ferret_fill_levels = ($nospaces)

   DEFINE SYMBOL up_fill_levels = `UPCASE("($ferret_fill_levels)")`
   DEFINE SYMBOL ferret_fill_levels = ($up_fill_levels) 
   LET nc = `STRINDEX("($up_fill_levels)", "C")`
   DEFINE SYMBOL ferret_center_levels = `nc GT 0`
   IF `nc GT 0` THEN 
      LET slen = STRLEN ("($up_fill_levels)") 
      IF `slen GT 1` THEN DEFINE SYMBOL num_fine_levels = `SUBSTRING ("($up_fill_levels)", 1, slen-1)`
      IF `STRINDEX("($up_fill_levels)", "V") GT 0` THEN
         CANCEL SYMBOL num_fine_levels
      ELSE
         CANCEL SYMBOL input_fill_levels  ! we will redefine the levels in this script
      ENDIF 
   ENDIF
   LET other = `STRINDEX("($ferret_fill_levels)", "D")` + \
      `STRINDEX("($ferret_fill_levels)", ",")`+ \
      `STRINDEX("($ferret_fill_levels)", "(")`+ \
      `STRINDEX("($ferret_fill_levels)", ".")`

   IF `nc EQ 0 AND other EQ 0` THEN 
      DEFINE SYMBOL num_fine_levels = ($ferret_fill_levels)  ! How to test that this is just a number??
      CANCEL SYMBOL input_fill_levels  ! we will redefine the levels in this script
   ENDIF

! used to use a simple number as the # of fine levs for open-ended. 
! but this left no way to force the script to use old-style linear levs, short of spelling out levels.
!   IF `nc EQ 0 AND other NE 0` THEN  EXIT/SCRIPT  

   IF `nc EQ 0` THEN  EXIT/SCRIPT
ENDIF


! IF fill levels have not been set by the user or in the config, then
! use Ferrets auto-open-ended levels

DEFINE SYMBOL ferret_fill_levels = ($num_fine_levels)v
IF  ($ferret_center_levels"0|0|1") THEN \
      DEFINE SYMBOL ferret_fill_levels = ($num_fine_levels)vc

! End of file ------------LAS_auto_levels.jnl--------------------------
