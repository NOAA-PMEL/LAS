! LAS_draw_multi_line_plot_e.jnl
!  Do a multi-line 1-D plot for an ensemble dataset. 
! Author: Ansley Manke
! Date: 03/03/2014 
!  4/06/13 Input is an averaged variable. Plot it with
!          all of the member variables as colored lines.

LET xvf = ($1%0.1%)
LET xvl = ($2%0.9%)

let yvf = ($3%0.16%)  ! 0.2 >> 0.16
LET yvl = ($4%0.98%)

! Open the window, apply size, scale options
GO LAS_open_window

DEFINE VIEWPORT/AXES/TEXT=0.85/x=`xvf`:`xvl`/y=`yvf`:`yvl` full1
DEFINE VIEWPORT/AXES/TEXT=0.85/x=`xvf`:`xvl`/y=0:`yvl` full2

SET VIEW full1

LET save_count = ($data_count)
PLOT($qualifiers)($qualifiers_0)/NOKEY/SET ($plot_arg)
   IF `($ferret_dep_axis_scale"0|*>1")` THEN PPL YAXIS ($ferret_dep_axis_scale)
   ppl txlsze,0.07
   PPL xlab
   PPL ylab ($data_0_units)
   DEFINE SYMBOL data_count = 1  ! just so the region labels script will work
   GO reset_labels
PPL PLOT

DEFINE SYMBOL data_count = `save_count`

! Make the first line thick
LET thicker = 2
PLOT/OVER/NOLAB/THICK=`thicker`/COLOR=BLACK/LINE ($ferret_plot_var_0)

IF ($labnum_calend%0|*>1%) THEN
   DEFINE SYMBOL n_left_labels = `($n_left_labels"0") + 1`
   DEFINE SYMBOL upper_left_($n_left_labels) = ($lab($labnum_calend))
   DEFINE SYMBOL calendar_lab = ($upper_left_($n_left_labels"0"))
ENDIF

DEFINE SYMBOL ax_vert_save = ($ax_vert"D")
DEFINE SYMBOL ax_horiz_save = ($ax_horiz"D")
DEFINE SYMBOL xax_min_save = ($xaxis_min)
DEFINE SYMBOL xax_max_save = ($xaxis_max)

! Draw the legend.

DEFINE SYMBOL thicksave = ($thicknum)
DEFINE SYMBOL thicknum = 2
DEFINE SYMBOL legend_qual = /LINE

GO LAS_lineplot_legend ($data_count) ($ferret_palette)

! The results script needs the actual axis directions. Restore them.

DEFINE SYMBOL ax_vert = ($ax_vert_save)
IF ($ax_vert"|D>1|*>0") THEN CANCEL SYMBOL ax_vert
DEFINE SYMBOL ax_horiz = ($ax_horiz_save)
IF ($ax_horiz"|D>1|*>0") THEN CANCEL SYMBOL ax_horiz

DEFINE SYMBOL xaxis_min = ($xax_min_save)
DEFINE SYMBOL xaxis_max = ($xax_max_save)

! --------------- End of LAS_draw_multi_line_plot_e  ---------------------------

