! reset_labels.jnl
! remove lots of labels as they are relocated by the scripts
! Define symbols used by the annotations xml
! 1/2018 issue 1854 Inside of a SHADE/SET, LET commands that redefine a variable can
! cause curvi coord data to be wiped out. As a workaround, cancel any vars we define
! 3/2020 Issue 1773 If the data is really monthly or yearly, can configure with the
!        ferret property <timelab_res>month</timelab_res> or <timelab_res>year</timelab_res> 
!        Here, implement a hack for datasets with timesteps defined e.g. 
!        as month-since 1-Jan-1990 so timesteps fall near the startor end of month 
!        THEN a request for Nov-1990 gets the timestep at 31-Oct-1990:15:00

IF ($xform_dms"0|*>1") THEN PPL XFOR (($xform_dms))
IF ($yform_dms"0|*>1") THEN PPL YFOR (($yform_dms))
IF ($axlab_command"0|*>1") THEN PPL ($axlab_command)
IF ($tnmlab_command"0|*>1") THEN PPL ($tnmlab_command)  
IF ($axtic_command"0|*>1") THEN PPL ($axtic_command)
IF ($axlen_command"0|*>1") THEN PPL ($axlen_command)
IF ($axtype_command"0|*>1") THEN PPL ($axtype_command)
if ($labnum_dset"0|*>1") THEN go unlabel ($labnum_dset)
if ($labnum_datitl"0|*>1") THEN go unlabel ($labnum_datitl)
if ($labnum_dods"0|*>1") THEN go unlabel ($labnum_dods)
IF `STRLEN("($ferret_view)") EQ 2` THEN
   IF `($ppl$zmin"0") GE 1.e35 AND ($ppl$zmax"0") LE -1.e35` THEN DEFINE SYMBOL nokey = 1
   IF `($nokey"0") EQ 0` THEN 
      PPL SHAKEY 1,,,,,,,,,,1
      IF `($ferret_key_annotate"1") EQ 0` THEN keymark 0
   ENDIF
ENDIF

IF `($data_count"1") EQ 1` THEN
   IF ($labnum_x"0|*>1") THEN 
      DEFINE SYMBOL longitude_lab ($lab($labnum_x))
   ENDIF
   IF ($labnum_y"0|*>1") THEN 
      DEFINE SYMBOL latitude_lab ($lab($labnum_y))
   ENDIF
   IF ($labnum_z"0|*>1") THEN
      DEFINE SYMBOL depth_lab ($lab($labnum_z))
   ENDIF
   IF ($labnum_t"0|*>1") THEN
      DEFINE SYMBOL time_lab ($lab($labnum_t))

! HACK: does the time match the region request down to month/year?
      IF `($ferret_timelab_res"0|month>1|*>0") AND \
       STRCMP("($region_t_lo)", "($region_t_hi)") EQ 0`  THEN
         LET month = SUBSTRING("($time_lab)", 1, 3)
	 LET dash = STRINDEX("($REGION_T_LO)", "-")
	 LET rmonth = SUBSTRING ("($REGION_T_LO)", dash+1, 3)
         IF `STRCMP(month, rmonth) NE 0` THEN
	    LET ryear = SUBSTRING ("($REGION_T_LO)", dash+5, 4)
	    DEFINE SYMBOL time_lab = TIME : `UPCASE(rmonth)`-`ryear`
	 ENDIF
      ENDIF
      IF `($ferret_timelab_res"0|year>1|*>0") AND \
       STRCMP("($region_t_lo)", "($region_t_hi)") EQ 0`  THEN
	 LET dash = STRINDEX("($REGION_T_LO)", "-")
	 LET ryear = SUBSTRING ("($REGION_T_LO)", dash+5, 4)
         IF `STRCMP("($time_lab)", ryear) NE 0` THEN
	    DEFINE SYMBOL time_lab = TIME : `ryear`
	 ENDIF
      ENDIF
   ENDIF

   IF ($labnum_calend"0|*>1") THEN
      DEFINE SYMBOL calendar_lab ($lab($labnum_calend))
   ENDIF
   IF ($labnum_year"0|*>1") THEN
      DEFINE SYMBOL year_lab ($lab($labnum_year))
   ENDIF

! Get the E-axis label number(s) and/or name(s). If we do both, then the text should say two different 
! things, Ensemble Member and Ensemble Name or Ensemble Label.
   REPEAT/RANGE=1:($labnum_dset"0")/NAME=lnum (\
     DEFINE SYMBOL lnum = `lnum`;\
     SHOW SYMBOL lab($lnum);\
     IF `STRINDEX("($lab($lnum))", "E :") EQ 1` THEN DEFINE SYMBOL labnum_e = ($lnum) )

   IF ($labnum_e%0|*>1%) THEN
!      DEFINE SYMBOL ensemble_lab Ensemble Member ($region_e_lo)
      
      IF `varnames,RETURN=defined` THEN CANCEL VAR varnames
      IF `labels,RETURN=defined` THEN CANCEL VAR labels
      LET varnames = ..varnames
      IF `IS_ELEMENT_OF_STR_N(varnames, "labels") EQ 0` THEN LET labels = _m[ge=($data_var)]

      DEFINE SYMBOL ensemble_names Ensemble Member: `labels[e=($region_e_lo)]`
      IF `($region_e_hi) NE ($region_e_lo)` THEN 
!         DEFINE SYMBOL ensemble_lab Ensemble Members ($region_e_lo): ($region_e_hi)
         DEFINE SYMBOL ensemble_names Ensemble Members `labels[e=($region_e_lo)]`
	 REPEAT/E=`($region_e_lo)+1`:($region_e_hi) (\
	     DEFINE SYMBOL ensemble_names ($ensemble_names) `labels` )
      ENDIF
   ENDIF
   IF ($ferret_view"0|e>1|*>0") THEN
      IF ($variable_lab"0|*>1") THEN DEFINE SYMBOL variable_lab = ($variable_lab) vs Ensemble Member
   ENDIF
ENDIF


IF ($ferret_annotations"0|*>1") THEN 
   PPL TITLE
   PPL XLAB
   PPL YLAB
   IF ($labnum_x"0|*>1") THEN GO unlabel ($labnum_x)
   IF ($labnum_y"0|*>1") THEN GO unlabel ($labnum_y)
   IF ($labnum_z"0|*>1") THEN GO unlabel ($labnum_z)
   IF ($labnum_t"0|*>1") THEN GO unlabel ($labnum_t)
   IF ($labnum_calend"0|*>1") THEN GO unlabel ($labnum_calend)
   GO unlabel 1  ! Just for good measure
   GO unlabel 2
   GO unlabel 3
   GO unlabel 4
   GO unlabel 5
   GO unlabel 6
   GO unlabel 7
ENDIF

IF ($ferret_annotations"0|*>1") THEN GO key_at_top

IF `add_urls,RETURN=defined` THEN CANCEL VAR add_urls

IF `STRCMP("($data_0_dataset_name)", "($data_0_dataset_url)") EQ 0` THEN 
   LET add_urls = 0
ELSE 
   LET add_urls = 1
ENDIF

DEFINE SYMBOL dataset_title_lab = DATASET: ($data_0_dataset_name)


!  ----------------- End of reset_labels.jnl ------------------------------------
