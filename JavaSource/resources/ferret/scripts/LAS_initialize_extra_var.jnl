! LAS_initialize_extra_var.jnl

! 10/18/2017 ACM
! Initialize another variable for prop-prop style data listings.
! Set the region and dataset t0 be the same as for var 0 (for now anyway)
!
! Cancel the dataset so that another application of the
! init script will work.
!
! 10/2017 For extra variables beyond 3, make the definitions of intermediate vars unique.


GO LAS_initialize_region ($list_var)
DEFINE SYMBOL data_($list_var)_url = ($data_0_url)

IF `($data_($list_var)_url"($data_0_url)|($data_0_url)>1|*>0") AND \
 ($ferret_init_script"0|*>1")` THEN CANCEL DATA ($data_num)

GO LAS_initialize_data ($list_var)

IF ($error_status"0|*>1") THEN
  MESSAGE/ERROR **ERROR ($error_string)
  EXIT/PROGRAM
ENDIF

! Here is variable ($list_var) title 
DEFINE SYMBOL ferret_title_($list_var) = ($ferret_plot_title"($data_($list_var)_var)")
DEFINE SYMBOL ferret_plot_var_($list_var) = ($($fer_sym)_var)
DEFINE SYMBOL ferret_plot_var = ($($fer_sym)_var)

LET/TITLE="($ferret_title_($list_var))" zvar($list_var) = XSEQUENCE(($ferret_plot_var_($list_var)))


! Is it a varible on the E axis of a DSG file?

DEFINE SYMBOL vshape = `($ferret_plot_var_($list_var)),RETURN=shape`
IF `($vshape"0|E>1|*>0)` THEN

! Is it a Profile or Trajectory dataset?
   IF `($rowvar"0|*>1") EQ 0` THEN GO LAS_setup_dsg
   IF ($rowvar"0|*>1") THEN DEFINE SYMBOL rowvar = rowsize

! Expand trajectory variable into obs axis
   DEFINE SYMBOL vtype = 0
   DEFINE SYMBOL vtype = `zvar($list_var),RETURN=dtype`
   DEFINE SYMBOL is_string = ($vtype"0|CHAR>1|*>0")

   IF ($is_string"0|0|*>1") THEN 
      LET zvar($list_var) = EXPND_BY_LEN_STR(($ferret_plot_var_($list_var)), ($rowvar),`($rowvar)[m=@sum]`)

! Set string var to a sequence number, unless its the qc_flag or region id.

	 IF `(($data_($list_var)_var_in"0|region_ID>1|qc_flag>1|*>0") EQ 0) AND ($its_prop_plot"0|*>1")` THEN 
	    LET mvar_($list_var) = _m[ge=($ferret_plot_var_($list_var))]
	    LET zvar($list_var) = EXPND_BY_LEN(mvar_($list_var), ($rowvar),`($rowvar)[m=@sum]`)
	 ELSE
	    LET zvar($list_var) = EXPND_BY_LEN(($ferret_plot_var_($list_var)), ($rowvar),`($rowvar)[m=@sum]`)
	 ENDIF
   ELSE
      LET zvar($list_var) = EXPND_BY_LEN(($ferret_plot_var_($list_var)), ($rowvar),`($rowvar)[m=@sum]`)
   ENDIF

ENDIF

! ----- End of LAS_initialize_extra_var.jnl ------------------------------------------------
