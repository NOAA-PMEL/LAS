!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! LAS_results.jnl
!
! $Author: ansley $
! $Date: 2006/07/13 18:08:18 $
! 3/30/2007 ACM When writing the map_scale.xml file, first list the urls separately;
!               long URLs can make the Ferret command too long.
! 2/11/2008 ACM Add DATA_MIN, DATA_MAX and DATA_EXISTS to the map scale file for 2D plots. 
! 5/2010    ACM Add axis direction symbols for 1D plots: axis_vert or axis_horiz give the 
!               indep axis, use D for the dep axis.
!              
! 5/2010    ACM Write time range in formatted time strings if a time axis.
! 4/2012    ACM Testing removal of the URL and variables from the map-scale file.
! .......................................................................................
!
! LAS_results.jnl creates the result files. 
!
! The argument is the refmap type "box", "xline", "yline", or "point"

! Result "plot_image" is the the image file result_plot_image_filename

! For Google Earth plots, use transparency for white background if the 
! ferret version has that option available.

IF `($its_GE"0|*>1") AND ($ferret_version) GE 6.12` THEN DEFINE SYMBOL trans = /TRANS

IF ($result_plot_image_filename%0|*>1%) THEN FRAME($trans)/FORMAT=gif/FILE="($result_plot_image_filename)"

! Result map_scale_file is output map scale file
! Write the output immediately after drawing the plot; it uses symbols 
! that are set when making the plot. If a colorbar is made, do that after the
! map_scale file is written.
 
! X region for plot does not apply to the abstract axis for 
! the variables SYMS_NAMES, SYMS_VALS, so cancel region
! assume we are running Ferret v5.81 + with /NOROWLAB qualifier on LIST command

cancel region 

IF ($have_las_plotvar"0") THEN 
   LET the_plot_var = las_plotvar  ! from LAS_auto_levels
ELSE
   IF ($native_curvilinear_xy"0|0|*>1") THEN 
      LET the_plot_var = ($ferret_plot_base_var_noregion)
   ELIF ($do_hybrid_z"0|0|*>1") THEN
      LET the_plot_var = ($ferret_plot_base_var_noregion)
   ELIF ($native_Z_plot"0|0|*>1") THEN
      LET the_plot_var = ($ferret_var_0)
   ELIF ($ferret_plot_var"0|0|*>1") THEN
      LET the_plot_var = ($ferret_plot_var)
      IF ($ferret_plot_var"0|sampled_var>1|*>0) THEN LET the_plot_var = ($ferret_plot_var)
   ELSE
      IF ($basemap_url"0|0|*>1") THEN
         USE ($basemap_url)
         LET the_plot_var = ($basemap_var)
      ENDIF
   ENDIF
ENDIF

! First list the urls and variable names; the URLs can be so long they make the
! commands too long for the Ferret command-line buffer. 

! Always write the map_scale file.

!Do not write out the url and variable name output. This info is not saved
!when map_scale.xml is generated anyway. (See ferret ticket 1933)

   IF `($xstride"0|*>1) EQ 0` THEN DEFINE SYMBOL xstride = 1 
   IF `($ystride"0|*>1) EQ 0` THEN DEFINE SYMBOL ystride = 1

   LET syms_names = {\
     "PPL$XMIN", "PPL$XMAX", \
      "PPL$YMIN", "PPL$YMAX",\
      "PPL$XPIXEL", "PPL$YPIXEL",\
      "PPL$WIDTH", "PPL$HEIGHT",\
      "PPL$XORG","PPL$YORG",\
      "PPL$XLEN","PPL$YLEN",\
      "XAXIS_MIN","XAXIS_MAX",\
      "YAXIS_MIN","YAXIS_MAX",\
      "VP_TOP_MARGIN","VP_RT_MARGIN",\
      "XSTRIDE","YSTRIDE"}

! When animating a Z profile, drawing the first plot: 
! The symbols XAXIS_MIN and XAXIS_MAX may not be defined.
! Set them here using the data min and max.

   DEFINE SYMBOL has_xaxis_sym = ($XAXIS_MIN"0|*>1")
   IF `($has_xaxis_sym) EQ 0` THEN
      PPL %RANGE ($PPL$XMIN1) ($PPL$XMAX1) 10
      DEFINE SYMBOL xaxis_min = ($ppl$range_low) 
      DEFINE SYMBOL xaxis_max = ($ppl$range_high)
   ENDIF

! For curvilinear grids, region_0_x_lo and region_0_x_hi may
! have been changed to another modulo longitude branch. If so
! then the x axis min and max need to be reset.
      
   DEFINE SYMBOL xaxlo = ($XAXIS_MIN)
   DEFINE SYMBOL xaxhi = ($XAXIS_MAX)

   IF ($region_0_x_lo_orig"0|*>1") THEN
      IF `($region_0_x_lo_orig) - ($XAXIS_MIN) LT (-350)` THEN 
         DEFINE SYMBOL xaxlo = `($XAXIS_MIN) - 360`
         DEFINE SYMBOL xaxhi = `($XAXIS_MAX) - 360`
      ELIF `($region_0_x_lo_orig) - ($XAXIS_MIN) GT   350 ` THEN 
         DEFINE SYMBOL xaxlo = `($XAXIS_MIN) + 360`
         DEFINE SYMBOL xaxhi = `($XAXIS_MAX) + 360`
      ENDIF
   ENDIF

   DEFINE SYMBOL taxis_dir = 0

   LET syms_vals = {\
      "($PPL$XMIN)", "($PPL$XMAX)", \
      "($PPL$YMIN)", "($PPL$YMAX)", \
      "($PPL$XPIXEL)", "($PPL$YPIXEL)",\
      "($PPL$WIDTH)", "($PPL$HEIGHT)",\
      "($PPL$XORG)", "($PPL$YORG)",\
      "($PPL$XLEN)", "($PPL$YLEN)",\
      "($xaxlo)", "($xaxhi)",\
      "($YAXIS_MIN)", "($YAXIS_MAX)",\
      "($VP_TOP_MARGIN)","($VP_RT_MARGIN)",\
      "($xstride)","($ystride)"}

   IF `($XAXIS_MIN"0|*>1) EQ 0 OR ($YAXIS_MIN"0|*>1) EQ 0 ` THEN LET syms_vals = {\
      "($PPL$XMIN)", "($PPL$XMAX)", \
      "($PPL$YMIN)", "($PPL$YMAX)", \
      "($PPL$XPIXEL)", "($PPL$YPIXEL)",\
      "($PPL$WIDTH)", "($PPL$HEIGHT)",\
      "($PPL$XORG)", "($PPL$YORG)",\
      "($PPL$XLEN)", "($PPL$YLEN)",\
      "($PPL$XMIN1)", "($PPL$XMAX1)", \
      "($PPL$YMIN1)", "($PPL$YMAX1)", \
      "($VP_TOP_MARGIN)","($VP_RT_MARGIN)",\
      "($xstride)","($ystride)"}

! assume we are running Ferret v5.81 or higher with /NOROWLAB qualifier on LIST command
   LIST/CLOBBER/FILE="($result_map_scale_filename)"/NOHEAD/NOROWLAB syms_names, syms_vals

! If symbols for formatted time-axis ends have been defined, write those.
! These are used by correlation plots.
! If a plot axis is a time axis, write its formatted-date ends. Line plots
! have symbols such as ppl$xmin1, 2D plots will have xaxis_min etc.

   IF ($HAXIS_tstart"0|*>1") THEN
      LET syms_names = {"HAXIS_TSTART", "HAXIS_TEND"}
      LET syms_vals = {"($HAXIS_TSTART)", "($HAXIS_TEND)"}
      LIST/APPEND/FILE="($result_map_scale_filename)"/NOHEAD/NOROWLAB syms_names, syms_vals
   ELSE
      IF ($ax_horiz"0|T>1|*>0") THEN
         LET tt= t[GT=($data_0_var),d=($dset0%1%)]
	 LET t1 = tboxlo[gt=tt,t=($xaxis_min%($ppl$xmin1)%)]
	 LET t2 = tboxhi[gt=tt,t=($xaxis_max%($ppl$xmax1)%)]
	 DEFINE SYMBOL haxis_tstart = `TAX_DATESTRING(t1, tt, "seconds")`
	 DEFINE SYMBOL haxis_tend = `TAX_DATESTRING(t2, tt, "seconds")`
         LET syms_names = {"HAXIS_TSTART", "HAXIS_TEND"}
         LET syms_vals = {"($HAXIS_TSTART)", "($HAXIS_TEND)"}
         LIST/APPEND/FILE="($result_map_scale_filename)"/NOHEAD/NOROWLAB syms_names, syms_vals
      ENDIF
   ENDIF

   IF ($VAXIS_tstart"0|*>1") THEN
      LET syms_names = {"VAXIS_TSTART", "VAXIS_TEND"}
      LET syms_vals = {"($VAXIS_TSTART)", "($VAXIS_TEND)"}
      LIST/APPEND/FILE="($result_map_scale_filename)"/NOHEAD/NOROWLAB syms_names, syms_vals
   ELSE
      IF ($ax_vert"0|T>1|*>0") THEN
         LET tt= t[GT=($data_0_var),d=($dset0%1%)]
	 LET t1 = tboxlo[gt=tt,t=($yaxis_min%($ppl$ymin1)%)]
	 LET t2 = tboxhi[gt=tt,t=($yaxis_max%($ppl$ymax1)%)]
	 DEFINE SYMBOL vaxis_tstart = `TAX_DATESTRING(t1, tt, "seconds")`
	 DEFINE SYMBOL vaxis_tend = `TAX_DATESTRING(t2, tt, "seconds")`
         LET syms_names = {"VAXIS_TSTART", "VAXIS_TEND"}
         LET syms_vals = {"($VAXIS_TSTART)", "($VAXIS_TEND)"}
         LIST/APPEND/FILE="($result_map_scale_filename)"/NOHEAD/NOROWLAB syms_names, syms_vals
      ENDIF
   ENDIF

! Assume we are running Ferret v6.07+ or higher with symbols AX_HORIZ and AX_VERT 
! defined for 2D plots. Write to the XML the horizontal and vertical axis and any
! attribute positive=
! TODO: Question - do we want a line for a possible positive= attribute to always
!       be written or only when that attribute exists??

! This section assumes there is a variable "the_plot_var" which is
! defined by standard LAS scripts but not for all customized interfaces.

   IF `($ferret_plot_var"0|*>1") EQ 1` THEN 

   IF `($ax_horiz"0|*>1") OR ($ax_vert"0|*>1")`  THEN 
      LET syms_names = {"AX_HORIZ", "AX_HORIZ_POSTV", "AX_VERT", "AX_VERT_POSTV", \
      "DATA_EXISTS", "DATA_MIN", "DATA_MAX"}

! Find the axis directions

! Remove striding so we can get the attributes of axes

      IF `($native_curvilinear_xy"0") EQ 0` THEN 

      IF ($ax_horiz"0|*>1") THEN
         DEFINE SYMBOL the_axis =  `the_plot_var,RETURN=($ax_horiz)axis`
         IF `STRINDEX("($the_axis)", "(") GT 0` THEN 
	    IF `($($ax_horiz)stride"0") GT 1` THEN 
	       CANCEL AXIS/STRIDE `($ax_horiz)_strided_var,RETURN=($ax_horiz)axis`
               DEFINE SYMBOL the_axis =  `($data_var),RETURN=($ax_horiz)axis`
	       IF `STRINDEX("($the_axis)", "(") EQ 0` THEN
                  LET attlist = (($the_axis)).attnames
	       ELSE
                  LET attlist = ($the_axis).attnames
	       ENDIF
	    ENDIF
         ELSE
            LET attlist = (($the_axis)).attnames
         ENDIF
	 IF `STRINDEX("($the_axis)", "(") EQ 0` THEN \
	   DEFINE SYMBOL the_axis = (($the_axis))
         IF `IS_ELEMENT_OF_STR(attlist, "positive") GT 0` THEN \
         DEFINE SYMBOL AX_HORIZ_POSTV = `($the_axis).positive`
      ELSE
         DEFINE SYMBOL ax_horiz = D
      ENDIF

      IF ($ax_vert"0|*>1") THEN
         DEFINE SYMBOL the_axis =  `the_plot_var,RETURN=($ax_vert)axis`
         IF `STRINDEX("($the_axis)", "(") GT 0` THEN 
	    IF `($($ax_vert)stride"0") GT 1` THEN 
	       CANCEL AXIS/STRIDE `($ax_vert)_strided_var,RETURN=($ax_vert)axis`
               DEFINE SYMBOL the_axis =  `($data_var),RETURN=($ax_vert)axis`
	       IF `STRINDEX("($the_axis)", "(") EQ 0` THEN
                  LET attlist = (($the_axis)).attnames
	       ELSE
                  LET attlist = ($the_axis).attnames
	       ENDIF
            ENDIF
         ELSE
            LET attlist = (($the_axis)).attnames
         ENDIF
	 IF `STRINDEX("($the_axis)", "(") EQ 0` THEN \
	   DEFINE SYMBOL the_axis = (($the_axis))
         IF `IS_ELEMENT_OF_STR(attlist, "positive") GT 0` THEN \
         DEFINE SYMBOL AX_VERT_POSTV = `($the_axis).positive`
      ELSE
         DEFINE SYMBOL ax_vert = D
      ENDIF
      
      ENDIF  ! curvilinear
      IF ($ax_horiz"0|*>1") THEN DEFINE SYMBOL ax_horiz = `DNCASE("($ax_horiz)")`
      IF ($ax_vert"0|*>1") THEN DEFINE SYMBOL ax_vert = `DNCASE("($ax_vert)")`

* Find the data min, max.  If all data was bad, send blank strings to the output.

      IF `($data_plotted_min"1.E38") EQ pplus_default_zmin` THEN 
         DEFINE SYMBOL DATA_EXISTS = 0
         CANCEL SYMBOL DATA_MIN
         CANCEL SYMBOL DATA_MAX
      ELSE
         DEFINE SYMBOL DATA_EXISTS = 1
         DEFINE SYMBOL DATA_MIN = ($data_plotted_min)
         DEFINE SYMBOL DATA_MAX = ($data_plotted_max)
	 IF ($data_max"0|bad>1|1.E+34>1|*>0") THEN 
	    CANCEL SYMBOL data_min
	    CANCEL SYMBOL data_max
	    DEFINE SYMBOL DATA_EXISTS = 0
	 ENDIF
	 IF ($data_min"0|bad>1|1.E+34>1|*>0") THEN 
	    CANCEL SYMBOL data_min
	    CANCEL SYMBOL data_max
	    DEFINE SYMBOL DATA_EXISTS = 0
	 ENDIF
      ENDIF

      IF ($its_prop_prop_plot"0|*>1") THEN
          DEFINE SYMBOL ax_horiz = ($data_0_id)
	  DEFINE SYMBOL ax_vert = ($data_1_id)
      ENDIF

      LET syms_vals = {"($AX_HORIZ)", "($AX_HORIZ_POSTV)", "($AX_VERT)", "($AX_VERT_POSTV)",\
       "($DATA_EXISTS)", "($DATA_MIN)", "($DATA_MAX)"}
      
      
      LIST/APPEND/FILE="($result_map_scale_filename)"/NOHEAD/NOROWLAB syms_names, syms_vals
   ENDIF  ! ax_horiz ax_vert defined
   ENDIF  ! ferret_plot_var defined


! If it was a color plot write the string needed to put into a /LEVELS qualifier,
! to duplicate the color levels from this plot.

   IF ($LEV_NUM"0|*>1") THEN
      LET syms_names = "LEVELS_STRING"
      IF ($LEV_OPNLEVS"0|*>1") THEN 
         LET syms_vals = "($LEV_OPNLEVS)"
      ELIF `(($LEV_MIN"0|*>1") + ($LEV_MAX"0|*>1") + ($LEV_DEL"0|*>1")) EQ 3` THEN
         IF `STRCMP("($LEV_DEL%1%)", "none") EQ 0` THEN DEFINE SYMBOL lev_del = 1
         LET syms_vals = "(($LEV_MIN),($LEV_MAX),($LEV_DEL))"
      ELSE
         LET syms_vals = "($LEV_TEXT%v%)"
      ENDIF
      LIST/APPEND/FILE="($result_map_scale_filename)"/NOHEAD/NOROWLAB syms_names, syms_vals
   ENDIF


! Done with map scale file

IF ($ferret_annotations%0|*>1%) THEN
   GO LAS_annotations_xml
ENDIF 

! Result "colorbar" is the legend of the plot_image...only for Google Earth
IF ($result_colorbar_filename%0|*>1%) THEN GO colorbar

! Result ref_map is a ref map. Save to result_ref_map_filename

IF  ($ferret_use_ref_map%0|false>0|*>1%) THEN 
  CAN VIEW
  SET VIEW full
  GO std_refmap ($region_0_x_lo) ($region_0_x_hi) ($region_0_y_lo) ($region_0_y_hi) $1%box% ($ref_magnify%0%) ($ref_xcompress%0%) ($ref_ycompress%0%)
  FRAME/FORMAT=gif/FILE="($result_ref_map_filename)"
ENDIF

! End of file ------------LAS_results.jnl--------------------------
