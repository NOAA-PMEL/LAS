! color_datekey.jnl
!
! set up date/time labels for the color key when a plot is colored by a time variable.
! Arguments 
!  1 variable containing a variable on a time axis corresponding to the 
!    colored-by-time data, e.g.  LET timepts = t[gt=timeaxis]
!  2 optional: hr, min, sec to force hour:min and/or :sec on the time labels.

LET tpts = $1
LET/QUIET PLMlabel = $2"0|hr>1|min>2|sec>3|*>0"

LET TLAB_tstart = `tpts[L=@min]`
DEFINE SYMBOL TLAB_tstart = `TLAB_tstart`

LET TLAB_tend = `tpts[L=@max]`
DEFINE SYMBOL TLAB_tend = `TLAB_tend`

LET TLAB_tstep = int((`TLAB_tend`-`TLAB_tstart`)/4)
DEFINE SYMBOL TLAB_tstep = `TLAB_tstep`

LET dates_same = 0

LET ttime = `($TLAB_tstart)+0*($TLAB_tstep)`
DEFINE SYMBOL day_0_fourth = `TAX_DATESTRING(ttime, tpts, "day")`

LET ttime = `($TLAB_tstart)+1*($TLAB_tstep)`
DEFINE SYMBOL day_1_fourth = `TAX_DATESTRING(ttime, tpts, "day")`
IF `STRCMP("($day_1_fourth)", "($day_0_fourth)") EQ 0` THEN LET dates_same = 1

LET ttime = `($TLAB_tstart)+2*($TLAB_tstep)`
LET ttime_last = `ttime`
DEFINE SYMBOL day_2_fourth = `TAX_DATESTRING(ttime, tpts, "day")`
IF `STRCMP("($day_2_fourth)", "($day_1_fourth)") EQ 0` THEN LET dates_same = 1

LET ttime = `($TLAB_tstart)+3*($TLAB_tstep)`
LET ttime_last = `ttime`
DEFINE SYMBOL day_3_fourth = `TAX_DATESTRING(ttime, tpts, "day")`
IF `STRCMP("($day_3_fourth)", "($day_2_fourth)") EQ 0` THEN LET dates_same = 1

! The last section may be degenerate depending on the 
! underlying time axis. Force it not to be, within
! single-precision time coordinates

LET ttime = `($TLAB_tstart)+4*($TLAB_tstep)`
IF `ttime LE ttime_last` THEN 
   LET ttime = `(1. - 1.e-5)* ($TLAB_tend) - 1`
ENDIF 
DEFINE SYMBOL day_4_fourth = `TAX_DATESTRING(ttime, tpts, "day")`
IF `STRCMP("($day_4_fourth)", "($day_3_fourth)") EQ 0` THEN LET dates_same = 1

! --------
! If some of the date symbols were repeated, add hours:minutes on a
! second line under the lables.

IF `dates_same GT 0` THEN
   IF `PLMlabel LT 2` THEN LET PLMlabel = 2

   LET ttime = `($TLAB_tstart)+0*($TLAB_tstep)`
   LET dayfrac = `TAX_DAYFRAC(ttime, tpts)`
   IF `PLMlabel GT 0` THEN
      GO pdatekey_dms
      DEFINE SYMBOL day_0_fourth = ($day_0_fourth)($dms)
   ENDIF
   
   LET ttime = `($TLAB_tstart)+1*($TLAB_tstep)`
   LET dayfrac = `TAX_DAYFRAC(ttime, tpts)`
   IF `PLMlabel GT 0` THEN
      GO pdatekey_dms
      DEFINE SYMBOL day_1_fourth = ($day_1_fourth)($dms)
   ENDIF
   
   LET ttime = `($TLAB_tstart)+2*($TLAB_tstep)`
   LET dayfrac = `TAX_DAYFRAC(ttime, tpts)`
   IF `PLMlabel GT 0` THEN
      GO pdatekey_dms
      DEFINE SYMBOL day_2_fourth = ($day_2_fourth)($dms)
   ENDIF
   
   LET ttime = `($TLAB_tstart)+3*($TLAB_tstep)`
   LET dayfrac = `TAX_DAYFRAC(ttime, tpts)`
   IF `PLMlabel GT 0` THEN
      GO pdatekey_dms
      DEFINE SYMBOL day_3_fourth = ($day_3_fourth)($dms)
   ENDIF
   
   ! The last section may be degenerate depending on the 
   ! underlying time axis. Force it not to be, within
   ! single-precision time coordinates
   
   LET ttime = `($TLAB_tstart)+4*($TLAB_tstep)`
   IF `ttime LE ttime_last` THEN 
      LET ttime = `(1. - 1.e-5)* ($TLAB_tend) - 1`
   ENDIF 
   LET dayfrac = `TAX_DAYFRAC(ttime, tpts)`
   IF `PLMlabel GT 0` THEN
      GO pdatekey_dms
      DEFINE SYMBOL day_4_fourth = ($day_4_fourth)($dms)
   ENDIF

ENDIF

! --------
IF ($horizontal_key"0|*>1") THEN
DEFINE SYMBOL datesize = 0.09
IF `dates_same GT 0` THEN  DEFINE SYMBOL datesize = 0.07

LET vht = (($ppl$ylen) + ($ppl$yorg)) * 1.15
IF ($vp_height"0|*>1") THEN LET vht = ($vp_height)

LET rem = vht - (($ppl$ylen) + ($ppl$yorg))
LET ymaxx = 0.22*($ppl$ylen)
LET key_hi = MIN (`($ppl$yorg) + ($ppl$ylen) + ymaxx`,`vht-0.051*rem`)

LET key_lo = (($ppl$yorg) + ($ppl$ylen) + 0.22*MIN (rem,ymaxx))
LET key_lo = (($ppl$ylen) + 0.22*MIN (rem,ymaxx))
LET key_inc = `0.1 * (key_hi - key_lo) + 0.05`

LET key_hi = `key_lo + 4.0*key_inc`
LET key_hi = `key_lo + 2.0*key_inc`

let keylen = `x2-x1`
let keydel = keylen/4
let keystart = x1 - ($ppl$xorg)

LABEL/NOUSER `keydel*0+keystart`,`key_hi`,0,0,($datesize),($day_0_fourth)
LABEL/NOUSER `keydel*0+keystart`,`key_lo+key_inc`,0,0,.07,l

LABEL/NOUSER `keydel*1+keystart`,`key_hi`,0,0,($datesize),($day_1_fourth)
LABEL/NOUSER `keydel*1+keystart`,`key_lo+key_inc`,0,0,.07,l

LABEL/NOUSER `keydel*2+keystart`,`key_hi`,0,0,($datesize),($day_2_fourth)
LABEL/NOUSER `keydel*2+keystart`,`key_lo+key_inc`,0,0,.07,l

LABEL/NOUSER `keydel*3+keystart`,`key_hi`,0,0,($datesize),($day_3_fourth)
LABEL/NOUSER `keydel*3+keystart`,`key_lo+key_inc`,0,0,.07,l

LABEL/NOUSER `keydel*4+keystart`,`key_hi`,0,0,($datesize),($day_4_fourth)
LABEL/NOUSER `keydel*4+keystart`,`key_lo+key_inc`,0,0,.07,l

ELSE ! vertical key

DEFINE SYMBOL datesize = 0.1
   
! Based on what key.F does to define the upper x corner of the colorbar key.
! If we are in a viewport, start with vp_width
! 10/2011 change size of labels to 0.1

LET vwid = (($ppl$xlen) + ($ppl$xorg)) * 1.15
IF ($vp_width"0|*>1") THEN LET vwid = ($vp_width)

LET rem = vwid - (($ppl$xlen) + ($ppl$xorg))
LET xmaxx = 0.25*($ppl$xlen)
LET key_hi = MIN (`($ppl$xorg) + ($ppl$xlen) + xmaxx`,`vwid-0.051*rem`)

LET key_lo = (($ppl$xorg) + ($ppl$xlen) + 0.25*MIN (rem,xmaxx))
LET key_lo = (($ppl$xlen) + 0.25*MIN (rem,xmaxx))
LET key_inc = `0.1 * (key_hi - key_lo)`

LET key_hi = `key_lo + 4.0*key_inc`
LET key_hi = `key_lo + 2.0*key_inc`

LABEL/NOUSER `key_hi`,`(($ppl$ylen)*0)/4-.05`,-1,0,($datesize),($day_0_fourth)
LABEL/NOUSER `key_hi`,`(($ppl$ylen)*1)/4-.05`,-1,0,($datesize),($day_1_fourth)
LABEL/NOUSER `key_hi`,`(($ppl$ylen)*2)/4-.05`,-1,0,($datesize),($day_2_fourth)
LABEL/NOUSER `key_hi`,`(($ppl$ylen)*3)/4-.05`,-1,0,($datesize),($day_3_fourth)
LABEL/NOUSER `key_hi`,`(($ppl$ylen)*4)/4-.05`,-1,0,($datesize),($day_4_fourth)

ENDIF

