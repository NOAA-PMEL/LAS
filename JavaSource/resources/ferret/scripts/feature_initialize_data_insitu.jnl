!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! feature_initialize_data_insitu.jnl
!
! 
! Author: ansley 
! 9/2012 (from Jons_initialize_data_insitu.jnl)
! 10/4/2016 fixes so init scripts still work
!  4/3/2017 *acm* Additions for point data types
!   10/2017 *acm* Make a time-labeled color key for time variables not named "time".
!   10/2017 *acm* Set up color variable in a more consistent way
!   11/2017 *acm* Remove any quotes from around variable title and units
!    3/2018 *acm* If a feature-type plot has a color palette setting, set up to use it

DEFINE SYMBOL num ($1"0")

! The dataset_id should be the ERDDAP dataset ID. Check that it's defined anf
! if not set it to the dataset_name

IF `($data_($num)_dataset_id"0|*>1") EQ 0` THEN DEFINE SYMBOL data_($num)_dataset_id = ($data_($num)_dataset_name)

! The variable at the x and y locations

IF ($data_($num)_var"0|1|*>1") THEN DEFINE SYMBOL data_var ($data_($num)_var)
IF ($data_($num)_title"0|1|*>1") THEN DEFINE SYMBOL data_title ($data_($num)_title)
! Remove any quotes surrounding data_title string
GO LAS_unquote_string data_title

IF `($data_count"0") GT 1` THEN
   REPEAT/RANGE=1:($data_count)/name=ivar (\
    DEFINE SYMBOL ivar = `ivar`;\
    GO LAS_unquote_string data_($ivar )_title;\
   )
ENDIF


IF ($data_($num)_name"0|1|*>1") THEN DEFINE SYMBOL data_name ($data_($num)_name)

DEFINE SYMBOL ferret_plot_title ($data_title)
IF ($data_($num)_units"0|1|text>0|none>0|*>1") THEN 
   DEFINE SYMBOL data_units ($data_($num)_units)
   ! Remove any quotes surrounding data_title string
   GO LAS_unquote_string data_unitss
ELSE 
   CANCEL SYMBOL data_($num)_units
ENDIF

! Text variables have units of "text". In this case don't include units with the title
! remove any labels that are degrees_east, etc, which are redundant

IF ($data_($num)_units"0|1|*>1") THEN 
   DEFINE SYMBOL data_units ($data_($num)_units)
   IF ($data_units"0|text>1|none>1|*>0") THEN CANCEL SYMBOL data_units
   IF ($data_units"0|degrees_east>1|degrees_north>1|*>0") THEN CANCEL SYMBOL data_units
   IF ($data_units"0|*>1") THEN DEFINE SYMBOL ferret_plot_title =($data_($num)_title) (($data_($num)_units))
ENDIF

DEFINE SYMBOL uptitle = `UPCASE("($ferret_plot_title%0%)")`
IF `(STRINDEX("($uptitle)", "TIME") GT 0) AND (STRINDEX("($uptitle)", " SINCE ") GT 0)` THEN \
DEFINE SYMBOL ferret_plot_title = Time


! Set up the variable title and units for single-variable plots.
! The prop-prop plot title A vs B colored by C are set elsewhere.

IF `($1"0") EQ 0` THEN 
   DEFINE SYMBOL variable_lab = ($ferret_plot_title)
   LET variable_lab =  data_title
   IF ($data_($num)_units"0|1|*>1") THEN LET variable_lab =  STRCAT(data_title, data_units)
ENDIF

! SET DATA and check for errors
!!!!  These are sometimes filled in with a tabledap address not the netCDF file.  Keep it using data_0_url.
!!!! SET DATA "($data_($num)_url)"

SET DATA "($data_0_url)"

IF ($fer_last_error"0|*>1") THEN
  DEFINE SYMBOL error_status = ERROR
  DEFINE SYMBOL error_type = DATA
  DEFINE SYMBOL error_string = The ($data_x_var) source file -- ($data_url) did not open successfully.
  EXIT/SCRIPT
ENDIF

! Get the dataset title
LET global_attnames = ..attnames
IF `IS_ELEMENT_OF_STR(global_attnames, "title") GT 0` THEN
  DEFINE SYMBOL dataset_title_att = `..title`
ENDIF


! Is there a custom palette?  This may be reset in custom_dataset_definitions.jnl below

IF `($ferret_insitu_palette"0|*>1")` THEN DEFINE SYMBOL ferret_palette =  ($ferret_insitu_palette)
IF `($ferret_Point_palette"0|*>1")` THEN DEFINE SYMBOL ferret_palette =  ($ferret_Point_palette)
IF `($ferret_Profile_palette"0|*>1")` THEN DEFINE SYMBOL ferret_palette =  ($ferret_Profile_palette)
IF `($ferret_Timeseries_palette"0|*>1")` THEN DEFINE SYMBOL ferret_palette =  ($ferret_Timeseries_palette)
IF `($ferret_TrajectoryProfile_palette"0|*>1")` THEN DEFINE SYMBOL ferret_palette =  ($ferret_TrajectoryProfile_palette)
IF `($ferret_Trajectory_palette"0|*>1")` THEN DEFINE SYMBOL ferret_palette =  ($ferret_Trajectory_palette)


GO custom_dataset_definitions

! Did the request return the no-data flag?
LET global_attnames = ..attnames
LET nglobal_atts = ..nattrs
IF `nglobal_atts GT 0` THEN
   IF `IS_ELEMENT_OF_STR(global_attnames, "query_result") GT 0` THEN
      DEFINE SYMBOL data_query_string = `..query_result`
      IF `STRINDEX("($data_query_string)", "No data found") GT 0` THEN

! Set dataset annotations 
! Do not annotate with variable - it's the location/time that's unavailable
   
         GO LAS_url_label 0
         CANCEL SYMBOL variable_lab 

! This script makes a blank plot, labeled with a message, calls LAS_results, and exits
         GO feature_abort_no_data
      ENDIF

   ENDIF
ENDIF

DEFINE SYMBOL dsetnum = `($data_0_var),RETURN=dsetnum`

! Is it a Profile or Trajectory dataset?
GO LAS_setup_dsg

IF `($its_listing"0|*>1") AND ($num) EQ 0` THEN 

! For ascii listings, if a variable is the same as the feature variable,
! then remove reference to the feature variable. It is always listed as part
! of the metadata.

   REPEAT/RANGE=0:($data_count)/name=ivar (\
    DEFINE SYMBOL ivar = `ivar`;\
    IF `($data_($ivar)_var"0|($feature_variable"A")>1|*>0")` THEN DEFINE SYMBOL iremove = `ivar`;\
    )

! Open the dataset. Rename the variables to varname_
! Keep original variable names as symbols data_0_var_orig, ...
! Treat longitude and latitude the same.

   GO LAS_rename_vars 0
   DEFINE SYMBOL data_var ($data_($num)_var)
ENDIF

IF ($feature_variable"0|*>1") THEN 
   IF `($have_feature_nums"0|*>1) EQ 0` THEN GO feature_initialize_vars
ENDIF

! The x and y locations for insitu plots
! Use the _CoordinateAxisType to define data_x_var, data_y_var,
! data_t_var if there is a time variable, data_z_var if there is depth.

IF ($feature_type"0|point>1|*>0") THEN 
   LET rowvar = x[gx=($DATA_VAR)]
   DEFINE SYMBOL rowvar = rowvar
   CANCEL SYMBOL its_trajectory_map
   DEFINE SYMBOL its_point_map = 1
ENDIF   

IF `($data_x_var"0|*>1") EQ 0` THEN GO feature_lon_lat_time_depth

DEFINE SYMBOL mark_end = ($data_var)

! If the plot is colored by profile ID then fake that variable, and exit

LET color_by_feature = 0
LET color_by_index = 0

IF `(($num)+1) eq ($data_count)` THEN
   IF `($data_($num)_var"0|vid>1|($feature_variable)>1|*>0") AND ($nodata"0|*>1) EQ 0` THEN 
      IF `($rowvar"0|*>1") EQ 0` THEN DEFINE SYMBOL rowvar = rowSize
      LET color_by_feature = 1
   ENDIF

! Or, is this a DSG file with the feature_variable we have already identified?

   IF `($data_($num)_var"0|($feature_variable)>1|*>0") AND ($rowvar"0|*>1")` THEN LET color_by_feature = 1
ENDIF

! Make the RowSize and other metadata variables be on the E axis.
IF `($rowvar"0|*>1") AND ($feature_type"0|point>0|*>1")` THEN 
   DEFINE SYMBOL rdir = `($rowvar),RETURN=shape`
   DEFINE SYMBOL rind = ($rdir"|X>I|E>M|POINT>M")
   LET setnum = `($rowvar),RETURN=dsetnum`
   IF ($rind"|I>1|*>0") THEN 
      LET esiz = `($rowvar),RETURN=isize`
      DEFINE AXIS/E=1:`esiz`:1 `($rowvar),RETURN=xaxis`M
      DEFINE SYMBOL eaxis = `($rowvar),RETURN=xaxis`M
      DEFINE SYMBOL rind = M
      LET varnames = ..varnames

       REPEAT/RANGE=1:`..nvars`/NAME=q\
       (DEFINE SYMBOL q = `q`;\
       DEFINE SYMBOL vname = `varnames[i=($q)]`;\
       DEFINE SYMBOl vshape = `($vname),RETURN=shape`;\
       LET vsiz =  `($vname),RETURN=size`;\
       IF `($vshape"|X>1|*>0) and vsiz EQ esiz` THEN ;\
          SET VAR/NAME=($vname)_in ($vname);\
          LET/D=`setnum` ($vname) = RESHAPE(($vname)_in, _E[ge=($eaxis)]);\
          SET ATT/LIKE=($vname)_in ($vname);\
       ENDIF;\
       )
   ENDIF
   LET longest = ($rowvar)[m=@max]+2
ENDIF

! Other variables on the E axis to be colored the same way.

IF `color_by_index EQ 1` THEN let color_by_feature = 1

IF `color_by_feature EQ 1` THEN

   USE "($data_($num)_url)"

   DEFINE SYMBOL data_($num)_var = `varnames[i=1]`
   DEFINE SYMBOL is_vid = 1
   DEFINE SYMBOL data_var_only ($data_($num)_var)
   DEFINE SYMBOL data_num `($data_($num)_var),RETURN=dsetnum`

! Create the profile variables and symbols

   GO LAS_initialize_feature_id
  
   DEFINE SYMBOL mark_end = ($data_var)

   IF ($its_prop_prop_plot"0|*>1") THEN
   
      DEFINE SYMBOL ferret_poly_levels = `($rowvar),RETURN=lsize`
      LET id_data = COMPRESSI(XSEQUENCE(TRANSPOSE_XZ(id_by_c)))
     IF ($its_listing"0|*>1") THEN
         LET/TITLE="($data_($num)_var)" var_data = ($data_($num)_var)
         DEFINE SYMBOL data_($num)_var = ($data_($num)_var)
         DEFINE SYMBOL data_var = ($data_($num)_var)
         DEFINE SYMBOL ferret_plot_var = ($data_($num)_var)
      ELSE
         LET/TITLE="($data_($num)_name%ID%)" var_data = id_data[i=1:`($data_x_var),RETURN=isize`]
         DEFINE SYMBOL data_($num)_var = var_data
         DEFINE SYMBOL data_var = var_data
         DEFINE SYMBOL ferret_plot_var = var_data
     ENDIF

      IF ($data_($num)_title"0|1|*>1") THEN DEFINE SYMBOL data_title ($data_($num)_title)

      IF ($data_($num)_name"0|1|*>1") THEN DEFINE SYMBOL data_name ($data_($num)_name)

      IF `($data_($num)_units%0|" ">0|text>0|none>0|*>1%) EQ 0` THEN CANCEL SYMBOL data_($num)_units
      IF ($data_($num)_units"0|1|*>1") THEN DEFINE SYMBOL data_units ($data_($num)_units)

      DEFINE SYMBOL ferret_plot_title ($data_title)
      IF ($data_($num)_units"0|1|*>1") THEN DEFINE SYMBOL ferret_plot_title ($data_title) ($data_($num)_units)

      DEFINE SYMBOL color_by_feature = 1

      EXIT/SCRIPT  ! dont need the rest of the setup for prop-prop plots
   ENDIF ! its_prop_prop_plot

ENDIF

! Is it a varible on the E axis of a DSG file?
IF ($its_trajectory_map"0|*>1") THEN
DEFINE SYMBOL vshape = `($data_var),RETURN=shape`
IF ($vshape"0|POINT>1|*>0") THEN 
   DEFINE SYMBOL eshape = `($data_var),return=eaxis`
   IF ($eshape"0|NORMAL>0|*>1") THEN DEFINE SYMBOL vshape = E
ENDIF

IF `($vshape"0|E>1|*>0) AND  (STRCMP("($data_var)", "($feature_variable)") NE 0)` THEN

! Is there a rowsize variable?
   IF ($rowvar"0|*>1") THEN DEFINE SYMBOL rowvar = rowsize

! Expand trajectory variable into obs axis
   DEFINE SYMBOL vtype = 0
   LET gettype = ($data_var)
   DEFINE SYMBOL vtype = `gettype,RETURN=dtype`
   DEFINE SYMBOL is_string = ($vtype"0|CHAR>1|*>0")

   IF ($is_string"0|0|*>1") THEN 
      LET/TITLE="($ferret_title_0)" data_var_expand = EXPND_BY_LEN_STR(($data_var), ($rowvar),`($rowvar)[m=@sum]`)
      LET mark_end = xsequence(_m[gm=($data_var)])
      DEFINE SYMBOL mark_end = mark_end
      DEFINE SYMBOL mark_end_m = 1
   ELSE
      LET/TITLE="($ferret_title_0)" data_var_expand = EXPND_BY_LEN(($data_var), ($rowvar),`($rowvar)[m=@sum]`)
      LET mark_end = ($data_var)
      DEFINE SYMBOL mark_end = mark_end
      DEFINE SYMBOL mark_end_m = 1
   ENDIF
   DEFINE SYMBOL data_var = data_var_expand
   
   DEFINE SYMBOL numobs = `($data_var),return = isize`
ENDIF
ENDIF

IF `($its_prop_prop_plot"0|*>1") EQ 0 and ($its_trajectory_map"0|*>1")` THEN  ! map plot, do decimation

IF ($rowvar"0|*>1") THEN 

! DSG file. Do decimation if there are over num_decimate points.

   LET num_decimate = 200000

   DEFINE SYMBOL  data_x_is_lon = 1
   
   LET full_range = 360*90
   LET max_range = ($region_x_range)* ($region_y_range)
   LET max_range = MAX(($region_x_range), ($region_y_range))

   DEFINE SYMBOL  data_x_is_lon = 1
   LET tol1 = `(max_range/full_range)`
   IF `tol1 LT 1.e-6` THEN LET tol1 = 1.e-6
   LET tol2 = `tol1`

! Tolerances 10% of the data range over the approx pixel width of a plot.

   LET tol1 = 0.1*($region_x_range)/200
   IF `tol1 LT 1.e-6` THEN LET tol1 = 1.e-6

   LET tol2 = 0.1*($region_y_range)/200
   IF `tol2 LT 1.e-6` THEN LET tol2 = 1.e-6

   GO setup_dsg_variables `num_decimate` `tol1` `tol2`  ! variable tolerance computed in the decimate script

ELSE
   LET nout_lonlat = `var_data_masked[i=@ngd]`
   DEFINE SYMBOL nout_lonlat = `nout_lonlat` 
ENDIF
ENDIF  ! map plot, do decimation

LET data_num = `($data_($num)_var),RETURN=dsetnum`
DEFINE SYMBOL data_num = `data_num`

IF ($ferret_init_script"0|*>1") THEN GO ($ferret_init_script) ($data_num)

! There may be a variable representing measurements to use in coloring 
! a track plot, etc.

IF ($data_var"0|*>1") THEN
   DEFINE SYMBOL ferret_plot_var = ($data_var)

! The variable may be requested to be masked by a range
! on a second variable.

   IF ($ferret_mask_var"0|*>1") THEN
      LET masking_var_1 = if ($ferret_mask_var) LE ($ferret_mask_max) THEN ($ferret_mask_var)
      LET masking_var = if masking_var_1 GE ($ferret_mask_min) THEN 1
      DEFINE SYMBOL ferret_plot_var = ($ferret_plot_var)* masking_var

      DEFINE SYMBOL msk_min = `($ferret_mask_min),prec=3`
      DEFINE SYMBOL msk_max = `($ferret_mask_max),prec=3`

      DEFINE SYMBOL mask_title = <NL>@ACwhere ($ferret_mask_var) is in [($msk_min),($msk_max)]

      DEFINE SYMBOL ferret_plot_title = ($ferret_plot_title)($mask_title)
      DEFINE SYMBOL data_title = ($data_title)($mask_title)
   ENDIF

ENDIF

IF ($ferret_plot_title"0|*>1") THEN 
   SET VAR/TITLE="($ferret_plot_title)" ($data_var)[d=`data_num`] 
   SET VAR/TITLE="($ferret_plot_title)" ($ferret_plot_var)[d=`data_num`] 
ELSE
   IF ($data_title"0|*>1") THEN 
      SET VAR/TITLE="($data_title)" ($data_var)[d=`data_num`]
      SET VAR/TITLE="($data_title)" ($ferret_plot_var)[d=`data_num`]
   ENDIF
ENDIF

! Special setup for color-by-time 
DEFINE SYMBOL color_vnum = 0
IF `($its_prop_prop_plot"0") AND ($data_count) GT 2` THEN DEFINE SYMBOL color_vnum = 2

IF `($data_($color_vnum)_var"0|time>1|*>0")` THEN 
   DEFINE SYMBOL ferret_plot_key = date_key 
   SET VAR/TITLE="Time" ($data_var)[d=`data_num`] 
   SET VAR/TITLE="Time" ($ferret_plot_var)[d=`data_num`] 

! Also check for other Time variables, e.g. Cast Time
ELSE
   LET attnames = ($data_($color_vnum)_var).attnames
   IF `IS_ELEMENT_OF_STR_N(attnames, "units") GT 0 AND \
    IS_ELEMENT_OF_STR_N(attnames, "time_origin") GT 0` THEN
      IF `STRINDEX(($data_($color_vnum)_var).units, "since") GT 0` THEN
         DEFINE SYMBOL ferret_plot_key = date_key 
         SET VAR/TITLE="Time" ($data_var)[d=`data_num`] 
         SET VAR/TITLE="Time" ($ferret_plot_var)[d=`data_num`] 
      ENDIF
   ENDIF
ENDIF

! Mark whether there are color levels set on first entry. 
! Compute automatic open-ended levels.

IF `data_num EQ 1` THEN
  IF ($ferret_fill_levels"0|*>1") THEN DEFINE SYMBOL input_fill_levels = 1
   IF ($ferret_poly_levels"0|*>1") THEN 
      DEFINE SYMBOL ferret_fill_levels ($ferret_poly_levels)
      DEFINE SYMBOL input_fill_levels = 1
   ENDIF

   LET its_plot = `STRINDEX(UPCASE("($operation_ID)"), "PLOT") GT 0`
   IF `STRLEN("($ferret_view)") EQ 2 AND its_plot GT 0` THEN 
     IF ($ferret_fill_levels"1|closed>0|*>1") THEN GO LAS_auto_levels  
   ENDIF
   IF `STRLEN("($ferret_view)") EQ 2 AND STRINDEX("($operation_ID)", "poly") GT 0` THEN 
     IF ($ferret_fill_levels"1|closed>0|*>1") THEN GO LAS_auto_levels  
   ENDIF
   IF ($ferret_fill_levels"0|closed>1|*>0") THEN CANCEL SYMBOL ferret_poly_levels
   IF ($ferret_fill_levels"0|closed>1|*>0") THEN CANCEL SYMBOL input_fill_levels
   IF ($ferret_fill_levels"0|closed>1|*>0") THEN CANCEL SYMBOL ferret_fill_levels

   IF `($ferret_poly_levels"0|*>1") EQ 0` THEN \
      IF ($ferret_fill_levels"0|*>1") THEN DEFINE SYMBOL ferret_poly_levels ($ferret_fill_levels)
ENDIF

! Set color levels for the data variable. Default levels, or apply a setting
! given by ferret_fill_levels
LET data_id = UPCASE("($data_0_dataset_ID)")

IF ($ferret_is_($custom_dataset)"0|*>1") THEN 
   IF ($custom_init"0|*>1") THEN GO ($custom_init)
ENDIF

IF `($ferret_plot_key"0|date_key>1|*>0") EQ 0` THEN
   DEFINE SYMBOL data_num = 1
   GO LAS_initialize_var_levels
ENDIF

! Apply the expression in ferret_expression. $ represents the variable.

IF ($ferret_expression"0|*>1") THEN 
   LET ev = STRINDEX("($ferret_expression)", "$")
   IF `ev EQ 0` THEN
      DEFINE SYMBOL error_status = ERROR
      DEFINE SYMBOL error_type = EXPRESSION
      DEFINE SYMBOL error_string = The expression ($ferret_expression) must contain at least one $.
      EXIT/SCRIPT
   ENDIF

   LET en = STRLEN("($ferret_expression)")
   LET e1 = ev - 1
   LET e2 = ev + 1
   IF `e1 GT 0` THEN DEFINE SYMBOL expr1 = `SUBSTRING("($ferret_expression)", 1, e1)`
   IF `e2 LT en` THEN DEFINE SYMBOL expr2 = `SUBSTRING("($ferret_expression)", e2, en)`

   DEFINE SYMBOL ferret_plot_var ($expr1)($data_var)($expr2)
   DEFINE SYMBOL ferret_plot_title ($expr1)($data_title)($expr2)

! This works except if the expression has spaces around the $ characters.
! Otherwise Ferret seems to be looking for script arguments.

   LET esecond = STRINDEX("($expr2)", "$") 
   IF `esecond GT 0` THEN

      LET ev = STRINDEX("($expr2)", "$")
      LET en = STRLEN("($expr2)")
      LET e1 = ev - 1
      LET e2 = ev + 1
      IF `e1 GT 0` THEN DEFINE SYMBOL expr3 = `SUBSTRING("($expr2)", 1, e1)`
      IF `e2 LT en` THEN DEFINE SYMBOL expr4 = `SUBSTRING("($expr2)", e2, en)`

      DEFINE SYMBOL ferret_plot_var ($expr1)($data_var)($expr3)($data_var)($expr4)
      DEFINE SYMBOL ferret_plot_title ($expr1)($data_var)($expr3)($data_var)($expr4)
   ENDIF
ENDIF

CANCEL VAR data_num

IF ($nodata"0|*>1") THEN
   DEFINE SYMBOL ferret_plot_title No data found to match this request
   CANCEL SYMBOL variable_lab
ENDIF

!---------------- end of feature_initialize_data_insitu.jnl -------------------------


