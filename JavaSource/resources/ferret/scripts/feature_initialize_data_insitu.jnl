!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! feature_initialize_data_insitu.jnl
!
! 
! Author: ansley 
! 9/2012 (from Jons_initialize_data_insitu.jnl)
! 10/4/2016 fixes so init scripts still work
!  4/3/2017 *acm* Additions for point data types
!   10/2017 *acm* Make a time-labeled color key for time variables not named "time".
!   10/2017 *acm* Set up color variable in a more consistent way
!   11/2017 *acm* Remove any quotes from around variable title and units
!    3/2018 *acm* If a feature-type plot has a color palette setting, set up to use it
!   4/2019 Modernizing for DSG-enabled Ferret

DEFINE SYMBOL num ($1"0")

! The dataset_id should be the ERDDAP dataset ID. Check that it's defined and
! if not set it to the dataset_name

IF `($data_($num)_dataset_id"0|*>1") EQ 0` THEN DEFINE SYMBOL data_($num)_dataset_id = ($data_($num)_dataset_name)

IF ($fview"0|T>1|*>0") THEN DEFINE SYMBOL use_as = /feature=timeseries

! The variable at the x and y locations

IF ($data_($num)_var"0|1|*>1") THEN DEFINE SYMBOL data_var ($data_($num)_var)
IF ($data_($num)_title"0|1|*>1") THEN DEFINE SYMBOL data_title ($data_($num)_title)

! Remove any quotes surrounding data_title string
GO LAS_unquote_string data_title

IF `($data_count"0") GT 1` THEN
   REPEAT/RANGE=1:($data_count)/name=ivar (\
    DEFINE SYMBOL ivar = `ivar`;\
    GO LAS_unquote_string data_($ivar )_title;\
   )
ENDIF

IF ($data_($num)_name"0|1|*>1") THEN DEFINE SYMBOL data_name ($data_($num)_name)

DEFINE SYMBOL ferret_plot_title ($data_title)
IF ($data_($num)_units"0|1|text>0|none>0|*>1") THEN 
   DEFINE SYMBOL data_units ($data_($num)_units)
   ! Remove any quotes surrounding data_title string
   GO LAS_unquote_string data_units
ELSE 
   CANCEL SYMBOL data_($num)_units
ENDIF

! Text variables have units of "text". In this case don't include units with the title
! remove any labels that are degrees_east, etc, which are redundant

IF ($data_($num)_units"0|1|*>1") THEN 
   DEFINE SYMBOL data_units ($data_($num)_units)
   IF ($data_units"0|text>1|none>1|*>0") THEN CANCEL SYMBOL data_units
   IF ($data_units"0|degrees_east>1|degrees_north>1|*>0") THEN CANCEL SYMBOL data_units
   IF ($data_units"0|*>1") THEN DEFINE SYMBOL ferret_plot_title =($data_($num)_title) (($data_($num)_units))
ENDIF

DEFINE SYMBOL uptitle = `UPCASE("($ferret_plot_title%0%)")`
IF `(STRINDEX("($uptitle)", "TIME") GT 0) AND (STRINDEX("($uptitle)", " SINCE ") GT 0)` THEN \
DEFINE SYMBOL ferret_plot_title = Time


! Set up the variable title and units for single-variable plots.
! The prop-prop plot title A vs B colored by C are set elsewhere.

IF `($1"0") EQ 0` THEN 
   DEFINE SYMBOL variable_lab = ($ferret_plot_title)
   LET variable_lab =  data_title
   IF ($data_($num)_units"0|1|*>1") THEN LET variable_lab =  STRCAT(data_title, data_units)
ENDIF

! open the dataset and report errors
USE($use_as) "($data_($num)_url)"

IF ($fer_last_error"0|*>1") THEN
  DEFINE SYMBOL error_status = ERROR
  DEFINE SYMBOL error_type = DATA
  DEFINE SYMBOL error_string = The ($data_x_var) source file -- ($data_url) did not open successfully.
  EXIT/SCRIPT
ENDIF

! Get the dataset title
LET global_attnames = ..attnames
IF `IS_ELEMENT_OF_STR(global_attnames, "title") GT 0` THEN
  DEFINE SYMBOL dataset_title_att = `..title`
ENDIF

DEFINE SYMBOL dset($num) = `($data_var),RETURN=dsetnum`


GO feature_initialize_plot_settings


GO custom_dataset_definitions

! Did the request return the no-data flag?

LET global_attnames = ..attnames
LET nglobal_atts = ..nattrs
IF `nglobal_atts GT 0` THEN
   IF `IS_ELEMENT_OF_STR(global_attnames, "query_result") GT 0` THEN
      DEFINE SYMBOL data_query_string = `..query_result`
      IF `STRINDEX("($data_query_string)", "No data found") GT 0` THEN

! Set dataset annotations 
! Do not annotate with variable - it's the location/time that's unavailable
   
         GO LAS_url_label 0
         CANCEL SYMBOL variable_lab 

! This script makes a blank plot, labeled with a message, calls LAS_results, and exits
         GO feature_abort_no_data
      ENDIF

   ENDIF
ENDIF

DEFINE SYMBOL dsetnum = `($data_($num)_var),RETURN=dsetnum`

! Is it a Profile, Timeseries, or Trajectory dataset?
GO LAS_setup_dsg


IF `($its_listing"0|*>1") AND ($num) EQ 0` THEN 

! For ascii listings, if a variable is the same as the feature variable,
! then remove reference to the feature variable. It is always listed as part
! of the metadata.

   REPEAT/RANGE=0:($data_count)/name=ivar (\
    DEFINE SYMBOL ivar = `ivar`;\
    IF `($data_($ivar)_var"0|($feature_variable"A")>1|*>0")` THEN DEFINE SYMBOL iremove = `ivar`;\
    )

! Open the dataset. Rename the variables to varname_
! Keep original variable names as symbols data_0_var_orig, ...
! Treat longitude and latitude the same.

   DEFINE SYMBOL data_var ($data_($num)_var)
ENDIF


! Define the feature mask

LET feature_mask = 1 + 0*rowsize

IF `($have_feature_mask"0|*>1") EQ 0` THEN 
   DEFINE SYMBOL features_shown = `nfeatures`
   LET subset_mask =  1 + 0*rowsize
ENDIF

IF ($ferret_cruise_list"0|*>1") THEN GO feature_list_mask

! Is the plot colored by profile ID

LET color_by_feature = 0

IF `(($num)+1) eq ($data_count)` THEN
   IF `($data_($num)_var"0|vid>1|($feature_variable)>1|*>0") AND  \
       ($nodata"0|*>1) EQ 0` THEN LET color_by_feature = 1

! Or, is this a DSG file with the feature_variable we have already identified?

   IF ($data_($num)_var"0|($feature_variable)>1|*>0")  THEN LET color_by_feature = 1

ENDIF

! Other variables on the E axis to be colored the same way.

! Is it a varible on the E axis of a DSG file?
IF ($its_trajectory_map"0|*>1") THEN
   DEFINE SYMBOL vshape = `($data_var),RETURN=shape`
   IF ($vshape"0|POINT>1|*>0") THEN 
      DEFINE SYMBOL eshape = `($data_var),return=eaxis`
      IF ($eshape"0|NORMAL>0|*>1") THEN DEFINE SYMBOL vshape = E
   ENDIF
ENDIF

IF ($vshape"0|E>1|*>0") THEN LET color_by_feature = 1

LET data_num = `($data_($num)_var),RETURN=dsetnum`
DEFINE SYMBOL data_num = `data_num`

IF ($ferret_init_script"0|*>1") THEN GO ($ferret_init_script) ($data_num)

! There may be a variable representing measurements to use in coloring 
! a track plot, etc.

IF ($data_var"0|*>1") THEN
   DEFINE SYMBOL ferret_plot_var = ($data_var)

! The variable may be requested to be masked by a range
! on a second variable.

   IF ($ferret_mask_var"0|*>1") THEN
      LET masking_var_1 = if ($ferret_mask_var) LE ($ferret_mask_max) THEN ($ferret_mask_var)
      LET masking_var = if masking_var_1 GE ($ferret_mask_min) THEN 1
      DEFINE SYMBOL ferret_plot_var = ($ferret_plot_var)* masking_var

      DEFINE SYMBOL msk_min = `($ferret_mask_min),prec=3`
      DEFINE SYMBOL msk_max = `($ferret_mask_max),prec=3`

      DEFINE SYMBOL mask_title = <NL>@ACwhere ($ferret_mask_var) is in [($msk_min),($msk_max)]

      DEFINE SYMBOL ferret_plot_title = ($ferret_plot_title)($mask_title)
      DEFINE SYMBOL data_title = ($data_title)($mask_title)
   ENDIF

ENDIF

IF ($ferret_plot_title"0|*>1") THEN 
   SET VAR/TITLE="($ferret_plot_title)" ($data_var)[d=`data_num`] 
   SET VAR/TITLE="($ferret_plot_title)" ($ferret_plot_var)[d=`data_num`] 
ELSE
   IF ($data_title"0|*>1") THEN 
      SET VAR/TITLE="($data_title)" ($data_var)[d=`data_num`]
      SET VAR/TITLE="($data_title)" ($ferret_plot_var)[d=`data_num`]
   ENDIF
ENDIF

! Special setup for color-by-time 
DEFINE SYMBOL color_vnum = 0
IF `($its_prop_prop_plot"0") AND ($data_count) GT 2` THEN DEFINE SYMBOL color_vnum = 2

IF `($data_($color_vnum)_var"0|($data_t_var)>1|*>0")` THEN 
   DEFINE SYMBOL ferret_plot_key = date_key 
   SET VAR/TITLE="Time" ($data_var)[d=`data_num`] 
   SET VAR/TITLE="Time" ($ferret_plot_var)[d=`data_num`] 

ENDIF

! Set color levels for the data variable. Default levels, or apply a setting
! given by ferret_fill_levels
LET data_id = UPCASE("($data_0_dataset_ID)")

IF ($ferret_is_($custom_dataset)"0|*>1") THEN 
   IF ($custom_init"0|*>1") THEN GO ($custom_init)
ENDIF

IF `($ferret_plot_key"0|date_key>1|*>0") EQ 0` THEN
   DEFINE SYMBOL data_num = 1
   GO LAS_initialize_var_levels
ENDIF

CANCEL VAR data_num

IF ($nodata"0|*>1") THEN
   DEFINE SYMBOL ferret_plot_title No data found to match this request
   CANCEL SYMBOL variable_lab
ENDIF

!---------------- end of feature_initialize_data_insitu.jnl -------------------------
