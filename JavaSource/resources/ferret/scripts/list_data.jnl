!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! $RCSfile: list_data.jnl,v $
! $Revision: 1.4 $
! 
! $Author: ansley $
! $Date: 2006/07/06 
!
! 2006/8/10 ACM define output variable with its region.
! 2007/3/13 ACM Set up to write netCDF files of curvilinear data
! 3/29/2007 ACM Fix logic interpreting contents of ferret_interpolate_data 
! 8/ 9/2007 ACM Allow for multiple variables, appending each to the file
!               until all are written.
! 12/2016 set list precision to 7
!  4/2019 Implement the property ferret_points_zt (e.g. SPURS prawler profiles). 
!         For plots, it's a graphical ZT plot. For data listings, add the 
!         pressure variable to the listing or file.
!
! list_data.jnl creates a listing, CDF or ASCII (based on std_list.jnl)
! Assumes symbol ferret_format which will be one of "cdf" "txt" "tsv" "csv" "asc"
! Assumes symbol ferret_listing is the output file.
! Assumes symbol data_dup_varname is the optional duplicate variable name to be used on output

! Decode format argument
! ACM NOTE: The slashes mess up parsing in the DEFINE SYMBOL if we do them all at once.
! ferret_format may be asc, txt, or cdf

GO LAS_initial_setup 0

CANCEL SYMBOL frmt 
DEFINE SYMBOL frmt ($frmt)/($ferret_format%format=cdf|txt>heading|cdf>format=cdf|asc>width=1024|%)

IF ($ferret_format%0|asc>1|*>0) THEN DEFINE SYMBOL frmt ($frmt)/format=(5G12.5)

IF ($ferret_data_format%0|tsv>1|csv>1|asc>1|*>0%) THEN 
   DEFINE SYMBOL frmt ($frmt) = ($frmt)/($ferret_data_format%|tsv>format=tab|csv>format=comma|asc>heading=enhanced%)
   SET LIST/PREC=7
   IF ($ferret_points_zt%0|*>1%) THEN 
      LIST/APPEND/FILE="($result_ferret_listing_filename)" "Data Is Listed at fixed z-levels. Adding pressure to this listing"
      DEFINE SYMBOL nzt = ($data_count)
      DEFINE SYMBOL data_count = `($data_count) + 1`
      DEFINE SYMBOL data_($nzt)_dataset_name = ($data_0_dataset_name)
      DEFINE SYMBOL data_($nzt)_url = ($data_0_url)
      USE ($data_0_url)
      DEFINE SYMBOL data_($nzt)_name = `($ferret_points_zt),return=title`
      DEFINE SYMBOL data_($nzt)_region = ($data_0_region)
      DEFINE SYMBOL data_($nzt)_title = `($ferret_points_zt),return=title`
      DEFINE SYMBOL data_($nzt)_units = `($ferret_points_zt),return=units`
      DEFINE SYMBOL data_($nzt)_var = ($ferret_points_zt)
      DEFINE SYMBOL data_($nzt)_t_hi = ($region_0_t_hi)
      DEFINE SYMBOL data_($nzt)_t_lo = ($region_0_t_lo)
      DEFINE SYMBOL data_($nzt)_x_hi = ($region_0_x_hi)
      DEFINE SYMBOL data_($nzt)_x_lo = ($region_0_x_lo)
      DEFINE SYMBOL data_($nzt)_y_hi = ($region_0_y_hi)
      DEFINE SYMBOL data_($nzt)_y_lo = ($region_0_y_lo)
      DEFINE SYMBOL data_($nzt)_z_hi = ($region_0_z_hi)
      DEFINE SYMBOL data_($nzt)_z_lo = ($region_0_z_lo)
   ENDIF
ENDIF

CANCEL MODE INTERPOLATE  ! important when evaluating the size limits

SET LIST/FILE="($result_ferret_listing_filename)"

IF ($ferret_data_format"0|csv>1|*>0") THEN
   GO list_csv_data
ELSE
   REPEAT/RANGE=1:($data_count"1")/NAME=outvar (GO list_a_variable `outvar-1`; CANCEL SYMBOL ferret_list_var)
ENDIF
