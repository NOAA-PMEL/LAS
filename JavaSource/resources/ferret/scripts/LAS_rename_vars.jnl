!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! $RCSfile: LAS_rename_vars.jnl

! Set up for listing data from the Correlation viewer.
! We will want to define output variables using original file
! variable names, but with a mask to apply constraints. 

! Open the dataset.  For all variables data_n_var that have been defined
! in the calling script, store the original name in symbol data_n_var_in 
! and rename the variable in the dataset to data_n_var_.  Also change the 
! symbol data_n_var to match the new file variable name.

! Do the same for variables longitude and latitude in the file.
! If it's a featureType other than trajectory, also rename depth

DEFINE SYMBOL num ($1"0")

IF ($data_($num)_chained"0|1|*>1) THEN
  DEFINE SYMBOL data_url \"($data_($num)_file)\"
ELSE
  DEFINE SYMBOL data_url \"($data_($num)_url)\"
ENDIF

! SET DATA and check for errors

SET DATA ($data_url)

IF ($fer_last_error"0|0|1|*>1") THEN
  DEFINE SYMBOL error_status = ERROR
  DEFINE SYMBOL error_type = DATA
  DEFINE SYMBOL error_string = The ($data_var) source file -- ($data_url) did not open successfully.
  EXIT/SCRIPT
ENDIF

! Rename variables, appending an underscore.  
! If a variable is listed twice, handle that.

! Save the name of the feature variable.
IF `($feature_variable_in"0|*>1") EQ 0` THEN \
  IF ($feature_variable"0|*>1") THEN DEFINE SYMBOL feature_variable_in = ($feature_variable)

 REPEAT/RANGE=0:100/name=ivar (\
   GO var_rename;\
   IF ($exit_rename"0|*>1") THEN exit/loop;\
   )

IF ($feature_type%0|Trajectory>0|none>0|*>1%) THEN DEFINE SYMBOL rename_depth = 1

LET varnames = ..varnames
LET nvars = ..nvars
REPEAT/RANGE=1:`nvars`/name=ivar (\
 DEFINE SYMBOL ivar = `ivar`;\
 DEFINE SYMBOL vname = `varnames[i=($ivar)]`;\
 IF ($vname"0|longitude>1|*>0") THEN ;\
    SET VAR/NAME=longitude_ longitude;\
    DEFINE SYMBOL longitude_in = longitude;\
 ENDIF;\
 IF ($vname"0|latitude>1|*>0") THEN ;\
    SET VAR/NAME=latitude_ latitude;\
    DEFINE SYMBOL latitude_in = latitude;\
 ENDIF;\
 IF `($rename_depth"0|*>1") AND ($vname"0|depth>1|*>0")` THEN ;\
    SET VAR/NAME=depth_ depth;\
    DEFINE SYMBOL depth_in = depth;\
    DEFINE SYMBOL depth_done = 1;\
 ENDIF;\
 IF ($vname"0|($feature_variable)>1|*>0") THEN ;\
    SET VAR/NAME=($feature_variable)_ ($feature_variable);\
    DEFINE SYMBOL ($feature_variable)_in = ($feature_variable);\
    DEFINE SYMBOL ($feature_variable)_done = 1;\
 ENDIF;\
 )

IF ($($feature_variable)_done"0|*>1") THEN  DEFINE SYMBOL feature_variable = ($feature_variable)_

! Fix constraints

REPEAT/RANGE=0:100/name=ivar (\
 DEFINE SYMBOL ivar = `ivar`;\
 IF `($constraint_($ivar)_lhs"0|*>1") EQ 0` THEN EXIT/LOOP;\
 DEFINE SYMBOL constr_var = ($constraint_($ivar)_lhs);\
 IF `IS_ELEMENT_OF_STR("($constr_var)", varnames) NE 0` THEN EXIT/CYCLE;\
 DEFINE SYMBOL constraint_($ivar)_lhs = ($constr_var)_;\
 )

! End of $RCSfile ------------ LAS_rename_vars.jnl--------------------------
