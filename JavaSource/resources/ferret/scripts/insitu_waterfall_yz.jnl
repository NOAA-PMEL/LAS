!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! $Header$
! insitu_waterfall_yz.jnl
!
! Creates a YZ Waterfall plot 
!
! Set the gif filename, changing it from .gif to the name from this request
! Set up to write info to the debug log so that FshowGO can be used
GO LAS_initial_setup

! Initialize the region and data.

DEFINE SYMBOL  data_initialization_script = feature_initialize_data_insitu

SET MODE verify:always  ! allows for debugging

DEFINE SYMBOL ribbon = 1   ! straight lines colored by the color var
DEFINE SYMBOL its_insitu = 1

! Open the dataset, analyze the DSG dataset, set up plot variables and 
! Z-axis for plotting. 

GO LAS_insitu_waterfall_setup

! Make a blank plot with the correct Y axis on the horizal and depth
! axis on the vertical.

LET ntic = 10
PPL %range ($region_y_lo),  ($region_y_hi), `ntic`
LET ylo = ($ppl$range_low)
LET yhi = ($ppl$range_high)

DEFINE AXIS/y=`ylo`:`yhi`/npoints=100/units="`($data_y_var),RETURN=units`" haxis

DEFINE SYMBOL insitu_blankvar = blankvar
LET blankvar = 100+y[gy=haxis] + z[gz=vaxis]
DEFINE SYMBOL insitu_blankvar_qual = /PAL=white/NOLAB/NOKEY/LEV=(0,1,1)($vlim)
SHADE($insitu_blankvar_qual) blankvar

ANNOTATE/norm/xpos=-0.05/ypos=0.5/angle=90/halign=0/siz=0.12 "`($data_z_var),RETURN=units`"

PPL COLOR,6,60,60,60 ! grey
PLOT/OVER/VS/SYM=dot/NOKEY/NOLAB/COLOR=6 ($data_y_var), ($data_z_var)

PLOT/OVER/VS/RIBBON/MISS=blank/SYM=19/THICK($qualifiers)/SET ($data_y_var), ($data_z_var), ($data_var)
   IF ($ferret_annotations%0|*>1%) THEN GO reset_labels
   GO key_at_top
PPL RIBBON/OVER

! Add location labels as annotation notes.

DEFINE SYMBOL longitude_lab = Longitude: ($region_x_lo):($region_x_hi)
DEFINE SYMBOL time_lab = Time ($region_t_lo):($region_t_hi)

! Add feature information on the top
GO feature_labels

! restore key-annotation setting: keys not annotated
KEYMARK 0

! Save the results

GO LAS_results box

! End of file ------------insitu_waterfall_yz.jnl--------------------------
