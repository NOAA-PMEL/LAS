! Compute a trend line to be plotted with a line plot

IF ($ferret_view"|x>1|*>0") THEN LET p = x[gx=($plot_arg),($region_x)]
IF ($ferret_view"|y>1|*>0") THEN LET p = y[gy=($plot_arg),($region_y)]
IF ($ferret_view"|z>1|*>0") THEN LET p = z[gz=($plot_arg),($region_z)]
IF ($ferret_view"|t>1|*>0") THEN LET p = t[gt=($plot_arg),($region_t)]
LET q = ($plot_arg)

SET GRID q
GO regress($ferret_view)

! Got inconsistent region error on a z plot
LET qhat = `SLOPE`*P + `INTERCEP`

! Set variable-mean and variable-(mean and trend)

LET/TITLE="Variable minus mean" demeaned = ($plot_arg) - `qave`
LET/TITLE="minus mean and trend"  detrended = ($plot_arg) - qhat

! Set up to define the dependent axis scale
DEFINE SYMBOL dep_dir = Y
IF ($ferret_view"|z>1|*>0") THEN DEFINE SYMBOL dep_dir = X

! Ignore any color setting
DEFINE SYMBOL qualifiers = ($qualifiers)/COLOR=black

! Reset the graticule lines to OFF, unless the user explicitly turned them on.
! CANCEL MODE GRATICULE
! IF ($ferret_use_graticules"0|0|*>1) THEN SET MODE GRATICULE:(DASH,COLOR=black)

DEFINE SYMBOL slope_units = ($data_units)/(`($plot_arg),RETURN=($ferret_view)units`)

! if just the variable and its trend are to be plotted.
IF `($ferret_trend_line"0|1|2|*>0") EQ 1` THEN

   DEFINE VIEW/x=0:1/y=0:1 trend_view
   SET VIEW trend_view

   PLOT($qualifiers)/NOKEY/SET/TITLE=" " ($plot_arg)
      IF ($xform_dms"0|*>1") THEN PPL XFOR (($xform_dms))
      IF `($ferret_dep_axis_scale"0|*>1")` THEN PPL ($dep_dir)AXIS ($ferret_dep_axis_scale)
      GO reset_labels
   PPL PLOT

   IF ($fview"0|Z>1|*>0") THEN
      DEFINE SYMBOL v1 = ($xaxis_min)
      DEFINE SYMBOL v2 = ($xaxis_max)
   ENDIF

   PLOT/OVER/LINE/COLOR=BLUE/NOLAB qhat
   ! Label the slope of the line above the plot.
   LABEL/NOUSER `($ppl$xlen)-0.1`,`($ppl$ylen)+0.2`,1, 0, 0.11, @P4@ASSlope = `slope,P=3` ($slope_units)

   LET y_title_off = -1.0

! Or draw 2-panel plot with variable and trend, then variable-mean and var-mean&trend

ELIF `($ferret_trend_line"0|1|2|0") EQ 2` THEN
   IF ($dep_dir"0|Y>1|*>0") THEN 
      DEFINE VIEW/axes/x=0.1:0.9/y=0.55:0.8 trend_up 
      DEFINE VIEW/axes/x=0.1:0.9/y=0.2:0.45 trend_dn
      SET VIEW trend_up
   ELSE
      SET VIEW left
   ENDIF
   PLOT($qualifiers)/NOKEY/SET/TITLE=" " ($plot_arg)
      IF ($xform_dms"0|*>1") THEN PPL XFOR (($xform_dms))
      IF `($ferret_dep_axis_scale"0|*>1")` THEN PPL ($dep_dir)AXIS ($ferret_dep_axis_scale)
      GO reset_labels
   PPL PLOT
   DEFINE SYMBOL v1 = ($xaxis_min)
   DEFINE SYMBOL v2 = ($xaxis_max)
   PLOT/OVER/LINE/COLOR=BLUE/NOLAB qhat

   ! Label the slope of the line above the plot.
   LABEL/NOUSER `($ppl$xlen)-0.1`,`($ppl$ylen)+0.2`,1, 0, 0.11, @P4@ASSlope = `slope,P=3` ($slope_units)

   IF ($dep_dir"0|Y>1|*>0") THEN 
      SET VIEW trend_dn
   ELSE
      SET VIEW right
   ENDIF
   IF ($ferret_view"|z>1|*>0") THEN 
      DEFINE SYMBOL varunits = @AS($LABX% %) 
   ELSE
      DEFINE SYMBOL varunits = @AS($LABY% %) 
   ENDIF

   ! make automatic line/symbol settings
   CANCEL SYMBOl qualifiers
   GO set_linesym

   PLOT($qualifiers)/SET demeaned, detrended
      IF `($labnum_x%0|*>1%)` THEN GO unlabel ($labnum_x)
      IF `($labnum_y%0|*>1%)` THEN GO unlabel ($labnum_y)
      IF `($labnum_z%0|*>1%)` THEN GO unlabel ($labnum_z)
      IF `($labnum_t%0|*>1%)` THEN GO unlabel ($labnum_t)
      IF `($labnum_calend%0|*>1%)` THEN GO unlabel ($labnum_calend)
      IF ($dep_dir"0|Y>1|*>0") THEN PPL YLAB ($varunits)
      IF ($dep_dir"0|X>1|*>0") THEN PPL XLAB ($varunits)
      IF ($dep_dir"0|X>1|*>0") THEN PPL YLAB
   PPL PLOT
   
   LET y_title_off = -1.2

ENDIF

! Return to full viewport so that the labels are placed correctly
SET VIEW full

PLOT/VS/NOAX/NOLAB/COLOR=8 {0,0},{1,1}
LABEL/NOUSER `($ppl$xlen)/2`,-1.1,0, 0, 0.16, @AC($ferret_title_0)

!  ----------------- End of trend_line.jnl ------------------------------------
