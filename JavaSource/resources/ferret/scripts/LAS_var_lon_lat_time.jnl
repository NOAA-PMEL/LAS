! LAS_var_lon_lat_time.jnl
! 10/2017
!
! Check attributes to determine if the variable is a longiutde,
! latitude, or formatted time.
! Argument is a variable name
! Defines symbols var_is_lon, var_is_lat, var_is_time with values of 0 or 1.

cancel symbol var_is_lon, var_is_lat, var_is_time

DEFINE SYMBOL invarname = ($1)
LET invarname = ($1)

DEFINE SYMBOL var_is_lon = ($invarname"0|longitude>1|*>0)
IF `($var_is_lon) EQ 0` THEN
   LET attnames = invarname.attnames
   IF `IS_ELEMENT_OF_STR_N (attnames, "units") GT 0` THEN
      LET units_str = invarname.units
      IF `STRINDEX( UPCASE(units_str), "DEG") GT 0` THEN \
        DEFINE SYMBOL var_is_lon = `STRINDEX( UPCASE(units_str), "_E") GT 0` 
   ENDIF
ENDIF

DEFINE SYMBOL var_is_lat = ($invarname"0|latitude>1|*>0)
IF `($var_is_lat) EQ 0` THEN
   LET attnames = invarname.attnames
   IF `IS_ELEMENT_OF_STR_N (attnames, "units") GT 0` THEN
      LET units_str = invarname.units
      IF `STRINDEX( UPCASE(units_str), "DEG") GT 0` THEN \
        DEFINE SYMBOL var_is_lat = `STRINDEX( UPCASE(units_str), "_N") GT 0` 
   ENDIF
ENDIF

DEFINE SYMBOL var_is_time = ($invarname"0|time>1|*>0)
IF `($var_is_time) EQ 0` THEN
   LET attnames = invarname.attnames
   IF `IS_ELEMENT_OF_STR_N(attnames, "units") GT 0 AND IS_ELEMENT_OF_STR_N(attnames, "time_origin") GT 0` THEN
      LET units_str = invarname.units
      DEFINE SYMBOL var_is_time = `STRINDEX( UPCASE(units_str), "SINCE") GT 0`
   ENDIF
ENDIF

! End of file ------------LAS_var_lon_lat_time.jnl--------------------------

