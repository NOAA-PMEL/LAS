! setup_pp_region_annotations.jnl
! For a dimension X, Y, Z, T make a label
! in the form of an upper-left label with the 
! range.  For PropProp plots, the axes are
! labeled with the data variable ranges (salinity, temperature).
! Here we are adding the space and time region labels.

! To do: format these nicely with Degrees North etc.

IF ($dim($idim)"0|X>1|*>0") THEN
   IF ($longitude_lab"0|*>1") THEN EXIT/SCRIPT
   LET reg_lo = ($region_x_lo)
   LET reg_hi = ($region_x_hi)
   DEFINE SYMBOL longitude_lab = ($xlabel): `reg_lo,prec=6`E
   IF `($region_x_range"0") GT 0` THEN DEFINE SYMBOL longitude_lab = ($longitude_lab) to `reg_hi,prec=6`E
ENDIF

IF ($dim($idim)"0|Y>1|*>0") THEN
sh sym *lat*lab*
   IF ($latitude_lab"0|*>1") THEN EXIT/SCRIPT
   DEFINE SYMBOL NorS = N
   LET reg_lo = ($region_y_lo)
   LET reg_hi = ($region_y_hi)
   IF `reg_lo LT 0` THEN 
      DEFINE SYMBOL NorS = S
      LET reg_lo = ABS(`reg_lo,prec=6`)
   ENDIF
   DEFINE SYMBOL latitude_lab = ($ylabel): `reg_lo,prec=6`($NorS)
   DEFINE SYMBOL NorS = N
   IF `reg_hi LT 0` THEN 
      DEFINE SYMBOL NorS = S
      LET reg_hi = ABS(`reg_hi,prec=6`)
   ENDIF
   IF `($region_y_range"0") GT 0` THEN DEFINE SYMBOL latitude_lab = ($latitude_lab) to `reg_hi,prec=6`($NorS)
ENDIF

IF ($dim($idim)"0|Z>1|*>0") THEN
   IF ($depth_lab"0|*>1") THEN EXIT/SCRIPT
   LET reg_lo = ($region_z_lo)
   LET reg_hi = ($region_z_hi)
   DEFINE SYMBOL zunits = `($ferret_plot_var),RETURN=zunits`
   DEFINE SYMBOL upunits = `UPCASE("($zunits)")`
   IF `STRCMP("($upunits)", "METERS") EQ 0` THEN DEFINE SYMBOL zunits = m
   DEFINE SYMBOL depth_lab = ($zlabel) (($zunits)): `reg_lo,prec=6`
   IF `($region_z_range"0") GT 0` THEN DEFINE SYMBOL depth_lab = ($depth_lab) to `reg_hi,prec=6`
ENDIF

IF ($dim($idim)"0|T>1|*>0") THEN
   IF ($time_lab"0|*>1") THEN EXIT/SCRIPT
   DEFINE SYMBOL time_lab = ($tlabel): ($region_t_lo)
   IF `($region_y_range"0") GT 0` THEN DEFINE SYMBOL time_lab = ($time_lab) to ($region_t_hi)
ENDIF

! End of  ------------- setup_pp_region_annotations.jnl--------------------------
