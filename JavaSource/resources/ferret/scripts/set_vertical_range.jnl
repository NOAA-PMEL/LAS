! Define variables for consistent vertical axes for all the time series
! in a set of stations being plotted.
   
! Argument is the group-number 0 through ngroups-1

DEFINE SYMBOL ig = ($1)

plot/set ($ferret_plot_var_($ig))

LET plotmin_($ig) = ($ppl$ymin1)
LET plotmax_($ig) = ($ppl$ymax1)

IF `plotmin_($ig) EQ plotmax_($ig)` THEN
   LET plotmin_($ig) = ($ppl$ymin1) - 1
   LET plotmax_($ig) = ($ppl$ymin1) + 1
ENDIF

CAN SYM ppl$range*
PPL %RANGE `plotmin_($ig)`, `plotmax_($ig)`,5
let plotinc = ($ppl$range_inc)
  
let plotmin_($ig) = ($ppl$range_low)
let plotmax_($ig) = ($ppl$range_high)

IF `plotmax_($ig) EQ plotmin_($ig)` THEN

!  precision in pplus prevents enough accuracy for min,max,range symbols.

   LET rng_offset = `plotmax_($ig)`
   let ferplotvar = ($ferret_plot_var)
   LET rangemin = ferplotvar[x=@min,y=@min,z=@min,t=@min] - rng_offset
   LET rangemax = ferplotvar[x=@max,y=@max,z=@max,t=@max] - rng_offset


   IF `($data_count) GT 1` THEN
      LET rangemin = dmin - rng_offset
      LET rangemax = dmax + rng_offset
   ENDIF

   can sym ppl$range*
   ppl %range `0.8*rangemin`,`1.2*rangemax`,5
   let plotmin_($ig) = ($ppl$range_low)  + rng_offset
   let plotmax_($ig) = ($ppl$range_high) + rng_offset
   let plotinc = ($ppl$range_inc)
   
   IF `plotmin_($ig) EQ plotmax_($ig)` THEN
      LET plotmin_($ig) = `plotmin_($ig) - 0.01*ABS(plotmin_($ig))`
      LET plotmax_($ig) = `plotmax_($ig) + 0.01*ABS(plotmax_($ig))`
   ENDIF
ENDIF
