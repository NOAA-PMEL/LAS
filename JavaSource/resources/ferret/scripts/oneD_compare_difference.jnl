!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! oneD_compare_difference.jnl
! 
! Ansley Manke
! Date: 2007/02/02 
! 3/7/2007: ACM. Put code to check that the region was not too small, resulting 
!           in degenerate plot into LAS_initialize_data.jnl rather than in the 
!           individual plot scripts.
! 3/29/2007 ACM Fix logic interpreting contents of ferret_interpolate_data 
! 11/9/2007 ACM If ferret_dep_axis_scale is defined, apply vertical axis scaling.
! 8/2012 ACM Fixing ticket 1337, F-TDS bug. If there no _FillValue, set it 
!            to a known value that matches the value used in get_datavar.jnl
! 10/10/2012 Reverse the sense of the difference; ticket 1377
!  2/10/2014 For E-direction plots, make the line plot in PLOT/STEP style, ticket 1573
!
! oneD_compare_difference.jnl creates a T line plot for use with the Back 
! End Server code that sits behind a Live Access Server (LAS). 
! It produces a difference plot of 2 variables which are on the same
! grid in the plot direction
!
! Set any Ferret modes
!
! Set the gif filename, changing it from .gif to the name from this request
GO LAS_reset_gifname


IF ($ferret_interpolate_data%0|false>0|true>1|1|0|*>1%) THEN SET MODE INTERPOLATE

DEFINE SYMBOL fview = `UPCASE("($ferret_view)")`

! Define symbols associated with the region and data and perform any
! initialization needed for this dataset.

GO LAS_initialize_region 0
GO LAS_initialize_data 0

! Check for errors (They often occur during dataset initialization.)

IF ($error_status"0|*>1") THEN
  MESSAGE/ERROR **ERROR ($error_string)
  EXIT/PROGRAM
ENDIF

! Check whether the region is too small; reduces to a single grid point.
! (Region is a point)
GO LAS_check_1d_region

! Check for errors 
IF ($error_status"0|ERROR>1") THEN
  MESSAGE/ERROR **ERROR ($error_string)
  EXIT/PROGRAM
ENDIF

! Here is variable 0 and its title

DEFINE SYMBOL ferret_var_0 = ($ferret_plot_var)
DEFINE SYMBOL ferret_title_0 = ($ferret_plot_title"($data_var)")

LET attlist = ($data_0_var).attnames
IF `IS_ELEMENT_OF_STR(attlist,{"missing_value","_FillValue"} ) EQ 0` THEN
   SET VAR/BAD=-9999999 ($data_0_var)
ENDIF

! Initialize next dataset. 
! Assume the interface has returned correct, matching regions in the plot direction.

! Apply any expression only to the first variable not to this one.
CANCEL SYMBOL ferret_expression 

GO LAS_initialize_region 1
GO LAS_initialize_data 1

! Check for errors (They often occur during dataset initialization.)

IF ($error_status"0|*>1") THEN
  MESSAGE/ERROR **ERROR ($error_string)
  EXIT/PROGRAM
ENDIF

! Here is variable 1 and its title

DEFINE SYMBOL ferret_var_1 = ($ferret_plot_var)
DEFINE SYMBOL ferret_title_1 = ($ferret_plot_title"($data_var)")

LET attlist = ($data_1_var).attnames
IF `IS_ELEMENT_OF_STR(attlist,{"missing_value","_FillValue"} ) EQ 0` THEN
   SET VAR/BAD=-9999999 ($data_1_var)
ENDIF

! If this is a Time plot, then if the output axis is a modulo time axis, 
! check for the case of the first dataset on a modulo axis and the second 
! a non-modulo axis, which often results in all-missing result.

IF `STRINDEX("($ferret_view)", "t") GT 0` THEN GO check_t_axes

! Compare the plot axes of the two varibles. They must be the same.
GO LAS_compare_axes "($data_0_var)[d=($data_0_num)]" "($data_1_var)[d=($data_1_num)]" ($fview)

! Check for errors 

IF ($error_status"0|*>1") THEN   
   DEFINE SYMBOL error_string = ($fview) grid of ($data_1_var) must match ($fview) grid of ($data_0_var)
   MESSAGE/ERROR **ERROR ($error_string)
   EXIT/PROGRAM
ENDIF

! Define the difference variable 

DEFINE SYMBOL ferret_diff_var = ($ferret_var_1)  -  ($ferret_var_0)

! Use Ferret properties to set up the plot qualifiers
GO LAS_set_plot_qualifiers

! symbols and/or line
GO set_linesym

! Open the window, apply size, scale options
GO LAS_open_window

! Set the title, and the labels for the upper left listing the locations
! and time in directions normal to the plot.

GO LAS_set_diff_labels ($fview)


IF ($ferret_view"0|e>1|*>0") THEN
   DEFINE SYMBOL qualifiers = ($qualifiers)/STEP=connected
   IF `($region_e_hi) - ($region_e_lo) LT 20` THEN DEFINE SYMBOL qualifiers = ($qualifiers)/THICK
ENDIF


! Do the plot. 

PLOT($qualifiers)/title="($ferret_diff_title)"/SET ($ferret_diff_var)

   IF ($ferret_dep_axis_scale"0|*>1") THEN 
      IF ($ferret_view"0|z>1|*>0") THEN 
         PPL XAXIS ($ferret_dep_axis_scale)
      ELSE
         PPL YAXIS ($ferret_dep_axis_scale)
      ENDIF
   ENDIF
   IF ($ferret_view"0|e>1|*>0") THEN PPL axlint,0,  ! custom e-axis labels

   GO reset_labels
PPL plot

! For an ensemble-dataset plot in the E direction, label the horizontal axis with
! the labels, if available.

IF ($ferret_view"0|e>1|*>0") THEN
   LET dely = `0.04*ABS(($yaxis_max)-($yaxis_min))`
   LET yloc = `($yaxis_min)-dely`
   LET varnames = ..varnames
   IF `IS_ELEMENT_OF_STR_N(varnames, "labels") GT 0` THEN 
      REPEAT/E=($region_e_lo):($region_e_hi) (\
	      LABEL/USER `_e`, `yloc`,0,0,0.1,`LABELS`)
   ELSE
      REPEAT/E=($region_e_lo):($region_e_hi) (\
	      LABEL/USER `_e`, `yloc`,0,0,0.1, "`_E`")
   ENDIF
ENDIF


! Overall header at the very top
GO LAS_ferret_las_version_header

! Put the labels defined for above the plot

GO labels_above_plot

! Save the results

DEFINE SYMBOL result_type = ($ferret_view)line
IF `STRINDEX("($ferret_view)", "t") GT 0 OR STRINDEX("($ferret_view)", "z") GT 0` THEN\
  DEFINE SYMBOL result_type = point
GO LAS_results ($result_type)

! End of file ------------ oneD_compare_difference.jnl--------------------------

