! LAS_is_range
!  called by LAS_xytvar_setup
!
! Given a min and max variables defined, see if they are valid data
! and use them to define axis min,max, delta. If min and max are not
! valid then return nominal 0:100 and set an error flag.
! 2/25/2020 ACM robust method usin Ferret/PyFerret internal method 
!           to set range for constant-valued data

LET baddat = 0

CANCEL SYMBOL dm_labels

LET ok = IFV ($1) THEN 1 ELSE 0
IF `ok` THEN 
   DEFINE SYMBOL ax_lo = `($1)`
ELSE 
   DEFINE SYMBOl ax_lo = bad
ENDIF
LET ok = IFV ($2) THEN 1 ELSE 0
IF `ok` THEN 
   DEFINE SYMBOL ax_hi = `($2)`
ELSE 
   DEFINE SYMBOl ax_hi = bad
ENDIF


IF `($ax_lo"0|bad>1|*>0") OR ($ax_hi"0|bad>1|*>0")` THEN 
  LET ax_lo = 0
  LET amin = 0
  LET ax_hi = 100
  LET amax = 100
  LET del_ax = 100
  LET baddat = 1
  PPL axlabp,0,0
  PPL TICS,0,0,0,0
  DEFINE SYMBOL  put_label = 1
  EXIT/SCRIPT ! Data All Missing or Inconsistent Variable Constraints
ENDIF 

LET ax_lo = `$1`
LET ax_hi = `$2`

! Time is in seconds.
IF ($is_time"0|0|*>1") THEN
   LET since_loc = STRINDEX("`($data_t_var).units`","since") - 2
   IF `since_loc GT 1` THEN 
      LET tunits = SUBSTRING("`($data_t_var).units`",1,`since_loc`)
      DEFINE SYMBOL tunits = `tunits`
   ENDIF
   
   IF `($tunits"0|seconds>1|*>0) EQ 0` THEN
      MESSAGE/ERROR **ERROR Time is assumed to be in seconds, finding ($tunits)
      EXIT/PROGRAM
   ENDIF

   let hrsec = 3600
   let daysec = 24*3600

   IF ($constr_tim_min"0|*>1") THEN
      LET ax_lo = `(ax_lo - hrsec),prec=12`
      LET ax_hi = `(ax_hi +hrsec),prec=12`

   ELSE
      IF `ABS(($ax_hi)-($ax_lo)) LT 180*daysec` THEN
        ! do nothing
      ELIF `ABS(($ax_hi)-($ax_lo)) LT 365*daysec` THEN
         LET ax_lo = `(ax_lo - 15*daysec),prec=12`
         LET ax_hi = `(ax_hi + 15*daysec),prec=12`
      ELIF `ABS(($ax_hi)-($ax_lo)) LT 5*365*daysec` THEN
         LET ax_lo = `(ax_lo - 30*daysec),prec=12`
         LET ax_hi = `(ax_hi + 30*daysec),prec=12`
      ELSE
         LET ax_lo = `(ax_lo - 180*daysec),prec=12`
         LET ax_hi = `(ax_hi + 180*daysec),prec=12`
      ENDIF
   ENDIF

   LET del_ax = daysec
   IF `ax_hi - ax_lo LT 26*hrsec` THEN LET del_ax = hrsec
   
   IF `STRINDEX("($tunits)", "sec") GT 0` THEN LET del_ax = 3600

ELSE
   IF `ax_lo EQ ax_hi` THEN
      LET ax_lo = `ax_lo - 0.5`
      LET ax_hi = `ax_hi + 0.5`  
   ENDIF
   
!  PPL %RANGE `ax_lo`, `ax_hi`, 50
   PPL %RANGE `ax_lo`, `ax_hi`, 30
   IF `($ppl$range_low) EQ ($ppl$range_high)` THEN
      CANCEL SYMBOL yaxis*  ! Ferret adds +/- a bit for const value plots
      PLOT/SET {($ppl$range_low), ($ppl$range_high)}
      LET ax_lo = ($yaxis_min)
      LET ax_hi = ($yaxis_max)
      PPL %RANGE `ax_lo`, `ax_hi`, 30
   ELSE
      LET ax_lo = ($ppl$range_low)
      LET ax_hi = ($ppl$range_high)
   ENDIF
   LET del_ax = ($ppl$range_inc)
   IF `ABS(ax_hi - ax_lo) LT 1` THEN DEFINE SYMBOL dm_labels = dm
   IF `ABS(ax_hi - ax_lo) LT 0.06` THEN DEFINE SYMBOL dm_labels = dms

ENDIF
! End of file ------------ LAS_is_range.jnl--------------------------

