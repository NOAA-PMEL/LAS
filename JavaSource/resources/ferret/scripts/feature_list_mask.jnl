! feature_list_mask.jnl
! If a cruise list (trajectory list, profile list)
! is defined, set up the mask.
!
! Format the cruise list and define the mask.  Do this just once.

IF `($have_feature_mask"0|*>1") EQ 1` THEN EXIT/SCRIPT
IF `($ferret_cruise_list"0|*>1") EQ 0` THEN EXIT/SCRIPT

! For character feature-id variable need to convert the 
! list of ids to a list of quoted strings.

DEFINE SYMBOL vtype = `($feature_variable),RETURN=dtype`
IF ($vtype"0|CHAR>1|*>0") THEN 

   LET all_len = STRLEN("($ferret_cruise_list)")
   LET subset_len = `all_len`

   ! Initialize, first id in symbol all_subset

   DEFINE SYMBOL subset_list = ($ferret_cruise_list)
   LET s1 = 1
   LET s2 =  `STRINDEX("($subset_list)", ",")`
   IF `s2 EQ 0` THEN LET s2 = subset_len+1
   DEFINE SYMBOL one_cruise = "`SUBSTRING("($subset_list)", 1,s2-1)`"
   DEFINE SYMBOl all_subset = ($one_cruise)
   DEFINE SYMBOL subset_list= ($all_subset)

   IF `s2 NE 0` THEN 
      LET s1 = `s1+s2`
       IF `s1 LT all_len` THEN 
         DEFINE SYMBOL subset_list= `SUBSTRING("($ferret_cruise_list)",s1,subset_len-s2)`
         LET subset_len = STRLEN("($subset_list)")
      ENDIF
   ENDIF

! If more in the list, add them.

   IF `(s2 NE 0) AND (s1 LT all_len)` THEN
     REPEAT/RANGE=1:200 ( ;\
        LET s2 =  `STRINDEX("($subset_list)", ",")`;\
        IF `s2 EQ 0` THEN LET s2 = subset_len+1;\
        DEFINE SYMBOL one_cruise = "`SUBSTRING("($subset_list)", 1,s2-1)`";\
        DEFINE SYMBOl all_subset = ($all_subset),($one_cruise);\
        IF `s2 EQ 0` THEN EXIT/LOOP;\
        LET s1 = `s1+s2`;\
        IF `s1 GE all_len` THEN EXIT/LOOP;\
        DEFINE SYMBOL subset_list= `SUBSTRING("($ferret_cruise_list)",s1,subset_len-s2)`;\
        LET subset_len = STRLEN("($subset_list)");\
        )
   ENDIF

   LET subset_ids = {($all_subset)}

   ! Define the mask variable
   LET subset_mask =  ELEMENT_INDEX_STR ( ($feature_variable), subset_ids )

ELSE ! numeric variable

   list ($feature_variable)
   say ($ferret_cruise_list)

   LET subset_mask = if element_index ( ($feature_variable), {($ferret_cruise_list)}) then 1

ENDIF

! Apply the mask at the dataset level

SET DATA/FMASK=subset_mask ($dsetnum)

DEFINE SYMBOL features_shown = `subset_mask[e=@sum]`
DEFINE SYMBOL subset_mask = 1   ! mask is defined
LET feature_mask = subset_mask  ! used in the key when color-by-cruise

DEFINE SYMBOL have_feature_mask = 1

!---------------- end of feature_list_mask.jnl -------------------------
