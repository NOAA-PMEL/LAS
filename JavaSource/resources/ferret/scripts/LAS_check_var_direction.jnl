! LAS_check_var_direction.jnl
! 
! Author: ansley
! $Date: 2007/06/22
!
! Check whether the region is too small= and reduces to a single grid point.
! Call after region and dataset have been initialized for 1D plot.

! Do this only if this is not a native curvlinear or hybrid z plot.

IF `($do_curvi_xy"0") AND ($native_curvilinear_xy"0")` THEN EXIT/SCRIPT
IF `($do_hybrid_z"0") AND ($native_Z_plot"0")` THEN EXIT/SCRIPT
IF `($do_curvi_xy_and_hybrid_z)` THEN EXIT/SCRIPT

! See if there is an axis in this direction (may happen for a var in a multi-line plot)
DEFINE SYMBOL ax1d = `($data_var),RETURN=($ferret_view)axis`

IF `STRCMP("($ax1d)", "NORMAL") EQ 0` THEN
   DEFINE SYMBOL has_no_line = 1
   DEFINE SYMBOL error_status = ERROR
   DEFINE SYMBOL error_type = REGION
   DEFINE SYMBOL error_action = Checking grid and region  
   DEFINE SYMBOL error_string No ($ferret_view) axis for variable ($data_name)
   GO LAS_error_msg 
   EXIT/PROGRAM
ENDIF

! Or there may be a 1-point axis

DEFINE SYMBOL index_dir = ($ferret_view%N|x>I|y>J|z>K|t>L|*>N%)
IF `STRCMP("($index_dir)", "N") EQ 0` THEN EXIT/SCRIPT

LET tsiz = `($data_var),return=($index_dir)size`
IF `tsiz LE 1` THEN
   DEFINE SYMBOL has_no_line = 1
   DEFINE SYMBOL error_status = ERROR
   DEFINE SYMBOL error_type = REGION
   DEFINE SYMBOL error_action = Checking grid and region  
   MESSAGE/ERROR **ERROR DEFINE SYMBOL error_string No ($ferret_view) axis for variable ($data_name)
   GO LAS_error_msg 
   EXIT/PROGRAM
ENDIF

! Or the region may reduce to a single point.
! If doing a slice of a curvilinear variable then note that RETURN=shape is not
! always correct for these variables (dont exit, test works for some cases)
! IF `STRINDEX ("($ferret_plot_var)", "sampled_var") GT 0` THEN EXIT/SCRIPT


LET the_plot_var = ($ferret_plot_var)
LOAD the_plot_var
DEFINE SYMBOL varshape = `the_plot_var,RETURN=shape`
DEFINE SYMBOL viewshape =  `UPCASE("($ferret_view)")`
IF `STRCMP("($varshape)", "POINT") EQ 0` THEN
   DEFINE SYMBOL error_status = ERROR
   DEFINE SYMBOL error_type = REGION
   DEFINE SYMBOL error_action = Checking grid and region  
   MESSAGE/ERROR **ERROR The ($viewshape) region is too small. It reduces to a single point.<NL>\
   Variable ($data_name)
   GO LAS_error_msg 
  EXIT/PROGRAM
ENDIF

! For multi-line plot, if var has extra dimensons use IJKL=1 in the extra directions.
 
IF `STRLEN("($varshape)") GT 1` THEN
   IF `($data_count) GT 1` THEN  

      DEFINE SYMBOL  varshape0 = `($data_0_var)[d=($dset0)],RETURN=shape`
      DEFINE SYMBOL  varshape1 = `($data_1_var)[d=($dset1)],RETURN=shape`
      IF `STRLEN("($varshape1)") GT STRLEN("($varshape0)")` THEN
        IF `STRINDEX("($varshape0)", "X") EQ 0 AND STRINDEX("($varshape1)", "X") NE 0` THEN DEFINE SYMBOL missing_x = 1
        IF `STRINDEX("($varshape0)", "Y") EQ 0 AND STRINDEX("($varshape1)", "Y") NE 0` THEN DEFINE SYMBOL missing_y = 1
        IF `STRINDEX("($varshape0)", "Z") EQ 0 AND STRINDEX("($varshape1)", "Z") NE 0` THEN DEFINE SYMBOL missing_z = 1
        IF `STRINDEX("($varshape0)", "T") EQ 0 AND STRINDEX("($varshape1)", "T") NE 0` THEN DEFINE SYMBOL missing_t = 1
      ENDIF

      IF ($missing_x"0|*>1") THEN 
         IF ($native_curvilinear_xy"0") THEN
            DEFINE SYMBOL error_status = ERROR
            DEFINE SYMBOL error_type = REGION
            DEFINE SYMBOL error_action = Checking grid and region  
            DEFINE SYMBOL error_string Variable ($data_name) on ($region_full) is not a ($viewshape) line
            GO LAS_error_msg 
            EXIT/PROGRAM
	 ENDIF
         DEFINE SYMBOL region_full ($region_full),I=1
	 DEFINE SYMBOL region_xyz ($region_xyz),I=1
	 DEFINE SYMBOL region_x ($region_x),I=1
	 IF `(STRCMP("($fview%A%)", "X") NE 0) AND ($have_lon_lab"0|0|1|*>1") EQ 0` THEN
	    DEFINE SYMBOL n_left_labels = `($n_left_labels"0") + 1`
	    DEFINE SYMBOL upper_left_($n_left_labels"0") = Longitude `($data_var),RETURN=xstart`
	    DEFINE SYMBOL longitude_0_lab = ($upper_left_($n_left_labels"0"))
	 ENDIF
      ENDIF
      IF ($missing_y"0|*>1") THEN 
         IF ($native_curvilinear_xy"0") THEN
            DEFINE SYMBOL error_status = ERROR
            DEFINE SYMBOL error_type = REGION
            DEFINE SYMBOL error_action = Checking grid and region  
            DEFINE SYMBOL error_string Variable ($data_name) on ($region_full) is not a ($viewshape) line
            GO LAS_error_msg 
            EXIT/PROGRAM
	 ENDIF
         DEFINE SYMBOL region_full ($region_full),J=1
	 DEFINE SYMBOL region_xyz ($region_xyz),J=1
	 IF `(STRCMP("($fview%A%)", "X") NE 0) AND ($have_lon_lab"0|0|1|*>1") EQ 0` THEN
	    DEFINE SYMBOL n_left_labels = `($n_left_labels"0") + 1`
	    DEFINE SYMBOL upper_left_($n_left_labels"0") = Latitude `($data_var),RETURN=ystart`
	    DEFINE SYMBOL latitude_0_lab = ($upper_left_($n_left_labels"0"))
	 ENDIF
      ENDIF
      IF ($missing_z"0|*>1") THEN 
         DEFINE SYMBOL region_full ($region_full),K=1
	 DEFINE SYMBOL region_xyz ($region_xyz),K=1
	 IF `(STRCMP("($fview%A%)", "Z") NE 0) AND ($have_z_lab"0|0|1|*>1") EQ 0` THEN
 	    DEFINE SYMBOL n_left_labels = `($n_left_labels"0") + 1`
	    DEFINE SYMBOL upper_left_($n_left_labels"0") = Depth `($data_var),RETURN=zstart` (`($data_var),RETURN=zunits`)
	    DEFINE SYMBOL have_z_lab = 1
	    DEFINE SYMBOL depth_0_lab = ($upper_left_($n_left_labels"0"))
	 ENDIF
      ENDIF

      IF ($missing_t"0|*>1") THEN 
         DEFINE SYMBOL region_full ($region_full),L=1
	 DEFINE SYMBOL region_t ($region_t),L=1
	 IF `(STRCMP("($fview%A%)", "T") NE 0) AND ($have_time_lab"0|0|1|*>1") EQ 0` THEN
	    DEFINE SYMBOL n_left_labels = `($n_left_labels"0") + 1`
	    DEFINE SYMBOL upper_left_($n_left_labels"0") = `($data_var),return=tstart,prec=5`
	    DEFINE SYMBOL have_time_lab = 1
	    DEFINE SYMBOL have_year_lab = 1
	    DEFINE SYMBOL time_0_lab = ($upper_left_($n_left_labels"0"))
	 ENDIF
      ENDIF
      GO LAS_redefine_variable


   ELSE
      DEFINE SYMBOL error_status = ERROR
      DEFINE SYMBOL error_type = REGION
      DEFINE SYMBOL error_action = Checking grid and region  
      DEFINE SYMBOL error_string Variable ($data_name) on ($region_full) is not a ($viewshape) line
      GO LAS_error_msg 
      EXIT/PROGRAM
   ENDIF
ENDIF
      
! End of file ------------LAS_check_var_direction.jnl-------------------------------

