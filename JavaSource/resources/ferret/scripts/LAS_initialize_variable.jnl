!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! $RCSfile: LAS_initialize_variable.jnl
! $Revision: 1.1
! 
! $Author: Ansley, Jing $
! $Date: 2007/29/2007
! ACM 4/2007 apply ferret_expression2 to second plot of comparisons.
! ACM 8/2007 Call LAS_auto_levels to set up auto contour/fill levels.
! 3/2008 ACM Use SHADE/TRIM for shade underlays of FILL plots (Ferret v6.1),
!            so no need to call LAS_define_region_plus_1.jnl
! 10-Jul-2008 ACM Define ferret_x_var, needed for listing data to files
!  3-Jun-2011 ACM Allow configuration to send in ferret_factor and 
!             ferret_factor_label to apply factor to variables and to add
!             a nice label to the title/units string.

! this is the second part of LAS_initialize_data.jnl

! Define the plot variable. For comparison plots, we want to save the dataset number
! for each dataset.


DEFINE SYMBOL data_num = `($data_var),RETURN=dsetnum`
DEFINE SYMBOL data_($num)_num = `($data_var),RETURN=dsetnum`

! If a factor has been passed in, apply it
IF ($ferret_factor"0|*>1") THEN
   IF `($redefined_($data_($num)_var)_factor"0|*>1") EQ 0` THEN
      SET var/name=input_($data_($num)_var) ($data_($num)_var)
      LET/D=($data_num) ($data_($num)_var) = ($ferret_factor)* input_($data_($num)_var)
   ENDIF
   SET ATT/like=input_($data_($num)_var) ($data_($num)_var)  ! will annotate units explicitly, below.
   DEFINE SYMBOL redefined_($data_($num)_var)_factor = 1
ENDIF

! This syntax needs the variable without quotes even if its a reserved name.
LET attlist = ($data_($num)_var).attnames

IF ($data_var_nq"0|*>1") THEN LET attlist = ($data_var_nq).attnames
DEFINE SYMBOL data_var_only = `UPCASE("($data_var)")`

IF ($ferret_bad_flag"0|*>1") THEN SET VAR/BAD=($ferret_bad_flag) ($data_var)

IF ($ferret_timelab_res"0|*>1") THEN SET MODE CALENDAR:($ferret_timelab_res)

! setup for analysis ops on curvilinear grids 
IF `IS_ELEMENT_OF_STR (attlist, "ferret_definition") GT 0 AND\
    ($ferret_curvi_coord_lon"0|*>1")` THEN
   LET its_x_analysis = STRINDEX("($data_($num)_title)", "x=") 
   LET its_y_analysis = STRINDEX("($data_($num)_title)", "y=")
   IF `its_x_analysis OR its_y_analysis ` THEN
      LET data_var_definition = `($data_var).ferret_definition`
      DEFINE SYMBOL data_var_def = `($data_var).ferret_definition`
      DEFINE SYMBOL data_var = data_var_definition

! If there is an X or Y analysis operation then the X or Y region has
! Not been set. Get the X and/or Y analysis request, set up 
! x an y region definitions, and prepare to do the request either
! by finding the region in the rectiliear portion of the grid
! or by first doing a curvi-to-rectilnear regridding operation.

      GO LAS_set_x_y_analysis.jnl
   ENDIF
ENDIF

! Be sure units and title are defined.
! If units are not defined, try to get units from the data variable.
! If the variable in the file has no units, leave data_units symbol undefined.

! data_units should have been defined in LAS_initialize_dataset

IF ($data_units"0|*>1") THEN
   DEFINE SYMBOL has_units = 1
ELSE
   IF `IS_ELEMENT_OF_STR (attlist, "units") OR IS_ELEMENT_OF_STR (attlist, "UNITS")` THEN
      DEFINE SYMBOL data_var_units `($data_var),RETURN=units`123   ! check for empty units string.
      IF `STRLEN("($data_var_units)" ) EQ 3` THEN
         CANCEL SYMBOL data_var_units
      ELSE
         DEFINE SYMBOL data_var_units `($data_var),RETURN=units`
         DEFINE SYMBOL data_units (`($data_var),RETURN=units`)
      ENDIF
   ENDIF
ENDIF

IF ($check_for_pseudonames"0|*>1") THEN
   LET pseudo_names = {\
"'i'", "'j'", "'k'", "'l'", "'x'", "'y'", "'z'", "'t'", "'xbox'", "'ybox'", "'zbox'", "'tbox'", \
"'xboxlo'", "'yboxlo'", "'zboxlo'", "'tboxlo'", "'xboxhi'", "'yboxhi'", "'zboxhi'", "'tboxhi'",  \
"'I'", "'J'", "'K'", "'L'", "'X'", "'Y'", "'Z'", "'T'", "'XBOX'", "'YBOX'", "'ZBOX'", "'TBOX'", \
"'XBOXLO'", "'YBOXLO'", "'ZBOXLO'", "'TBOXLO'", "'XBOXHI'", "'YBOXHI'", "'ZBOXHI'", "'TBOXHI'"}

   IF `IS_ELEMENT_OF_STR("($data_var)",pseudo_names) `  THEN
      LET slen = STRLEN("($data_var)")
      DEFINE SYMBOL data_var_nq = `SUBSTRING("($data_var)", 2, slen-2)`
      LET ($data_var_nq)_1 = ($data_var)
      SET ATT/LIKE=($data_var) ($data_var_nq)_1
      DEFINE SYMBOL data_var ($data_var_nq)_1
   ENDIF
ENDIF

IF ($ferret_factor_label"0|*>1") THEN 
   DEFINE SYMBOL ferret_plot_title  "($data_title) ($data_units) ($ferret_factor_label)"
   IF `($num) GT 0` THEN DEFINE SYMBOL ferret_plot_title  "($data_title) ($data_units) ($ferret_factor_label)"
ELIF ($ferret_factor"0|*>1") THEN 
   DEFINE SYMBOL ferret_plot_title  "($data_title) ($data_units) (x($ferret_factor))"
ENDIF

IF `($data_title"0|*>1") EQ 0` THEN
  IF `($has_units"0|*>1") EQ 0` THEN 
     SYMBOL has_units = STRCMP (" ", "`($data_var),return=units`")
     SYMBOL has_units = `($has_units) OR ($data_units"0|*>1")`
  ENDIF
  IF ($has_units"0|*>1") THEN \
  DEFINE SYMBOL ferret_plot_title "($data_var) ($data_units)"
  CANCEL SYMBOL has_units
ENDIF


DEFINE SYMBOL ferret_plot_var = ($data_var)[d=($data_num),($region_full)]
IF ($analysis_def"0|*>1") THEN  ! analysis_def from curvi data or transform
   LET fplot_var = ($ferret_plot_var)
   DEFINE SYMBOL ferret_plot_var = fplot_var($analysis_def)
ENDIF

DEFINE SYMBOL ferret_plot_var_noregion = ($data_var)[d=($data_num)]
DEFINE SYMBOL ferret_plot_var_($num)_noregion = ($data_var)[d=($data_num)]

DEFINE SYMBOL ferret_plot_base_var = ($ferret_plot_var) 
DEFINE SYMBOL ferret_plot_base_var_($num) = ($ferret_plot_var) 
DEFINE SYMBOL ferret_plot_base_var_noregion = ($data_var)[d=($data_num)]

! Needed for transform_seasonal, save variable definition without any
! time-region information that may be added, and also with the time region only

DEFINE SYMBOL ferret_xyz_var = ($data_var)[d=($data_num),($region_xyz)]
DEFINE SYMBOL ferret_t_var = ($data_var)[d=($data_num),($region_t)]

! ferret_x_var needed for listing data to files
DEFINE SYMBOL ferret_x_var = ($data_var)[d=($data_num),($region_x)]
IF `($region_x"0|*>1) EQ 0` THEN DEFINE SYMBOL ferret_x_var = ($data_var)[d=($data_num)]

! Apply any transforms that may have been specified.
GO LAS_transforms ($num)

! Get any DEFINE VARIABLE analysis definitions
! This renames the data variable, and so redefines the symbol data_var
! and sets up the analysis: e.g. ,z=0:2000@ave

! unComment this for testing.

IF ($data_0_analysis_label"0|*>1") THEN 
   GO LAS_analysis
   DEFINE SYMBOL ferret_plot_var = ($data_var)[d=($data_num),($region_full)($data_analysis_expr)]
   DEFINE SYMBOL ferret_plot_var_noregion = ($data_var)[d=($data_num)($data_analysis_expr)]
   IF ($data_0_analysis_oceanmask"0|*>1") THEN 
     LET/d=($data_num) plot_var =  ($data_var)[d=($data_num)] * analysis_mask
     DEFINE SYMBOL ferret_plot_var = plot_var[($region_full)($data_analysis_expr)]
     DEFINE SYMBOL ferret_plot_var_noregion = plot_var[($data_analysis_expr)]
   ENDIF
ENDIF


! (check for Google Earth plots is inside LAS_set_strides)
IF `($num)+1 EQ ($data_count)` THEN GO LAS_set_strides.jnl

! to set non-native strides for dataset 1 on a difference plot.
IF `($no_native_strides_xy"0|*>1") AND ($set_ndx_strides"0|*>1") EQ 0` THEN 
   GO LAS_set_strides
ENDIF

! Set up color levels
GO LAS_initialize_var_levels

! End of $RCSfile ------------LAS_initialize_variable.jnl--------------------------
