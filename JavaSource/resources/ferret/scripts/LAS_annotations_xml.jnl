! LAS_annotations_xml.jnl
! write xml for annotations that are taken off the plot
! and given back to LAS for display

! Make a label with the Ferret and LAS version
GO LAS_ferret_las_version_header

!CANCEL MODE VERIFY
! Remove duplicate labels
!  If dataset name and URL are the same, list just once
IF `STRCMP("($dataset_url_lab%0)", "($dataset_title_lab%99%)") EQ 0` THEN CAN SYM dataset_url_lab

! If the url-label doesnt really contain a url, get rid of it.

IF ($dataset_title_lab%0|*>1%) THEN
  IF `STRINDEX("($dataset_url_lab%0%)", "http") EQ 0` THEN CANCEL SYMBOL dataset_url_lab
ENDIF


! IF there is a dataset-difference title, use it.
IF ($ferret_diff_list_title"0|*>1") THEN DEFINE SYMBOL dataset_title_lab = ($ferret_diff_list_title)


! Write the header line

DEFINE SYMBOL out = <?xml version=\"1.0\" encoding=\"ISO-8859-1\" ?>
MESSAGE/QUIET/CONTINUE/OUTFILE="($result_annotations_filename)"/CLOBBER ($out)

! Set up list qualifiers
DEFINE SYMBOL lquals = /NOHEAD/NOROWLAB/FILE="($result_annotations_filename)"/FORMAT=(a)/APPEND
DEFINE SYMBOL out = <annotations>
GO write_to_xml

! orthogonal axis labels
IF `($longitude_lab%0|*>1%)+($latitude_lab%0|*>1%)+($depth_lab%0|*>1%)+($time_lab%0|*>1%)+($ens_lab%0|*>1%)+($forecast_lab%0|*>1%) GT 0` THEN \
    GO annotation_orthogonal_axes 

! variable and dataset labels
IF `($variable_lab%0|*>1%)+($dataset_title_lab%0|*>1%)+($dataset_url_lab%0|*>1%) GT 0` THEN \
    GO annotation_data

! LAS/Ferret version header line
IF ($las_ferret_header_label"0|*>1") THEN
   DEFINE SYMBOL out = <annotation_group type="las">
   GO write_to_xml
   DEFINE SYMBOL out = <annotation type="header">
   GO write_to_xml
   DEFINE SYMBOL out = `LIST_VALUE_XML("value", "($las_ferret_header_label)", 1, "($result_annotations_filename)" )`
   DEFINE SYMBOL out = </annotation>
   GO write_to_xml
   DEFINE SYMBOL out = </annotation_group>
   GO write_to_xml
ENDIF

! Other notes that may have been set up.
! The symbol note_($mnote)_lab_quoted indicates that
! string(s) containing an apostrophe or other reserved
! character have been replaced by an innocuous string
! and the actual value needs to be inserted befor writing.

IF `($note_num"0") GT 0 OR ($calendar_lab"0|*>1") OR ($dataset_institute_lab"0|*>1")` THEN
   DEFINE SYMBOL out = <annotation_group type="notes">
   GO write_to_xml

   IF `($note_num"0") GT 0` THEN
      REPEAT/RANGE=1:($note_num)/NAME=m (\
         DEFINE SYMBOL mnote = `m`; \
	 IF `($note_($mnote)_lab"0|*>1") EQ 0` THEN EXIT/CYCLE; \
         DEFINE SYMBOL out = <annotation type="note">; \
         GO write_to_xml; \
	 CANCEL SYMBOL qn; \
	 IF ($note_($mnote)_lab_quoted"0|*>1") THEN\
	    REPEAT/RANGE=1:5/NAME=q (GO set_quoted_annotation ($mnote); IF `still_quoted EQ 0` THEN EXIT/LOOP); \
	 ; \
	 sh var note_($mnote)_lab; \
	 DEFINE SYMBOL note_status = `note_($mnote)_lab,return=status`; \
	 DEFINE SYMBOL note_ready = `($note_status"0|AVAILABLE>1|*>0")`; \
	 IF ($note_ready) THEN ; \
         DEFINE SYMBOL out = `LIST_VALUE_XML("value", note_($mnote)_lab, 1, "($result_annotations_filename)" )`; \
	 ELSE; \
         DEFINE SYMBOL out = `LIST_VALUE_XML("value", "($note_($mnote)_lab)", 1, "($result_annotations_filename)" )`; \
	 ENDIF; \
         DEFINE SYMBOL out = </annotation>; \
         GO write_to_xml; \
       )
    ENDIF

    IF ($calendar_lab"0|*>1") THEN
      DEFINE SYMBOL out = <annotation type="note">
      GO write_to_xml
      DEFINE SYMBOL out = `LIST_VALUE_XML("value", "($calendar_lab)", 1, "($result_annotations_filename)" )`
      DEFINE SYMBOL out = </annotation>
      GO write_to_xml
   ENDIF
   IF ($dataset_institute_lab"0|*>1") THEN
      DEFINE SYMBOL out = <annotation type="note">
      GO write_to_xml
      DEFINE SYMBOL out = `LIST_VALUE_XML("value", "Institution: ($dataset_institute_lab)", 1, "($result_annotations_filename)" )`
      DEFINE SYMBOL out = </annotation>
      GO write_to_xml
   ENDIF

   DEFINE SYMBOL out = </annotation_group>
   GO write_to_xml
ENDIF

! closing tag
DEFINE SYMBOL out = </annotations>
GO write_to_xml


!-----------------End of LAS_annotations_xml ---------------------------------

