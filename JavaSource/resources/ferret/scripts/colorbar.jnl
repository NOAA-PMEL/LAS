!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! File: colorbar.jnl
! 
! 
! $Author: ansley $
! $Date: Feb 27, 2007
! 
!
! colorbar.jnl creates a colorbar only plot for use with the Back End Server
! code that sits behind a Live Access Server (LAS). 
!
! usage:   GO colorbar
! 
! The script is called after the dataset and region have been
! initialized.
!
! If no levels were specified in the symbol ferret_fill_levels, compute 
! a set of contour levels, from the data minumum and maximum.
!
! skip the computation if levels have already been specified.

! If the plot is a native curvilinear XY plot then ($ferret_plot_var) has 3 arguments.
! in this case use the variable defined by plot_var. Get the range of this variable
! that was needed to plot the variable.

IF ($native_curvilinear_xy"0|*>1") THEN  ! see LAS_transforms.jnl   
   GO get_curv_coord_bbox
   LET the_var = plot_var[d=($data_num),i=($clon_imin):($clon_imax),j=($clat_jmin):($clat_jmax)]

ELSE
   LET the_var = ($ferret_plot_var)

ENDIF

IF ($ferret_fill_levels"1|*>0") THEN

   LOAD the_var
   LET vmin = the_var[x=@min,y=@min,z=@min,t=@min]
   LET vmax = the_var[x=@max,y=@max,z=@max,t=@max]
   PPL %RANGE `vmin,p=9` `vmax,p=9` 10
   DEFINE SYMBOL lo_lim ($PPL$RANGE_LOW)
   DEFINE SYMBOL hi_lim ($PPL$RANGE_HIGH)
   DEFINE SYMBOL delta ($PPL$RANGE_INC)
   DEFINE SYMBOL ferret_fill_levels (($PPL$RANGE_LOW),($PPL$RANGE_HIGH),($PPL$RANGE_INC))

ENDIF

! Draw the color bar.
SET WINDOW/SIZ=0.1/ASPECT=3

! If range was called we can define  a variable that is all blank
! If levels were specified, maybe (20c) then let the plot be drawn, just tiny.
LET blank_var = the_var 
IF  ($PPL$RANGE_LOW"0|*>1") THEN LET blank_var = IF the_var THEN ( ($PPL$RANGE_LOW) - 10 )

! Draw the colorbar using the levels and the palette that will be used for the main plot.

DEFINE SYMBOL colorbar_quals \
  NOAXES/NOLABELS/LEVELS=($ferret_fill_levels)/PALETTE=($ferret_palette"rainbow|default>rainbow|*>*)
IF ($ferret_curvi_quals"0|*>1") THEN \
  DEFINE SYMBOL colorbar_quals = ($colorbar_quals)($ferret_curvi_quals)


SHADE/SET/($colorbar_quals) blank_var
   PPL AXLEN 0.01 0.01
   LET bar_wid = ($PPL$WIDTH)* 0.1
   PPL SHAKEY 1,1,0.2,,,,`(($PPL$WIDTH)/2)-bar_wid`,`(($PPL$WIDTH)/2)+bar_wid`,1,`($PPL$HEIGHT)-1`
   PPL SHADE

! ?? save the result as a gif image -- needs a name.

! IF ($result_plot_image_filename%0|*>1%) THEN FRAME/FORMAT=gif/FILE="colorbar_($result_plot_image_filename)"
IF ($result_colorbar_filename%0|*>1%) THEN FRAME/FORMAT=gif/FILE="($result_colorbar_filename)"

