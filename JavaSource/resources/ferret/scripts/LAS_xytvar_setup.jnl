! LAS_xytvar_setup.jnl 
! ACM 3/2009
! 5/2014  Fixes to XY map plots
! 10/2016 Performance fixes. The underlay plot can be on a coarser grid 
!         while the overlay will be done with full resolution data.
! 10/2017 Writing a note when data selection results in no data
!         Here, if we already make such a note, xx var is all-missing,
!         set a symbol to skip the new annotation.
! 11/2017 find direction variables based on attributes instead of assuming
!         longitude, latitude, and time are variable names
! 12/2017 Define varibles for min,max of p and q for efficiency
!  1/2018 Time will only be the independent variable - no vertical time axis.
!  2/2018 Make a blank plot and exit immediately on finding a var is all-bad.
!  2/2018 Label thumbnails var vs time with the variable title.
!  2/2018 issue #1859 handle super short time ranges. Defining the time axis if < 2 hours

! Draw the underlying axes of a property-property plot,
! formatted as a longitude, latitude or time axis.

! variable p is the prop_var (independent axis) masked for any subset chosen and
!  with XSEQUENCE applied to put it on an x axis
! variable q is the data_var (dependent axis) masked for any subset chosen and
!  with XSEQUENCE applied to put it on an x axis

! Define two viewports filling the window and do the axes in one, the 
! plot in the other. If the annotations are being done separately,
! draw in the viewports defined in set_full_margins.jnl

! May turn off axis labeling later
PPL TICS .6,.125,.6,.125
PPL TICS .125,.2,.125,.2
PPL axlabp,-1,-1

!! TESTING; do we want to only constrain the map for constraints, not for
!! chosen trajectories?

DEFINE SYMBOL xvar = constraints_mask*xvar
DEFINE SYMBOL yvar = constraints_mask*yvar
LET p = ($xvar) 
LET q = ($yvar)

! Are the variables longitude and latitude (when the dataset is not of trajectory type)
! If so then connect the dots and do not draw the regression line

DEFINE SYMBOL propunits = none`($ferret_prop_var),return=units`
DEFINE SYMBOL plotunits = none`($ferret_plot_var),return=units`
IF `STRLEN("($propunits)") GT 4` THEN DEFINE SYMBOL propunits = `($ferret_prop_var),return=units`
IF `STRLEN("($plotunits)") GT 4` THEN DEFINE SYMBOL plotunits = `($ferret_plot_var),return=units`
LET itsa_lon_prop = `STRINDEX("($propunits)", "east") GT 0 OR STRINDEX("($data_0_units)", "east") GT 0`
LET itsa_lat_plot = `STRINDEX("($plotunits)", "north") GT 0 OR STRINDEX("($data_1_units)", "north") GT 0` 

GO LAS_var_lon_lat_time ($ferret_prop_var)

LET itsa_lon_prop = ($var_is_lon"0")
LET itsa_lat_prop = ($var_is_lat"0")
LET itsa_tim_prop = ($var_is_time"0")

GO LAS_var_lon_lat_time ($ferret_plot_var)

LET itsa_lon_plot = ($var_is_lon"0")
LET itsa_lat_plot = ($var_is_lat"0")
LET itsa_tim_plot = ($var_is_time"0")


IF `itsa_lon_prop + itsa_lat_plot EQ 2` THEN 
   DEFINE SYMBOL connect_dots = 1
   DEFINE SYMBOL do_rsquare = 0
ENDIF

CANCEL SYMBOL set_window_quals

! If they are not lon, lat, or time, put axis labels on the plot.
! (easier to understand, e.g. SST as a fcn of AIRT)

IF ($variable_0_lab"0|*>1") THEN 
   DEFINE SYMBOL ax_label_x PPL XLAB @AC($variable_0_lab)
   IF `itsa_tim_prop OR itsa_lon_prop OR itsa_lat_prop`  THEN CANCEL SYMBOL ax_label_x
ENDIF

IF ($variable_1_lab"0|*>1") THEN 
   DEFINE SYMBOL ax_label_y PPL YLAB @AC($variable_1_lab)
   IF `itsa_lon_plot OR itsa_lat_plot` THEN  CANCEL SYMBOL ax_label_y
ENDIF


! If the axis labels are long, make the text size smaller
LET labfac = 1.
if `(labwid("($ax_label_x)", 0.12) / ($ppl$xlen)) GT 1` THEN LET labfac = ($ppl$xlen)/labwid("($ax_label_x)",0.12)
if `(labwid("($ax_label_y)", 0.12) / ($ppl$ylen)) GT 1` THEN LET labfac = MIN(`labfac`,`($ppl$ylen)/labwid("($ax_label_y)",0.12)`)
IF `labfac LT 1 AND labfac GT 0.06` THEN DEFINE SYMBOL ax_label_resize PPL LABSET,,`0.12*labfac`,`0.12*labfac`

! Do not label axes with the words LONGITUDE, LATITUDE
IF `itsa_lon_prop OR itsa_lat_prop`  THEN DEFINE SYMBOL ax_label_x = ppl xlab
IF `itsa_lon_plot OR itsa_lat_plot` THEN DEFINE SYMBOL ax_label_y = ppl ylab


! size for the label that says a variable is all-missing
DEFINE SYMBOL miss_lab_siz = 0.14

! For thumbnail plots
DEFINE SYMBOL mainlab_size = 0.14
DEFINE SYMBOL errlab_size = 0.16
IF ($ferret_thumb"0|0|*>1") THEN  
   DEFINE SYMBOL poly_size = 1.3
   DEFINE SYMBOL is_thumb = 1
   DEFINE SYMBOL mainlab_size = 0.22
   DEFINE SYMBOL errlab_size = 0.2
   DEFINE SYMBOL thumb_alab_size = PPL AXLSZE,.14,.14
   DEFINE SYMBOL txl_lab_size = .14
   PPL TXLSZE,($txl_lab_size)
   LET labfac = 1.8
   DEFINE SYMBOL ax_label_resize PPL LABSET,,`0.12*labfac`,`0.12*labfac`
   DEFINE SYMBOL miss_lab_siz = 0.24

   DEFINE SYMBOL do_rsquare = 0

ENDIF


! If this has already been done, just go make the underlay plot
IF `($axes_already_defined"0|*>1") EQ 0` THEN


DEFINE SYMBOL win_asp = /ASPECT=.8

IF `itsa_tim_prop` THEN DEFINE SYMBOL win_asp = /ASPECT=0.75
IF `itsa_lon_prop AND itsa_lat_plot` THEN DEFINE SYMBOL win_asp = /ASPECT=`($region_Y_range)/($region_X_range)`

DEFINE SYMBOL set_window_quals = /SIZE=($ferret_size"0.5")($win_asp)

DEFINE VIEWPORT/XLIM=0:1/YLIM=0:1 vfull1
DEFINE VIEWPORT/XLIM=0:1/YLIM=0:1 vfull2

DEFINE SYMBOL view1 = vfull1
DEFINE SYMBOL view2 = vfull2
IF ($ferret_annotations"0|*>1") THEN
   DEFINE SYMBOL view1 = full_view
   DEFINE SYMBOL view2 = full_view2
ENDIF

SET VIEW ($view1)

! Depth on the vertical axis?  We'll want to make it increasing downwards.

LET attnames = ($ferret_plot_var_nq_noregion%($ferret_plot_var)%).attnames

IF `IS_ELEMENT_OF_STR_N(attnames, "positive")` THEN 
   DEFINE SYMBOL posdirec = `($ferret_plot_var_nq_noregion%($ferret_plot_var)%).positive`
   DEFINE SYMBOL reverse_vert = ($posdirec"0|DOWN>1|down>1|*>0")
ENDIF

! Look for a standard_name of depth, or one of the sea_floor_depth_below names:
IF `IS_ELEMENT_OF_STR_N(attnames, "standard_name")` THEN 
   DEFINE SYMBOL posdirec = `($ferret_plot_var_nq_noregion%($ferret_plot_var)%).standard_name`
   DEFINE SYMBOL reverse_vert = ($posdirec"0|DEPTH>1|depth>1|*>0")
   IF `STRINDEX("($posdirec)", "sea_floor_depth") GT 0` THEN DEFINE SYMBOL reverse_vert = 1
ENDIF

! If this symbol is set during this script, then
! the script will make a blank plot and exit.
CANCEL SYMBOL put_label

! For the line connecting points in each feature, color it grey.
PPL COLOR,6,60,60,60 ! grey


let pq = p*q
let pqmask = pq-pq
let p_var = p + pqmask
let q_var = q + pqmask

! Define some variables used below
LET pminmax = MINMAX(p_var)
LET qminmax = MINMAX(q_var)

! Check for all-bad data. pminmax and qminmax depend on both p and q.
! If bad, figure out which variable is all-bad and set the label string. 

DEFINE SYMBOL isbad =  `pminmax[i=2] - pminmax[i=1] `
IF ($isbad"0|bad>1|*>0")  THEN 
   DEFINE SYMBOL qgood = `q[i=@ngd] GT 0`
   DEFINE SYMBOL pgood = `p[i=@ngd] GT 0`
   DEFINE SYMBOL bad_verb = is
   IF `($pgood) EQ 0 AND ($qgood) NE 0` THEN DEFINE SYMBOL bad_title = ($ferret_title_0) 
   IF `($pgood) NE 0 AND ($qgood) EQ 0` THEN DEFINE SYMBOL bad_title = ($ferret_title_1) 
   IF `($pgood) EQ 0 AND ($qgood) EQ 0` THEN 
      DEFINE SYMBOl bad_verb = are
      DEFINE SYMBOL bad_title = ($ferret_title_0) and <nl>($ferret_title_1) 
   ENDIF
ENDIF

IF ($bad_title"0|*>1") THEN
   PLOT/VS/NOLAB/SYM=dot {0,1}, {0,1}
   IF ($ferret_thumb"0|0|*>1") THEN  
      LET ylab_bad = `($ppl$ylen)-3*($miss_lab_siz)`
      LABEL/NOUSER 0.2, `ylab_bad-0.4`, -1, 0, ($miss_lab_siz), @AS($bad_title)<NL>($bad_verb) All-Missing
      DEFINE SYMBOL no_novalid_lab = 1
      CANCEL SYMBOL bad_title
   ELSE
      LET ylab_bad = `($ppl$ylen)-2*($miss_lab_siz)`
      LABEL/NOUSER 0.2, `ylab_bad-0.2`, -1, 0, ($miss_lab_siz), @AS($bad_title) ($bad_verb) All-Missing
      DEFINE SYMBOL no_novalid_lab = 1
      CANCEL SYMBOL bad_title
   ENDIF
   DEFINE SYMBOL ngood_data = -1   ! Dont do another label in LAS_results.
   EXIT/SCRIPT
ENDIF

! Define axes for both variables using the range in the data

!----------
! Possibly redefine longitude to lon360 depending on the longitude range.
! Do this first, as the constraints are based on lon in 0-360. Then the
! constrained variable will be correct. (This fixes ticket 1623).

IF `itsa_lon_prop` THEN 

   DEFINE SYMBOL is_lon = 1
   DEFINE SYMBOL lon_units = `($ferret_prop_var),RETURN=units`

! Get min and max longitudes and create a LON axis.
! All this lon360 stuff is to deal with cases where longitude data is on 
! both sides of the dateline.

   LET amin = pminmax[i=1]
   LET amax = pminmax[i=2]
   let adel = `0.1*(amax-amin),prec=7`
   LET amin = MIN(`amin` - adel, 180)
   LET amax = MAX(`amax` + adel, -180)

! Initialize   
   LET ax_lo = ($region_x_lo)
   LET ax_hi = ($region_x_hi)

   IF `amax-amin LT ax_hi-ax_lo` THEN 
      LET ax_lo = amin
      let ax_hi = amax
   ENDIF

! make sure del_ax is defined
   GO LAS_is_range ax_lo ax_hi ! get del_ax

   LET varnames = ..varnames
   IF `IS_ELEMENT_OF_STR(varnames, "lon360") GT 0` THEN
      LET/UNITS="($lon_units)" xvar_360 = lon360
   ELSE
      LET/UNITS="($lon_units)" xvar_360 = IF ($ferret_prop_var) LT 0 THEN ($ferret_prop_var)+360 ELSE ($ferret_prop_var)
   ENDIF
   
   LET xvminmax360 = MINMAX(xvar_360)
   LET amin360 = xvminmax360[i=1]
   LET amax360 = xvminmax360[i=2]
   let adel = `0.1*(amax360-amin360),prec=7`
   LET amin = MIN(`amin360 - adel`, 360)
   LET amax = MAX(`amax360 + adel`, 0)

    LET adiff = amax - amin

   IF `(amax360 - amin360) LT adiff` THEN 

      LET ax_lo360 = xvminmax360[i=1]
      LET ax_hi360 = xvminmax360[i=2]
      LET ax_lo360 = `MIN(ax_lo360-adel, 360)` 
      LET ax_hi360 = `MAX(ax_hi360+adel, 0)`

      IF `(ax_hi360-ax_lo360) LT (ax_hi-ax_lo)` THEN 
         LET xvar = xvar_360
         DEFINE SYMBOL pvar_reset = xvar_360
         SET VAR/NAME=($ferret_prop_var)_in ($ferret_prop_var)
         LET/UNITS="($lon_units)"/d=1 ($ferret_prop_var) = IF ($ferret_prop_var)_in LT 0 THEN ($ferret_prop_var)_in+360 ELSE ($ferret_prop_var)_in
         CANCEL SYMBOL pvar_reset
         LET/UNITS="($lon_units)" xvar_360 = ($ferret_prop_var)
         LET ax_lo = `ax_lo360`
         LET ax_hi = `ax_hi360`
      ENDIF

      CANCEL SYMBOL put_label  ! an all-missing label may not be needed if we are using LON+360
      LET amin = ax_lo
      LET amax = ax_hi
      GO LAS_is_range ax_lo ax_hi ! get del_ax
      LET ax_lo = `amin`
      let ax_hi = `amax`

      IF `(ax_hi-ax_lo) GE 359` THEN 
         LET ax_lo = ($region_x_lo)
         LET ax_hi = ($region_x_hi)
      ENDIF

   ELSE  ! case with 360 degree range
      IF `((ax_hi-ax_lo) GT 350) AND (ax_lo LT (-178))` THEN
         LET/UNITS="($lon_units)" xvar = xvar_360
         DEFINE SYMBOL pvar_reset = xvar_360
         LET ax_lo = 0
         LET ax_hi = 360
      ENDIF

   ENDIF  !`(ax_hi-ax_lo) LT 359`

   IF `amax GT 180` THEN 
         LET/UNITS="($lon_units)" xvar = xvar_360
         DEFINE SYMBOL pvar_reset = xvar_360
   ENDIF

   IF `(ax_lo GT amin) OR (ax_hi LT amax)` THEN

      IF `ax_lo-amin LT 1` THEN LET ax_lo = `ax_lo - 1 `
      IF `amax-ax_hi LT 1` THEN LET ax_hi = `ax_hi + 1`
      
         IF `(ax_lo GT amin) OR (ax_hi LT amax)` THEN
            LET/UNITS="($lon_units)" xvar = xvar_360
            DEFINE SYMBOL pvar_reset = xvar_360
	 ENDIF
   ENDIF

! If there was a longitude mask apply that to the plot region
! change the test - valid value may be zero

   IF `($constr_lon_min"-1.e34|*>1") NE -1.e34` THEN
      LET amin = ($constr_lon_min)
      LET amax = ($constr_lon_max)
      GO LAS_is_range amin amax H 
      LET ax_lo = `amin`
      let ax_hi = `amax`
   ENDIF

   DEFINE AXIS/x=`ax_lo`:`ax_hi`:`del_ax`/units="($lon_units%degrees_east%)" haxis
   IF ($dm_labels"0|*>1") THEN DEFINE SYMBOL special_label_x = ppl xfor (($dm_labels))
   DEFINE SYMBOL have_haxis = 1

   DEFINE SYMBOL region_x_range = `ax_hi - ax_lo` ! reset this so LAS_fland.jnl chooses the right resolution

   LET dep_var = x[gx=haxis]
   CANCEL SYMBOL data_1_units
   CANCEL SYMBOL ferret_title_0  ! dont need a horizontal axis label
ENDIF
!---------- Redefining longitude

LET amin = pminmax[i=1]
LET amax = pminmax[i=2]

IF `itsa_tim_prop` THEN DEFINE SYMBOL is_time = 1
GO LAS_is_range amin amax
CANCEL SYMBOL is_time

IF `($have_haxis"0|*>1") EQ 0` THEN \
DEFINE AXIS/x=`ax_lo`:`ax_hi`:`del_ax`/units="`p,RETURN=units`" haxis 
DEFINE AXIS/x=`ax_lo`:`ax_hi`:`del_ax`/units="`p,RETURN=units`" haxis1 

LET dep_var = x[gx=haxis]
LET dep_var1 = x[gx=haxis1]; sh var dep_var1


LET qminmax = MINMAX(q_var)
LET amin = qminmax[i=1]
LET amax = qminmax[i=2]
GO LAS_is_range amin amax
CANCEL SYMBOL is_time

DEFINE AXIS/y=`ax_lo`:`ax_hi`:`del_ax`/units="`q,RETURN=units`" vaxis 
DEFINE AXIS/y=`ax_lo`:`ax_hi`:`del_ax`/units="`q,RETURN=units`" vaxis1

LET indep_var = y[gy=vaxis]
LET indep_var1 = y[gy=vaxis1]

! Formatted horizontal axes
! See if the independent axis is longitude, latitude, or time
! If so redefine those axes

IF `itsa_lat_prop` THEN 

! Get min and max latitudes and create a LAT axis.

   LET amin = pminmax[i=1]
   LET amax = pminmax[i=2]
   LET pdel = 0.1*(amax-amin)

   LET amin = MAX(`amin-pdel`, -90)
   LET amax = MIN(`amax+pdel`,  90)

   GO LAS_is_range amin amax

   DEFINE AXIS/y=`ax_lo`:`ax_hi`:`del_ax`/units=deg haxis 

   IF ($dm_labels"0|*>1") THEN DEFINE SYMBOL special_label_x = ppl xfor (($dm_labels))

   LET dep_var = y[gy=haxis]

   DEFINE SYMBOL pqual = /TRANS 
   CANCEL SYMBOL data_1_units
   CANCEL SYMBOL ferret_title_0  ! dont need a horizontal axis label

! In this case, a horizontal Latitude axis, the independent variable is q

   LET amin = qminmax[i=1]
   LET amax = qminmax[i=2]
   GO LAS_is_range amin amax

   DEFINE AXIS/x=`ax_lo`:`ax_hi`:`del_ax`/units="`p,RETURN=units`" vaxis 

   LET indep_var = x[gx=vaxis]

ENDIF

IF `itsa_tim_prop` THEN 

! Get min and max times and create a time axis.
! use the result of LAS_is_range to determine the delta,
! but use the limits from the data. 

   LET amin = pminmax[i=1]
   LET amax = pminmax[i=2]
   DEFINE SYMBOL is_time = 1

   IF ($constr_tim_min"0|*>1") THEN 
      LET amin = ($constr_tim_min)
      LET amax = ($constr_tim_max)
   ENDIF

   GO LAS_is_range amin amax
   CANCEL SYMBOL is_time
   
!     handling case where the time range is zero or very short
      IF `ax_hi - ax_lo LT 7200 AND ($tunits%|seconds>1|*>0%)` THEN
         LET ax_hi = `ax_hi + 1800,prec=15`
         LET ax_lo = `ax_lo - 1800,prec=15`
         LET del_ax = 30
      ENDIF

      DEFINE AXIS/T=`ax_lo,prec=15`:`ax_hi,prec=15`:`del_ax`/T0="`($data_t_var).time_origin`"/units=($tunits) haxis
      DEFINE AXIS/T=`ax_lo,prec=15`:`ax_hi,prec=15`:`del_ax`/T0="`($data_t_var).time_origin`"/units=($tunits) haxis2k
      DEFINE SYMBOL haxis2k_defined = 1

! (possibly) coarser axis for the all-missing plot that will make the underlay.
      let thaxis = t[gt=haxis]
      let nthaxis = `thaxis,return=lsize`
      IF `nthaxis GT 2000` THEN \
      DEFINE AXIS/T=`ax_lo,prec=15`:`ax_hi,prec=15`/npoints=2000/T0="`($data_t_var).time_origin`"/units=($tunits) haxis2k
      DEFINE SYMBOL haxis2k_defined = 1

      DEFINE SYMBOL timeaxis = haxis
      DEFINE AXIS/x=`ax_lo`:`ax_hi`:`del_ax` haxis1 
      LET tt = t[gt=haxis]

      LET nt = `tt,RETURN=size`
      LET t1 = `tboxlo[gt=haxis,L=1]`
      LET tn = tboxhi[gt=haxis,L=`nt`]
      DEFINE SYMBOL haxis_tstart = `TAX_DATESTRING (t1, tt, "minutes")`
      DEFINE SYMBOL haxis_tend = `TAX_DATESTRING (tn, tt, "minutes")`
      DEFINE SYMBOL haxis_taxname = haxis

   LET dep_var = 0*t[gt=haxis]
   IF ($haxis2k_defined"0|1") THEN 
      LET dep_var2k = 0*t[gt=haxis2k]
   ELSE
      define axis/t=1:2:1/units=seconds haxis2k
      define symbol insitu_blankvar = 1
   ENDIF

   ! A time-lon plot would normally plot with time on the vertical
   IF `itsa_lon_plot` THEN DEFINE SYMBOL pqual = /TRANS 

   DEFINE SYMBOL connect_dots = 1
   DEFINE SYMBOL do_rsquare = 0

   LET timplotvar = t[gt=haxis2k]

ENDIF

! Formatted vertical axes

! See if the independent axis is longitude, latitude, or time
! If so redefine those axes

IF `itsa_lon_prop EQ 0 AND itsa_lon_plot` THEN 

   DEFINE SYMBOL is_lon = 1

! Get min and max longitudes and create a LON axis.
! Draw a blank plot to put formatted axis on the plot.

   DEFINE SYMBOL pqual = /TRANS  ! vertical longitude axis

   LET amin = qminmax[i=1]
   LET amax = qminmax[i=2]
   GO LAS_is_range amin amax

   DEFINE AXIS/x=`ax_lo`:`ax_hi`:`del_ax`/units=deg vaxis 
   IF ($dm_labels"0|*>1") THEN DEFINE SYMBOL special_label_y = ppl yfor (($dm_labels))

   LET indep_var = x[gx=vaxis]

   CANCEL SYMBOL data_units
   CANCEL SYMBOL ferret_title_1 ! dont need a vertical axis label

   ! Then we need to redefine the other variable to have a y axis

   IF `( itsa_tim_prop + itsa_lat_prop ) EQ 0` THEN 
      LET amin = pminmax[i=1]
      LET amax = pminmax[i=2]
      GO LAS_is_range amin amax
      DEFINE AXIS/y=`ax_lo`:`ax_hi`:`del_ax`/units="`p,RETURN=units`" haxis

      LET dep_var = y[gy=haxis]
      DEFINE SYMBOL do_rsquare = 0
   ELSE

      DEFINE SYMBOL connect_dots = 1
      DEFINE SYMBOL do_rsquare = 0
   ENDIF

   DEFINE SYMBOL do_rsquare = 0

ENDIF

IF `itsa_lat_prop EQ 0 AND itsa_lat_plot` THEN 

! Get min and max latitudes and create a LAT axis.
! Draw a blank plot to put formatted axis on the plot.

   LET amin = qminmax[i=1]
   LET amax = qminmax[i=2]

   GO LAS_is_range amin amax  ! get del_ax
   DEFINE AXIS/y=`ax_lo`:`ax_hi`:`del_ax`/units=deg vaxis
   DEFINE SYMBOL region_y_range = `ax_hi - ax_lo`  ! reset this so LAS_fland.jnl chooses the right resolution

   IF ($dm_labels"0|*>1") THEN DEFINE SYMBOL special_label_y = ppl yfor (($dm_labels))

   LET indep_var = y[gy=vaxis]
   CANCEL SYMBOL data_units
   CANCEL SYMBOL ferret_title_1  ! dont need a vertical axis label
   
   DEFINE SYMBOL do_rsquare = 0
   DEFINE SYMBOL connect_dots = 1

ENDIF

! Depth axis in vertical direction

IF ($reverse_vert"0") THEN

   LET amin = qminmax[i=1]
   LET amax = qminmax[i=2]
   GO LAS_is_range amin amax  ! get del_ax
   LET ax_lo = `amin`
   LET ax_hi = `amax`

   DEFINE AXIS/z=`ax_lo`:`ax_hi`:`del_ax`/units="`($ferret_plot_var),RETURN=units`"/depth vaxis 
   DEFINE AXIS/z=`ax_lo`:`ax_hi`:`del_ax`/units="`($ferret_plot_var),RETURN=units`"/depth vaxis1
   LET indep_var = z[gz=vaxis]
   LET indep_var1 = z[gz=vaxis1]

ENDIF

DEFINE SYMBOL axes_already_defined = 1

ENDIF  ! axes_already_defined, make the underlay plot.

IF `(($ax_label_x"0|*>1") + ($ax_label_y"0|*>1") GE 1) AND (($ferret_thumb"0|0|*>1") EQ 0)` THEN 
   DEFINE VIEW/AXES/X=0.12:0.99/y=0.06:0.93 full_view
   DEFINE VIEW/AXES/X=0.12:0.99/y=0.1:0.93 full_view2
   SET VIEW full_view
ENDIF

GO LAS_open_window
IF ($ferret_thumb"0|0|*>1") THEN 
   DEFINE VIEW/AXES/X=0.12:0.99/y=0.1:0.93 full_view 
   DEFINE VIEW/AXES/X=0.12:0.99/y=0.1:0.93 full_view2 
   SET VIEW full_view
ENDIF

! Do time underlay as a line plot, so the overlays work consistently
! Label the y-axis with the variable title; enlarge if its a thumbnail plot.

IF `itsa_tim_prop` THEN

   LET vert_minmax = MINMAX(indep_var)

   LET amin = vert_minmax[i=1]
   LET amax = vert_minmax[i=2]

   LET is_time = 0
   GO LAS_is_range amin amax
   IF `($haxis2k_defined"0|1") EQ 0` THEN let timplotvar = t[gt=haxis2k] - 1000

   PLOT/NOLAB/VLIM=`ax_lo`:`ax_hi` timplotvar

   ! label location to the left of the left axis

   LET xlab_loc = -0.06
   IF `MAX(ABS(ax_lo), ABS(ax_hi)) GT 1000` THEN LET xlab_loc = -0.08

   DEFINE SYMBOL yaxis_title = ($ferret_title_1%($data_1_title)%)

   IF `($is_thumb"0") AND ($yaxis_title"0|*>1")` THEN 

      ! use the title w/o units
       DEFINE SYMBOL yaxis_title = ($data_1_title)

      ! label size: Enlarge if possible depending on its length.

      LET pagefrac = (LABWID("($yaxis_title)", 0.12*labfac) / ($ppl$ylen)) 
      IF `pagefrac GT 1` THEN LET labfac = `labfac/pagefrac`
      
   ENDIF
   ANNOTATE/NORM/XPOS=`xlab_loc`/YPOS=0.5/HALIGN=0/ANGLE=90/SIZE=`0.12*labfac` "($yaxis_title)"
    
   EXIT/SCRIPT
ENDIF


LET bvar = dep_var + indep_var
IF `dep_var2k,RETURN=defined` then LET bvar = dep_var2k + indep_var

LET allmiss = IF bvar THEN (`bvar,RETURN=bad`)
CANCEL MODE nodata_lab
IF `($special_label_x"0|*>1") OR ($special_label_y"0|*>1") OR\
  ($ax_label_x"0|*>1") OR ($ax_label_y"0|*>1")` THEN
   SHADE/NOKEY/PAL=white($pqual)/SET/title=" " allmiss
   ($ax_label_x)
   ($ax_label_y)
   ($ax_label_resize)
   ($thumb_alab_size)
   IF `($lab1"0|*>1")` THEN go unlabel 1
   PPL SHADE
   SET VIEW full_view2
ENDIF

SHADE/NOKEY/PAL=white($pqual)/NOAX/title=" "/SET allmiss
   ($thumb_alab_size)
   ($ax_label_resize)
IF ($lab1"0|*>1") THEN
   IF `(STRINDEX("($lab1)", "YEAR") GT 0)` THEN 
      IF `STRINDEX("($note_($note_num%0%)_lab%0%)", "YEAR") EQ 0` THEN
         DEFINE SYMBOL note_num = `($note_num"0") + 1`
         DEFINE SYMBOL note_($note_num)_lab = ($lab1)
         IF ($ferret_thumb"0|0|*>1") THEN label/nouser `-0.2*($ppl$xorg)`,`-0.95*($ppl$yorg)`,-1,0,($txl_lab_size),($lab1)
      ENDIF
   ENDIF
   GO unlabel 1
ENDIF
PPL SHADE

! For time series, do a plot of the original variables to set the axis symbols
IF `itsa_tim_prop` THEN
   SET VIEW ($view2)
   LET bvar = dep_var1 + indep_var1
   IF `dep_var2k,RETURN=defined` then LET bvar = dep_var2k + indep_var1
   LET allmiss = IF bvar THEN (`bvar,RETURN=bad`)
   SHADE/NOAX/NOKEY/PAL=white/title=" " allmiss
   SET MODE/LAST nodata_lab
   EXIT/SCRIPT
ENDIF

! For latitudes, do a plot of the original variables to set the axis symbols

IF `itsa_lat_prop` THEN
   IF `itsa_lon_plot` THEN 
      SET MODE/LAST nodata_lab
      EXIT/SCRIPT
   ENDIF
   SET VIEW ($view2)
   LET bvar1 = dep_var1 + indep_var1
   LET allmiss1 = IF bvar THEN (`bvar1,RETURN=bad`)
   SHADE/NOAX/NOKEY/PAL=white($pqual)/title=" " allmiss1

ENDIF

! If its a lon/lat plot, make a map with the same resolution as it would be
! done on a map, but with light gray.
! Only if longitude is on the horizontal axis

IF `itsa_lon_prop AND itsa_lat_plot` THEN

   DEFINE SYMBOL region_x_lo = ($ppl$xmin)
   DEFINE SYMBOL region_x_hi = ($ppl$xmax)
   DEFINE SYMBOL land_palette = gray_light

   GO LAS_fland ($land_palette)
   SET DATA 1
   DEFINE SYMBOL lon_units = degrees_east
ENDIF

SET MODE/LAST nodata_lab

!!  See testing comment at top
DEFINE SYMBOL xvar = constraints_mask*xvar
DEFINE SYMBOL yvar = constraints_mask*yvar
LET p = ($xvar) 
LET q = ($yvar)

! End of file ------------LAS_xytvar_setup.jnl--------------------------
