! LAS_check_multi_regions.jnl
! 
! Author: ansley
! Date: 2010/09/13
! 12/2017 Avoid redefining data_url, data_var
!
! Take care of variables that have some but no all of the same axes:
! e.g. a multi-variable plot of surface temperature with subsurface variables.

! If there is a Z or T region but a variable does not have Z or T then define
! symbol add_to_var*  which will add 0* another variable to create an axis in that direction.

! If there is a Z or T axis but no region in that direction (depends on hte order in
! which the variables were selected), then add L=1 or K=1 to the region spec, so that
! a single time or depth is arbitrarily selected.

! The LET ($data_var)_test = '($data_var)' takes care of the possiblity of variables whose
! names are pseudo-variables
! set_add_for_region sets up to add 0*var if needed, so variables have the same grids.

cancel symbol fer_last_error
LET nc = `($data_count) - 1`

! Get the shape of all variables
REPEAT/RANGE=0:`nc`/NAME=ivar (\
DEFINE SYMBOL ivar = `ivar`; \
DEFINE SYMBOL test_url \"($data_($ivar)_url)\"; \
IF ($data_($ivar)_var"0|*>1") THEN DEFINE SYMBOL test_var ($data_($ivar)_var); \
IF ($data_($ivar)_var"0|*>1") THEN DEFINE SYMBOL test_var = `UPCASE("($test_var)")`; \
SET DATA ($test_url); \
LET ($test_var)_test = '($test_var)';\
DEFINE SYMBOL shape($ivar) = `($test_var)_test,RETURN=shape`; \
DEFINE SYMBOL dset($ivar) = `($test_var)_test,RETURN=dsetnum`;\
GO set_add_for_region `ivar`;\
)

! Now set up to add to the region spec if needed.

! If no T region but variable has T, then then we will add L=1 to the region spec.
REPEAT/RANGE=0:`nc`/NAME=ivar (\
   DEFINE SYMBOL ivar = `ivar`;\
   SH sym shape($ivar);\
   List STRINDEX("($shape($ivar))", "T");\
   IF `(STRINDEX("($shape($ivar))", "T") NE 0) AND (($region_t"0|*>1") EQ 0)` THEN \
DEFINE SYMBOL add_region_t = L=1;\
   IF `(STRINDEX("($shape($ivar))", "Z") NE 0) AND (($region_t"0|*>1") EQ 0)` THEN \
DEFINE SYMBOL need_t = ($ivar);\
)
!   IF `(STRINDEX("($shape($iadd))", "Z") NE 0) AND (($region_z"0|*>1") EQ 0)` THEN EXIT/loop; \

! If variables dont share their other axes then dont do this  (X and Y compared below)
IF ($add_region_t"0|*>1") THEN
   REPEAT/RANGE=0:`nc`/NAME=ivar (\
   IF `(STRINDEX("($shape($ivar))", "Z") NE 0) AND (STRINDEX("($shape($need_t))", "Z") NE 0)` THEN \
 GO compare_axes ($ivar) ($need_t) y;\
      IF ($cancel_it"0|*>1") THEN CANCEL SYMBOL add_region_t\
   )
ENDIF


! Now check, if no Z region but variable has Z, then we will add K=1 to the region spec.
REPEAT/RANGE=0:`nc`/NAME=ivar (\
   DEFINE SYMBOL ivar = `ivar`; \
   sho sym shape($ivar);\
   List STRINDEX("($shape($ivar))", "Z");\
   IF `(STRINDEX("($shape($ivar))", "Z") NE 0) AND (($region_z"0|*>1") EQ 0)` THEN \
 DEFINE SYMBOL add_region_z = K=1;\
   IF `(STRINDEX("($shape($ivar))", "Z") NE 0) AND (($region_z"0|*>1") EQ 0)` THEN \
 DEFINE SYMBOL need_z = ($ivar);\
)
!   IF `(STRINDEX("($shape($iadd))", "Z") NE 0) AND (($region_z"0|*>1") EQ 0)` THEN EXIT/loop; \

! If they dont share their other axes then dont do this; cancel the add_region symbol.

IF ($add_region_z"0|*>1") THEN
   REPEAT/RANGE=0:`nc`/NAME=ivar (\
   DEFINE SYMBOL ivar = `ivar`; \
   IF `(STRINDEX("($shape($ivar))", "X") NE 0) AND (STRINDEX("($shape($need_z))", "X") NE 0)` THEN \
 GO compare_axes ($ivar) ($need_z) x;\
      IF ($cancel_it"0|*>1") THEN CANCEL SYMBOL add_region_z;\
)
   
   REPEAT/RANGE=0:`nc`/NAME=ivar (\
   DEFINE SYMBOL ivar = `ivar`; \
   IF `(STRINDEX("($shape($ivar))", "Y") NE 0) AND (STRINDEX("($shape($need_t))", "Y") NE 0)` THEN \
 GO compare_axes ($ivar) ($need_z) y;\
      IF ($cancel_it"0|*>1") THEN CANCEL SYMBOL add_region_z;\
)

   REPEAT/RANGE=0:`nc`/NAME=ivar (\
   DEFINE SYMBOL ivar = `ivar`; \
   IF `(STRINDEX("($shape($ivar))", "T") NE 0) AND (STRINDEX("($shape($need_t))", "T") NE 0)` THEN \
 GO compare_axes ($ivar) ($need_z) t;\
      IF ($cancel_it"0|*>1") THEN CANCEL SYMBOL add_region_z;\
)
ENDIF

!CANCEL DATA/ALL
CANCEL DATA ($test_url)

IF ($add_region_z"0|*>1") THEN GO LAS_initialize_region
IF ($add_region_t"0|*>1") THEN GO LAS_initialize_region

! End of file ------------ LAS_check_multi_regions.jnl-------------------------------
