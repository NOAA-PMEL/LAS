!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! 
! $Author: rhs $
! 11/11  ACM consolidate scripts. xy_compare_difference needs
!            to be unique; the other 2D directions can all run from
!            a single script
!
! Compare_Plot.jnl decides which script to call to make a
! difference plot.
!
!
! Set the gif filename, changing it from .gif to the name from this request
GO LAS_reset_gifname

! New palette for difference plots. Added to the Ferret 
! environment for Ferret v6.83+

DEFINE SYMBOL ferret_palette no_white_greenpink_centered 

IF `($ferret_version"0") GE 6.83` THEN
   DEFINE SYMBOL ferret_palette centered_diff 
ENDIF

IF `STRLEN("($ferret_view)") EQ 2` THEN 
   DEFINE SYMBOL s1 = `SUBSTRING ( "($ferret_view)", 1, 1)`
   DEFINE SYMBOL s2 = `SUBSTRING ( "($ferret_view)", 2, 1)`

! region 1 needs to inherit its view axes ranges from region 0

   IF ($region_0_($s1)_lo"0|*>1") THEN
         DEFINE SYMBOL region_1_($s1)_lo ($region_0_($s1)_lo)
   ENDIF
   IF ($region_0_($s1)_hi"0|*>1") THEN
         DEFINE SYMBOL region_1_($s1)_hi ($region_0_($s1)_hi)
   ENDIF
   IF ($region_0_($s2)_lo"0|*>1") THEN
         DEFINE SYMBOL region_1_($s2)_lo ($region_0_($s2)_lo)
   ENDIF
   IF ($region_0_($s2)_hi"0|*>1") THEN
         DEFINE SYMBOL region_1_($s2)_hi ($region_0_($s2)_hi)
   ENDIF

! Run the difference-plot script. XY has extra details for 
! curvilienar data and land, so it is a separate script.

   IF ($ferret_view"0|xy>1|*>0") THEN
      go xy_compare_difference

   ELSE
      go twoD_compare_difference
   ENDIF

ELIF `STRLEN("($ferret_view)") EQ 1` THEN 

   ! region 1 needs to inherit its view axes values from region 0
   IF ($region_0_($ferret_view)_lo"0|*>1") THEN
         DEFINE SYMBOL region_1_($ferret_view)_lo ($region_0_($ferret_view)_lo)
   ENDIF
   IF ($region_0_($ferret_view)_hi"0|*>1") THEN
         DEFINE SYMBOL region_1_($ferret_view)_hi ($region_0_($ferret_view)_hi)
   ENDIF

   !! Make the line plot...
   go oneD_compare_difference

ELSE
  say " **ERROR: Attempt to make a comparison plot in ($ferret_view).  Only x, y, z, t, xy, xt, xz, yt, yz, and zt are allowed"
ENDIF

! End of file ------------ ComparePlot.jnl--------------------------
