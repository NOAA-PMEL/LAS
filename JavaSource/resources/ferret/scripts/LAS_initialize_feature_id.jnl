!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! LAS_initialize_feature_id.jnl
! ACM 8/2020 version for DSG datasets
!
! Define variables and symbols based on feature (trajecotory or profile)
! identification variables
! NOTE:  For trajectory set, the calculation of individual features only works if the 
! NOTE:  intermediate netCDF files is ordered by feature_ID.  Make sure that
! NOTE:  the SQL query ends with:  'ORDER BY feature_ID'.

!
! FEATURES      - unique identifier for each feature
! FEATURE_COUNTS - number of points per trajectory  Now use symbol rowvar

IF ($nodata"0|*>1") THEN EXIT/SCRIPT
IF `($ferret_memory"25.6") LT 200` THEN SET MEM/siz=200

! Create the 'feature_evnt' variable which increments for each new trajectory.

DEFINE SYMBOL feature_max_labels = 200
LET featurevar = ($rowvar)

IF `($have_feature_nums"0|*>1") EQ 0` THEN LET feature_numbers = _m[ge=featurevar]

! rind is direction of rowsize, defined in feature_initialize
LET longest = `rowsize[e=@max]` + 2

! If dsg file then these symbols are set up.
IF `($data_($num)_var"0|($feature_variable)>1|*>0") AND ($rowvar"0|*>1")` THEN 
   DEFINE SYMBOL data_var = feature_numbers
   EXIT/SCRIPT
ENDIF

IF `($its_prop_prop_plot"0|*>1") EQ 0` THEN
   DEFINE SYMBOL data_var = feature_numbers
   EXIT/SCRIPT
ENDIF

! If  a subset of feature ids is given then only plot those on a Prop/Prop 
! plot, but using the symbols and colors as determined by the whole dataset.
! If given, traj_list is a list of numbers.

   IF ($ferret_traj_list"0|*>1") THEN
      LET subset_ids = {($ferret_traj_list)}

! Define a mask to use on the entire list of values: plot or not to plot
! the polygons on the prop-prop plot.
     
      LET subm_ids = IF ELEMENT_INDEX(traj_numbers,subset_ids) THEN 1
      IF `($subset_mask"0|*>1") EQ 0` THEN LET subset_mask = subm_ids + 0*x[x=1:`ntrajs`]  ! put it on a non-abstract axis. 
      SET DATA/FMASK=subset_mask ($num)
   ELSE

! There may be a subset already defined from ferret_feature_list

      IF `($subset_mask"0|*>1") EQ 0` THEN LET subset_mask = 1 + 0*i[i=1:`nfeatures`]
      
      LET n_subset = `subset_mask[I=@NGD]`
      DEFINE SYMBOL features_shown = `n_subset`
      DEFINE SYMBOL prof_traj_shown = `n_subset`
   ENDIF

! Make time symbols w/o the trailing 00:00:00

IF ($region_0_t_lo"0|*>1") THEN
   DEFINE SYMBOL t_lab_lo = ($region_0_t_lo)
   LET iz = STRINDEX("($region_0_t_lo)", " 00:00:00")
   IF `iz GT 0` THEN 
      DEFINE SYMBOL t_lab_lo = `SUBSTRING("($region_0_t_lo)", 1, iz-1)`
   ELSE
      LET iz = STRINDEX("($region_0_t_lo)", ":00:00")
      IF `iz GT 0` THEN DEFINE SYMBOL t_lab_lo = `SUBSTRING("($region_0_t_lo)", 1, iz-1)`
   ENDIF
ENDIF 

IF `STRCMP("($region_0_t_lo)", "($region_0_t_hi)") EQ 0` THEN  EXIT/SCRIPT

IF ($region_0_t_hi"0|*>1") THEN
   DEFINE SYMBOL t_lab_hi = ($region_0_t_hi)
   LET iz = STRINDEX("($region_0_t_hi)", " 00:00:00")
   IF `iz GT 0` THEN 
      DEFINE SYMBOL t_lab_hi = `SUBSTRING("($region_0_t_hi)", 1, iz-1)`
   ELSE
      LET iz = STRINDEX("($region_0_t_hi)", ":00:00")
      IF `iz GT 0` THEN DEFINE SYMBOL t_lab_hi = `SUBSTRING("($region_0_t_hi)", 1, iz-1)`
   ENDIF
ENDIF

!---------------- end of LAS_initialize_feature_id.jnl -------------------------
