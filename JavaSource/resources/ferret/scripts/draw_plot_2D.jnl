!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! draw_plot_2D.jnl 
! 
! Author: ansley 
! Date: 2006/08/11 16:45:54 
! 3/7/2007: ACM. Put code to check that the region was not too small, resulting 
!           in degenerate plot into LAS_initialize_data.jnl rather than in the 
!           individual plot scripts.
! 3/29/2007 ACM Fix logic interpreting contents of ferret_interpolate_data 
! 7/15/2007 ACM Add lines to remove labels when a degenerate 2D plot is made 
!               This happens when the region chosen is smaller than a grid cell.
! 3/2008 ACM Use SHADE/TRIM for shade underlays of FILL plots (Ferret v6.1)
!11/2011 ACM Consolidate all except XY plots into a single 2D plot script.

! draw_plot_2D.jnl creates a 2D plot for use with the Back End Server
! code that sits behind a Live Access Server (LAS). 

! Set any Ferret modes
!

IF ($ferret_interpolate_data%0|false>0|true>1|1|0|*>1%) THEN SET MODE INTERPOLATE

DEFINE SYMBOL fview = `UPCASE("($ferret_view)")`

! Define symbols associated with the regign and data and perform any
! initialization needed for this dataset.

GO LAS_initialize_region 0
GO LAS_initialize_data 0

! Check for errors (They often occur during dataset initialization.)

IF ($error_status"0|*>1") THEN
  MESSAGE/ERROR **ERROR ($error_string)
  EXIT/PROGRAM
ENDIF

! Check whether the region is too small to make a 2D plot on this grid.
GO LAS_check_2d_region

! Check for errors 
IF ($error_status"0|ERROR>1") THEN
  MESSAGE/ERROR **ERROR ($error_string)
  EXIT/PROGRAM
ENDIF

! Here is variable 0 and its title

DEFINE SYMBOL ferret_var_0 = ($ferret_plot_var)
DEFINE SYMBOL ferret_title_0 = ($ferret_plot_title"($data_var)")

! Use Ferret properties to set up the plot qualifiers

IF ($ferret_view"0|te>1|*>0") THEN DEFINE SYMBOL ferret_contour_style = raster
GO LAS_set_plot_qualifiers

! Open the window, apply size, scale and WMS view options
GO LAS_open_window

! Set the URL label for the first dataset.
GO LAS_url_label 0

! Make a plot,needed to get the number of levels (SHADE/SET doesnt do it.)
IF `($plot_type"0|CONTOUR>0|*>1") AND ($degenerate_2d"0|*>1") EQ 0` THEN
IF ($ferret_annotations"0|*>1") THEN
   SHADE($qualifiers)($shade_qualifiers)/TITLE="($ferret_plot_title)" ($ferret_var_0)
   CANCEL VIEW
   SET VIEW full_view
ENDIF
ENDIF


! For an ensemble-z plot, make the Z axis in the vertical direction
IF ($ferret_view"0|ze>1|*>0") THEN DEFINE SYMBOL qualifiers = ($qualifiers)/TRANS


! Draw the plot and we are done

IF `($data_count"0") EQ 1` THEN
   DEFINE SYMBOL split_title = ($ferret_plot_title)
   GO LAS_split_title
   DEFINE SYMBOL ferret_plot_title = ($split_title)

   IF ($shade_underlay) THEN 
      SHADE($qualifiers)($shade_qualifiers)/TITLE="($ferret_plot_title)"/SET ($ferret_var_0)
      GO reset_labels 
      IF ($ferret_view"0|te>1|*>0") THEN 
         ppl yaxis,`($ppl$ymin)-0.5`,`($ppl$ymax)+0.5`,1
	 ppl axlint,,1
      ENDIF

! For an ensemble-z plot, will make custom axis labels.
      IF ($ferret_view"0|ze>1|*>0") THEN 
         PPL axlabp,0,
         PPL xaxis,`($xaxis_min)`,`($xaxis_max)`,1
      ENDIF

      PPL SHADE
      IF `($LEV_NUM"0") GT 245/2` THEN PPL SHASET RESET
      ($plot_type)($qualifiers)/OVER/NOLAB ($ferret_var_0)
   ELSE
      ($plot_type)($qualifiers)/TITLE="($ferret_plot_title)"/SET ($ferret_var_0)
         GO reset_labels
         IF ($ferret_view"0|te>1|*>0") THEN 
            ppl yaxis,`($ppl$ymin)-0.5`,`($ppl$ymax)+0.5`,1
	    ppl axlint,,1
	    ppl xaxis,`($ppl$ymin)-0.5`,`($ppl$ymax)+0.5`,1
         ENDIF
! For an ensemble-z plot, will make custom axis labels.
         IF ($ferret_view"0|ze>1|*>0") THEN 
            PPL axlabp,0,
	    ppl xaxis,`($xaxis_min)`,`($xaxis_max)`,1
         ENDIF

      PPL ($plot_type)
   ENDIF

   IF ($ferret_contour_style"0|default>0|color_filled_plus_lines>1|raster_plus_lines>1|*>0") THEN \
     CONTOUR/OVER/NOLAB($levsym) ($ferret_var_0)

! Put on the custom e-axis labels
   IF ($ferret_view"0|ze>1|*>0") THEN
      
      LET dely = `0.04*ABS(($yaxis_max)-($yaxis_min))`
      LET yloc = `($yaxis_max)+dely`
      LET labsiz = 0.1
      LET just = 0
      
      LET varnames = ..varnames
      IF `IS_ELEMENT_OF_STR_N(varnames, "labels") GT 0` THEN 
         REPEAT/E=($region_e_lo):($region_e_hi) (\
	   LABEL/USER `_e`, `yloc`,`just`,0,`labsiz`,`LABELS`;\
	   IF `($shade_underlay) AND (_e eq ($region_e_hi)-1)` THEN LET just = 1)
      ELSE
         REPEAT/E=($region_e_lo):($region_e_hi) (\
	   LABEL/USER `_e`, `yloc`,0,0,($labsiz), "`_E`")
      ENDIF

   ENDIF
   
! One of the results is the data min and max. Save those here.
   DEFINE SYMBOL data_plotted_min = ($ppl$zmin)
   DEFINE SYMBOL data_plotted_max = ($ppl$zmax)

ENDIF

! Overall header at the very top
GO LAS_ferret_las_version_header

! Add labels at the top for the location of overlaid lines
! and for URLs if they exist  
   
GO labels_above_plot

! Mark grid points. Options are no, subsample (tests if subsampling needed)
! or all to force marking all points. 

IF ($ferret_mark_grid"0|no>0|all>1|subsample>1|*>0") THEN GO mark_grid

! Save the results
GO LAS_results box


! End of file ------------ draw_plot_2D.jnl--------------------------

