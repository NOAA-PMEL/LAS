!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! xz_vector_plot.jnl
! 
! Author: ansley 
! Date: 2010/06/10
!
! xt_vector_plot.jnl creates an XT vector plot for use with the Back End Server
! code that sits behind a Live Access Server (LAS). 

! Set any Ferret modes

IF ($ferret_interpolate_data%0|false>0|true>1|1|0|*>1%) THEN SET MODE INTERPOLATE

DEFINE SYMBOL itsa_vector_plot = 1

! Define symbols associated with the region and data and perform any
! initialization needed for this dataset.

DEFINE SYMBOL var_num = 0
GO LAS_initialize_region ($var_num)
GO LAS_initialize_data ($var_num)
DEFINE SYMBOL ferret_vector_1 ($ferret_plot_var)
DEFINE SYMBOL ferret_plot_title_1 "($data_title) ($data_units)"
LET ferret_vector_1_noz = ($FERRET_PLOT_BASE_VAR_NOREGION)
DEFINE SYMBOL ferret_vector_1_noz ferret_vector_1_noz[($region_xyt)]
IF ($is_sampled"0") THEN DEFINE SYMBOL ferret_vector_1_noz ($ferret_plot_base_var_noregion($var_num))

DEFINE SYMBOL var_num = 1
GO LAS_initialize_data ($var_num)
DEFINE SYMBOL ferret_vector_2 ($ferret_plot_var)
DEFINE SYMBOL ferret_plot_title "($ferret_plot_title_1), ($data_title) ($data_units)"
DEFINE SYMBOL ferret_vector_2_noregion ($FERRET_PLOT_BASE_VAR_NOREGION)
LET ferret_vector_2_noz = ($FERRET_PLOT_BASE_VAR_NOREGION)
DEFINE SYMBOL ferret_vector_2_noz ferret_vector_2_noz[($region_xyt)]
IF ($is_sampled"0") THEN DEFINE SYMBOL ferret_vector_2_noz ($ferret_plot_base_var_noregion($var_num))

! Check for errors (They often occur during dataset initialization.)

IF ($error_status"0|*>1") THEN
  MESSAGE/ERROR **ERROR ($error_string)
  EXIT/PROGRAM
ENDIF

! Check whether the region is too small; reduces to a single grid point.
! (Region is a point)
GO LAS_check_2d_region

! Check for errors 
IF ($error_status"0|ERROR>1") THEN
  MESSAGE/ERROR **ERROR ($error_string)
  EXIT/PROGRAM
ENDIF

! Use Ferret properties to set up the plot qualifiers
! By default do not draw graticules, but user may turn them on.

IF ($ferret_use_graticules"1|default>1|*>0) THEN DEFINE SYMBOL ferret_use_graticules = none

GO LAS_set_plot_qualifiers
DEFINE SYMBOL qualifiers = ($qualifiers)/NOLAB

! Open the window, apply size, scale options
GO LAS_open_window

! Set the URL label for the first dataset.
GO LAS_url_label 0

! If the title is long, split it before the second-component label
DEFINE SYMBOL split_title = ($ferret_plot_title)
DEFINE SYMBOL split_string = ($data_title)
GO LAS_split_vector_title.jnl 
DEFINE SYMBOL ferret_plot_title ($split_title)

! Do the plot
GO LAS_vector_stickplot_z

! Save the results
GO LAS_results box

!  ----------------- End of yz_vector_plot.jnl ------------------------------------

