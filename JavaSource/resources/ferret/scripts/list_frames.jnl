!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! $RCSfile: list_frames.jnl,v $
!
! $Author: Jing Y. Li $
! $Date: 2007/08/22
! 2007/11/27 ACM If the variable is a native curvilinear data variable, operate 
!                on the data field not the var,xcoord,ycoord which has been 
!                defined as the *plot variable*". Specify the XY region for the
!                variable using the i,j box limits previously computed.
!                Question: should the longitude/ latitude coordinates or other 
!                informatino about the curvilinear grid be listed in the xml file?
!
! 4/28/2008 ACM refer always to ($data_var)[d=($data_num)] as there
!           may be other datasets open, e.g. for transformed variables.
! 4/28/2008 ACM Define tvar, used for getting time axis information, as
!           just t[gt=var] so that it isn't a huge variable to load in tax_datestring.
! 6/2010    ACM If its a vector plot, call LAS_auto_veclen_animate instead of 
!           LAS_auto_levels_animate.
! 4/2013    ACM Implement the Ferret property ferret_start_memory
!
! list_frames.jnl creates a listing in XML format for animation
! Assumes symbol ferret_listing is the output file.
! The listing file includes: 
! - fill_levels      for 2D plots
! - contour_levels   for 2D plots
! - dep_axis_scale   for 1D plots
! - vector_length    for vector plots
! - hasT             used to determine if the dataset has time axis
! - units            units of the time axis
! - frames           list of each individual time points

DEFINE SYMBOL its_autolev_animate = 1

GO LAS_initialize_region 0
GO LAS_initialize_data 0

IF `($FERRET_MEMORY"25.6") LT 100` THEN SET MEM/SIZ=100

! Here is the variable we will list out, with the region specified.
DEFINE SYMBOL ferret_list_var = ($ferret_plot_var)

CANCEL MODE long_lab
CANCEL MODE latit_lab

IF ($do_curvi_xy"0") THEN 
   LET the_var = ($data_var)[d=($data_num),i=($clon_imin):($clon_imax),j=($clat_jmin):($clat_jmax)]
   IF ($region_zt"0|*>1") THEN \
    LET the_var = ($data_var)[d=($data_num),i=($clon_imin):($clon_imax),j=($clat_jmin):($clat_jmax),($region_zt)]
   DEFINE SYMBOL imin ($clon_imin)
   DEFINE SYMBOL imax ($clon_imax)
   DEFINE SYMBOL isize `($clon_imax) - ($clon_imin) + 1`
   DEFINE SYMBOL jmin ($clat_jmin)
   DEFINE SYMBOL jmax ($clat_jmax)
   DEFINE SYMBOL jsize `($clat_jmax) - ($clat_jmin) + 1`
ELSE
   LET the_var = ($ferret_plot_var)
   DEFINE SYMBOL imin `the_var,return=xstart`
   DEFINE SYMBOL imax `the_var,return=iend`
   DEFINE SYMBOL isize `the_var,return=isize`
   DEFINE SYMBOL jmin `the_var,return=jstart`
   DEFINE SYMBOL jmax `the_var,return=jend`
   DEFINE SYMBOL jsize `the_var,return=jsize`
ENDIF

DEFINE SYMBOL kmin `the_var,return=kstart`
DEFINE SYMBOL kmax `the_var,return=kend`
DEFINE SYMBOL ksize `the_var,return=ksize`
DEFINE SYMBOL lmin `the_var,return=lstart`
DEFINE SYMBOL lmax `the_var,return=lend`
DEFINE SYMBOL lsize `the_var,return=lsize`

! For vector plots, compute consistent vector length for the 
! animation.

! For color or contour plots, LAS_auto_levels_animate checks whether 
! symbols for color levels have been set, and if not, sets them for 
! the variable at all time steps.

IF `($data_count"1") EQ 2` THEN
   GO LAS_auto_veclen_animate
ELSE
   GO LAS_auto_levels_animate 
ENDIF

!for animation of 1D plots

DEFINE SYMBOL ndim = STRLEN("($ferret_view)")

!x, xt, y, yt, z, or zt view
IF `(($ndim) EQ 1) OR ( (($ndim) EQ 2) AND (STRINDEX("($ferret_view)", "t") GT 0) )` THEN
! If the user gave dependent axis scaling, 
! remove any surrounding parentheses

 IF ($ferret_dep_axis_scale"0|*>1") THEN

   IF `STRINDEX("($ferret_dep_axis_scale)", "(") GT 0` THEN
      LET paren = `STRINDEX("($ferret_dep_axis_scale)", "(")`
      LET slen = `STRLEN("($ferret_dep_axis_scale)")`
      DEFINE SYMBOL ferret_dep_axis_scale = `SUBSTRING("($ferret_dep_axis_scale)", paren+1, slen-1)`
     
      LET paren = `STRINDEX("($ferret_dep_axis_scale)", ")")`
      LET slen = `STRLEN("($ferret_dep_axis_scale)")`
      DEFINE SYMBOL ferret_dep_axis_scale = `SUBSTRING("($ferret_dep_axis_scale)", 1, slen-1)`
   ENDIF
 ENDIF

! If dependent axis scale was not set by the user, determine a
! consistent scale to use

 IF `($ferret_dep_axis_scale"1|*>0")` THEN
    LOAD the_var
    LET vmin = the_var[x=@min,y=@min,z=@min,t=@min]
    LET vmax = the_var[x=@max,y=@max,z=@max,t=@max]
    PPL %RANGE `vmin,p=9` `vmax,p=9` 10
    DEFINE SYMBOL lo_lim ($PPL$RANGE_LOW)
    DEFINE SYMBOL hi_lim ($PPL$RANGE_HIGH)
    DEFINE SYMBOL delta ($PPL$RANGE_INC)
    DEFINE SYMBOL ferret_dep_axis_scale ($PPL$RANGE_LOW),($PPL$RANGE_HIGH),($PPL$RANGE_INC)
 ENDIF
ENDIF  ! ndim = 1

SET LIST/FILE="($result_ferret_listing_filename)"


IF ($do_curvi_xy"0") THEN 
   LET the_var = ($data_var)[d=($data_num),i=($clon_imin):($clon_imax),j=($clat_jmin):($clat_jmax)]
   IF ($region_zt"0|*>1") THEN \
    LET the_var = ($data_var)[d=($data_num),i=($clon_imin):($clon_imax),j=($clat_jmin):($clat_jmax),($region_zt)]
ENDIF 

DEFINE SYMBOL hasT = 1

!-----------------------------------------
! compute time range for animation on XY view 
!-----------------------------------------
!- only a single time ponit was selected
!- or, there is not time axis at all

! max_tpoints is the arbitrary size limit of the time range
! 21 might be a good value for an animation
! 251 might be a good value for a time series plot
!DEFINE SYMBOL max_tpoints 7   ! should be an odd number

! Determine a new time range if just a single point requested
IF ($do_curvi_xy"0") THEN 
   DEFINE SYMBOL nt = `the_var,return=lsize`   ! no. of time points requested
ELSE
   DEFINE SYMBOL nt = `the_var,return=lsize`   ! no. of time points requested
ENDIF

IF `($nt) eq 1` THEN

    LET L0 = `the_var,return=lstart`
  
    IF `L0 EQ 0` THEN
        DEFINE SYMBOL hasT = 0
    ENDIF

    !  CANCEL REGION/T
    !LET maxL = `($data_var),return=lsize`   ! no. of time points total
    !LET hafmax = `INT(($max_tpoints)/2)`
    !IF `maxL LE ($max_tpoints)` THEN
    !    DEFINE SYMBOL lmin 1
    !    DEFINE SYMBOL lmax `maxL`
    ! ELSE
    !    DEFINE SYMBOL lmin `MAX(L0-hafmax,1)`
    !    DEFINE SYMBOL lmax `MIN(L0+hafmax,maxL)`
    ! ENDIF

    DEFINE SYMBOL lmin `L0`
    DEFINE SYMBOL lmax `L0`

     IF ($region_t"0|*>1") THEN
        !SET REGION/T="`($data_var)[d=($data_num),L=($lmin)],return=TSTART`":"`($data_var)[d=($data_num),L=($lmax)],return=TEND`"
        DEFINE SYMBOL region_t = T="`($data_var)[d=($data_num),L=($lmin)],return=TSTART`":"`($data_var)[d=($data_num),L=($lmax)],return=TEND`"
        DEFINE SYMBOL region_xyzt = ($region_xyz),($region_t)
        DEFINE SYMBOL region_full = ($region_xyzt)
     ELSE
        MESSAGE/ERROR **ERROR  Cannot animate this variable. It has no time variation.
        EXIT/PROGRAM
     ENDIF

     IF ($do_curvi_xy"0") THEN 
        DEFINE SYMBOL ferret_plot_var = ($data_var)[d=($data_num),d=($data_num),($region_full)], xcoord, ycoord
     ELSE
        DEFINE SYMBOL ferret_plot_var = ($data_var)[d=($data_num),d=($data_num),($region_full)]
     ENDIF


ENDIF

IF ($do_curvi_xy"0") THEN 
   DEFINE SYMBOL ferret_list_var = the_var
ELSE
   DEFINE SYMBOL ferret_list_var = ($ferret_plot_var)
ENDIF

!----------------------------------------
! start writing to the file
!----------------------------------------
LET xmlhead = "<?xml version='1.0' encoding='UTF-8'?>"
define variable len = STRLEN(xmlhead)
define symbol fmt = A`len`
list/nohead/clobber/format=(($fmt))/FILE xmlhead

LET framesStart = "<lasAnimation>"
define variable len = STRLEN(framesStart)
define symbol fmt = A`len`
list/nohead/format=(($fmt))/FILE/append framesStart

!------------
! fill_levels  ! see the note under ferret_contour_levels
!------------
IF `($ferret_fill_levels"0|*>1") EQ 0` THEN DEFINE SYMBOL ferret_fill_levels 10

LET f = "<fill_levels>"
list/norowhead/nohead/format=(A13)/FILE/append f

let nlen = strlen ("($ferret_fill_levels)")
let a = "($ferret_fill_levels)"
list/norowhead/nohead/format=(A`nlen`)/FILE/append a

LET f = "</fill_levels>"
list/norowhead/nohead/format=(A14)/FILE/append f

!---------------
! contour_levels
! If there is no ferret_contour_levels then send a value
! anyway - the xml file apparently needs it to be set. It
! will not be used in the plot unless the plot style draws lines. 
! Just skipping this block and writing no contour_levels fails.
!---------------
IF `($ferret_contour_levels"0|*>1") EQ 0` THEN DEFINE SYMBOL ferret_contour_levels 10
LET f = "<contour_levels>"
list/norowhead/nohead/format=(A16)/FILE/append f

let nlen = strlen ("($ferret_contour_levels)")
let a = "($ferret_contour_levels)"
list/norowhead/nohead/format=(A`nlen`)/FILE/append a

LET f = "</contour_levels>"
list/norowhead/nohead/format=(A17)/FILE/append f

!---------------
! dep_axis_scale
!---------------
LET f = "<dep_axis_scale>"
list/norowhead/nohead/format=(A16)/FILE/append f

let nlen = strlen ("($ferret_dep_axis_scale)")
let a = "($ferret_dep_axis_scale)"
list/norowhead/nohead/format=(A`nlen`)/FILE/append a

LET f = "</dep_axis_scale>"
list/norowhead/nohead/format=(A17)/FILE/append f


!------------
! vector_length  ! see the note under ferret_contour_levels
!------------
IF `($ferret_vector_length"0|*>1") EQ 0` THEN DEFINE SYMBOL ferret_vector_length 10

LET f = "<vector_length>"
list/norowhead/nohead/format=(A15)/FILE/append f

let nlen = strlen ("($ferret_vector_length)")
let a = "($ferret_vector_length)"
list/norowhead/nohead/format=(A`nlen`)/FILE/append a

LET f = "</vector_length>"
list/norowhead/nohead/format=(A16)/FILE/append f


!------------
! hasT
!------------
LET f = "<hasT>"
list/norowhead/nohead/format=(A6)/FILE/append f

let nlen = strlen ("($hasT)")
let a = "($hasT)"
list/norowhead/nohead/format=(A`nlen`)/FILE/append a

LET f = "</hasT>"
list/norowhead/nohead/format=(A7)/FILE/append f

! ouput individual time points
LET f = "<frames>"
list/norowhead/nohead/format=(A8)/FILE/append f

!====>output time frames
let lvar = ($ferret_list_var)  ! if ferret_list_var contains an expression, cant just do t[gt=(symbol)]
let tvar = t[gt=lvar]
define symbol first_time = `tax_datestring(t[gt=tvar,L=1], t[gt=tvar], "seconds")`
let nlen = strlen ("($first_time)")

IF ($ferret_time_step"1|*>0") THEN
    list/norowhead/nohead/format=("<frame>",A`nlen`,"</frame>")/FILE/append tax_datestring(t[gt=tvar,L=($lmin):($lmax)], t[gt=tvar], "seconds")
ELSE
    list/norowhead/nohead/format=("<frame>",A`nlen`,"</frame>")/FILE/append tax_datestring(t[gt=tvar,L=($lmin):($lmax):($ferret_time_step)], t[gt=tvar], "seconds")
ENDIF

LET f = "</frames>"
list/norowhead/nohead/format=(A9)/FILE/append f

DEFINE SYMBOL time_units = `tax_units(tvar)`

!---------------------
! time unit in months
!--------------------
!IF `strcmp("($time_units)", "2629746") EQ 0` THEN
!    LET f = "<units>months</units>"
!    list/norowhead/nohead/format=(A21)/FILE/append f
!------------------
!time unit in years
!------------------
!ELIF `strcmp("($time_units)", "31536000") EQ 0` THEN
!    LET f = "<units>years</units>"
!    list/norowhead/nohead/format=(A20)/FILE/append f
!--------------------
!time unit in seconds
!--------------------
!ELSE
    LET f = "<units>seconds</units>"
    list/norowhead/nohead/format=(A22)/FILE/append f
!ENDIF

LET framesEnd = "</lasAnimation>"
list/nohead/format=(A15)/FILE/append framesEnd

