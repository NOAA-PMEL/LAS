!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! $RCSfile: LAS_initialize_region.jnl,v $
! $Revision: 1.1 $
! 
! $Author: ansley $
! $Date: 2006/07/06 17:54:11 $
!
! The LAS_initialize_region.jnl script defines the following symbols
! based on the region associated with data_#:
!
! region_~_lo/hi   -- lo and hi points on a given axis
! region_~_range   -- numeric range on a given axis
! region_~         -- domain in 'lo:hi' notation
! region_full      -- synonym for 'region_xyzt'
!
! Incoming argument determines which region to use
! The default number is 0, and the default region is region_0.
! If we are making only a map, then there are no data_ symbols
! defined, so data_0_region is not set, but we can define region 
! as region_0.
!
! ACM trac 267: Do not cancel any previously set limits or region symbols; 
! The previous v6 UI as to how it worked in the old product server expects
! to inherit some or all region info from the first dataset for use by the 
! second in comparisons etc. Keep this behavior.
!
! 10/2017 Expand the map if the region is super skinny, only for insitu data

DEFINE SYMBOL num ($1"0")
DEFINE SYMBOL region_name data_($num)_region
DEFINE SYMBOL region ($($region_name)"region_0")

! Dont use the data_n_region symbol coming in. The info for the region of each
! variable matches the variable number so that region_0_t_lo is the region for
! data_0_var, etc.

DEFINE SYMBOL region region_($num)


! When it's a map plot, check the aspect ratio.  If it's too skinny a plot
! in any direction.  This is pulled out of LAS_open_window, because
! it may reset the incoming region definition.

IF `($num) EQ 0 AND ($ferret_view"|xy>1|xyt>1|*>0") ` THEN

DEFINE SYMBOL region_x_range = `ABS( ($region_0_x_hi) - ($region_0_x_lo) )`
DEFINE SYMBOL region_y_range = `($region_0_y_hi) - ($region_0_y_lo)`

DEFINE SYMBOL win_aspect = 0
IF `($its_prop_prop_plot"0|*>1") EQ 0` THEN
  IF `($fview"1|t>0|*>1") AND ($region_x_range"0") NE 0` THEN \
    DEFINE SYMBOL win_aspect =  `($region_Y_range)/($region_X_range)`
ELSE
    DEFINE SYMBOL win_aspect = 0.8  ! prop-prop plots
ENDIF

! Set more reasonable aspect ratio if the region is extremely skinny.
! Else axis labels etc get squashed

IF `($win_aspect"0|*>1") EQ 0` THEN DEFINE SYMBOL win_aspect = 1

! For insitu sample data, show the data on a larger map if the region
! is skinny.  For gridded datasets, just show the requested region.

SET DATA "($data_0_url)"
LET global_attnames = ..attnames

IF `IS_ELEMENT_OF_STR_N(global_attnames, "featureType") ` THEN

IF `($win_aspect) LT 0.3` THEN 
   DEFINE SYMBOL win_aspect = 0.3
   LET need_y = ($region_X_range)* 0.3
   DEFINE SYMBOL region_0_y_lo = `($region_0_y_lo) - need_y/2`
   DEFINE SYMBOL region_0_y_hi = `($region_0_y_hi) + need_y/2`

   IF `($region_0_y_lo) LT -90` THEN
      DEFINE SYMBOL region_0_y_lo = -90
      DEFINE SYMBOL region_0_y_hi = `-90 + ABS(need_y)`
   ENDIF
   IF `($region_0_y_hi) GT 90` THEN
      DEFINE SYMBOL region_0_y_lo = `90 - ABS(need_y)`
      DEFINE SYMBOL region_0_y_hi = 90 
   ENDIF
ENDIF

IF `($win_aspect) GT 2.5` THEN 
   DEFINE SYMBOL win_aspect = 2.5
   LET need_x = ($region_Y_range)/2.5
   DEFINE SYMBOL region_0_x_lo = `($region_0_x_lo) - need_x/2`
   DEFINE SYMBOL region_0_x_hi = `($region_0_x_hi) + need_x/2`
ENDIF

ELSE 
   IF `($win_aspect) LT 0.3` THEN DEFINE SYMBOL win_aspect = 0.3
   IF `($win_aspect) GT 2.5` THEN DEFINE SYMBOL win_aspect = 2.5
ENDIF

!CANCEL DATA "($data_0_url)"
ENDIF  ! end of adjusting xy aspect ratio

! NOTE:  Occasionally the LAS applet will send in a longitude or latitude
! NOTE:  that looks like "24.400000000000002".  Such long, numerically
! NOTE:  irrelevant numbers can overflow Ferret's maximum symbol length.
! NOTE:  Rounding longitudes and latitudes to the nearest .00001 seems
! NOTE:  reasonable for all but the most detailed datasets.

! NOTE:  'region_x_m360' and 'region_x_p360' are used for those visualizations
! NOTE:  where Ferret doesn't auomatically deal with the modulo nature of
! NOTE:  longitude and allow the script writer to deal with this manually.

IF ($($region)_x_lo"0|*>1") THEN
  DEFINE SYMBOL region_x_lo = `INT(100000 * ($($region)_x_lo)) / 100000`
  DEFINE SYMBOL region_x_hi = `INT(100000 * ($($region)_x_hi)) / 100000`
  DEFINE SYMBOL limits_x = ($region_x_lo):($region_x_hi)
  DEFINE SYMBOL limits_x_m360 = `($region_x_lo)-360`:`($region_x_hi)-360`
  DEFINE SYMBOL limits_x_p360 = `($region_x_lo)+360`:`($region_x_hi)+360`
  DEFINE SYMBOL region_x = X=($region_x_lo):($region_x_hi)
  DEFINE SYMBOL region_x_m360 X=`($region_x_lo)-360`:`($region_x_hi)-360`
  DEFINE SYMBOL region_x_p360 X=`($region_x_lo)+360`:`($region_x_hi)+360`
  IF `($region_x_hi) GE ($region_x_lo)` THEN
    DEFINE SYMBOL region_x_range = `($region_x_hi) - ($region_x_lo)`
  ELSE
    DEFINE SYMBOL region_x_range = `($region_x_lo) - ($region_x_hi)`
  ENDIF
  DEFINE SYMBOL region_small = X=($region_x_lo)
  DEFINE SYMBOL region_comma = ,
ENDIF

IF ($($region)_y_lo"0|*>1") THEN
  DEFINE SYMBOL region_y_lo = `INT(1000 * ($($region)_y_lo)) / 1000`
  DEFINE SYMBOL region_y_hi = `INT(1000 * ($($region)_y_hi)) / 1000`
  DEFINE SYMBOL limits_y = ($region_y_lo):($region_y_hi)
  DEFINE SYMBOL region_y = Y=($region_y_lo):($region_y_hi)
  IF `($region_y_hi) GE ($region_y_lo)` THEN
    DEFINE SYMBOL region_y_range = `($region_y_hi) - ($region_y_lo)`
  ELSE
    DEFINE SYMBOL region_y_range = `($region_y_lo) - ($region_y_hi)`
  ENDIF
  DEFINE SYMBOL region_small = ($region_small)($region_comma)Y=($region_y_lo)
  DEFINE SYMBOL region_comma = ,
ENDIF



IF ($($region)_z_lo"0|*>1") THEN
  DEFINE SYMBOL region_z_lo = ($($region)_z_lo)
  DEFINE SYMBOL region_z_hi = ($($region)_z_hi)
  DEFINE SYMBOL limits_z = ($region_z_lo):($region_z_hi)
  DEFINE SYMBOL region_z = Z=($region_z_lo):($region_z_hi)
  IF `($region_z_hi) GE ($region_z_lo)` THEN
    DEFINE SYMBOL region_z_range = `($region_z_hi) - ($region_z_lo)`
  ELSE
    DEFINE SYMBOL region_z_range = `($region_z_lo) - ($region_z_hi)`
  ENDIF
  DEFINE SYMBOL region_small = ($region_small)($region_comma)Z=($region_z_lo)
  DEFINE SYMBOL region_comma = ,
ENDIF
IF ($add_region_z"0|*>1") THEN
   IF ($($region)_z_lo"0|*>1") THEN DEFINE SYMBOL add_region_z = z=($($region)_z_lo)
   DEFINE SYMBOL region_z = ($add_region_z)
   DEFINE SYMBOL region_z_range = 0
ENDIF

! NOTE:  Normally we use 'DEFINE SYMBOL ~ = ...'
! NOTE:  For the time axis we use 'DEFINE SYMBOL ~ ...' because the Ferret
! NOTE:  parser complains about unmatched quotes if we do it the other way.

IF ($($region)_t_lo"0|*>1") THEN
  DEFINE SYMBOL region_t_lo = ($($region)_t_lo)
  DEFINE SYMBOL region_t_hi = ($($region)_t_hi)
  DEFINE SYMBOL limits_t \"($region_t_lo)\":\"($region_t_hi)\"
  DEFINE SYMBOL region_t T=\"($region_t_lo)\":\"($region_t_hi)\"
  DEFINE SYMBOL region_small = ($region_small)($region_comma)T="($region_t_lo)"
  DEFINE SYMBOL region_comma = ,
ENDIF
IF ($add_region_t"0|*>1") THEN
   IF ($($region)_t_lo"0|*>1") THEN DEFINE SYMBOL add_region_t = t=($($region)_t_lo)
   DEFINE SYMBOL region_t = ($add_region_t)
ENDIF

IF ($($region)_e_lo"0|*>1") THEN
  DEFINE SYMBOL region_e_lo = ($($region)_e_lo)
  DEFINE SYMBOL region_e_hi = ($($region)_e_hi)
  DEFINE SYMBOL limits_e ($region_e_lo):($region_e_hi)
  DEFINE SYMBOL region_e E=($region_e_lo):($region_e_hi)
  DEFINE SYMBOL region_e M=($region_e_lo):($region_e_hi)
  DEFINE SYMBOL region_small = ($region_small)($region_comma)M=($region_e_lo)
ENDIF
IF ($add_region_e"0|*>1") THEN
   IF ($($region)_e_lo"0|*>1") THEN DEFINE SYMBOL add_region_e = M=($($region)_e_lo)
   DEFINE SYMBOL region_e = ($add_region_e)
ENDIF

! Now build up all possible multi-axis regions, being careful not to use
! commas when a region is missing

! region_xy
IF ($region_x"0|*>1") THEN
  IF ($region_y"0|*>1") THEN
    DEFINE SYMBOL region_xy = ($region_x),($region_y)
  ELSE
    DEFINE SYMBOL region_xy = ($region_x)
  ENDIF
ELSE
  IF ($region_y"0|*>1") THEN
    DEFINE SYMBOL region_xy = ($region_y)
  ENDIF
ENDIF

! region_xz
IF ($region_x"0|*>1") THEN
  IF ($region_z"0|*>1") THEN
    DEFINE SYMBOL region_xz = ($region_x),($region_z)
  ELSE
    DEFINE SYMBOL region_xz = ($region_x)
  ENDIF
ELSE
  IF ($region_z"0|*>1") THEN
    DEFINE SYMBOL region_xz = ($region_z)
  ENDIF
ENDIF

! region_xt
IF ($region_x"0|*>1") THEN
  IF ($region_t"0|*>1") THEN
    DEFINE SYMBOL region_xt = ($region_x),($region_t)
  ELSE
    DEFINE SYMBOL region_xt = ($region_x)
  ENDIF
ELSE
  IF ($region_t"0|*>1") THEN
    DEFINE SYMBOL region_xt = ($region_t)
  ENDIF
ENDIF

! region_yz
IF ($region_y"0|*>1") THEN
  IF ($region_z"0|*>1") THEN
    DEFINE SYMBOL region_yz = ($region_y),($region_z)
  ELSE
    DEFINE SYMBOL region_yz = ($region_y)
  ENDIF
ELSE
  IF ($region_z"0|*>1") THEN
    DEFINE SYMBOL region_yz = ($region_z)
  ENDIF
ENDIF

! region_yt
IF ($region_y"0|*>1") THEN
  IF ($region_t"0|*>1") THEN
    DEFINE SYMBOL region_yt = ($region_y),($region_t)
  ELSE
    DEFINE SYMBOL region_yt = ($region_y)
  ENDIF
ELSE
  IF ($region_t"0|*>1") THEN
    DEFINE SYMBOL region_yt = ($region_t)
  ENDIF
ENDIF

! region_zt
IF ($region_z"0|*>1") THEN
  IF ($region_t"0|*>1") THEN
    DEFINE SYMBOL region_zt = ($region_z),($region_t)
  ELSE
    DEFINE SYMBOL region_zt = ($region_z)
  ENDIF
ELSE
  IF ($region_t"0|*>1") THEN
    DEFINE SYMBOL region_zt = ($region_t)
  ENDIF
ENDIF

! region_xyz
IF ($region_xy"0|*>1") THEN
  IF ($region_z"0|*>1") THEN
    DEFINE SYMBOL region_xyz = ($region_xy),($region_z)
  ELSE
    DEFINE SYMBOL region_xyz = ($region_xy)
  ENDIF
ELSE
  IF ($region_z"0|*>1") THEN
    DEFINE SYMBOL region_xyz = ($region_z)
  ENDIF
ENDIF

! region_xyt
IF ($region_xy"0|*>1") THEN
  IF ($region_t"0|*>1") THEN
    DEFINE SYMBOL region_xyt = ($region_xy),($region_t)
  ELSE
    DEFINE SYMBOL region_xyt = ($region_xy)
  ENDIF
ELSE
  IF ($region_t"0|*>1") THEN
    DEFINE SYMBOL region_xyt = ($region_t)
  ENDIF
ENDIF

! region_xzt
IF ($region_xz"0|*>1") THEN
  IF ($region_t"0|*>1") THEN
    DEFINE SYMBOL region_xzt = ($region_xz),($region_t)
  ELSE
    DEFINE SYMBOL region_xzt = ($region_xz)
  ENDIF
ELSE
  IF ($region_t"0|*>1") THEN
    DEFINE SYMBOL region_xzt = ($region_t)
  ENDIF
ENDIF

! region_yzt
IF ($region_yz"0|*>1") THEN
  IF ($region_t"0|*>1") THEN
    DEFINE SYMBOL region_yzt = ($region_yz),($region_t)
  ELSE
    DEFINE SYMBOL region_yzt = ($region_yz)
  ENDIF
ELSE
  IF ($region_t"0|*>1") THEN
    DEFINE SYMBOL region_yzt = ($region_t)
  ENDIF
ENDIF

! region_xyzt
IF ($region_xyz"0|*>1") THEN
  IF ($region_t"0|*>1") THEN
    DEFINE SYMBOL region_xyzt = ($region_xyz),($region_t)
  ELSE
    DEFINE SYMBOL region_xyzt = ($region_xyz)
  ENDIF
ELSE
  IF ($region_t"0|*>1") THEN
    DEFINE SYMBOL region_xyzt = ($region_t)
  ENDIF
ENDIF

! There should be more of these but we will add as needed.

! region_xyzte
IF ($region_xyzt"0|*>1") THEN
  IF ($region_e"0|*>1") THEN
    DEFINE SYMBOL region_xyzte = ($region_xyzt),($region_e)
  ELSE
    DEFINE SYMBOL region_xyzte = ($region_xyzt)
  ENDIF
ELSE
  IF ($region_e"0|*>1") THEN
    DEFINE SYMBOL region_xyzte = ($region_e)
  ENDIF
ENDIF

DEFINE SYMBOL region_full = ($region_xyzte)

! For sampling from a curvilinear variable, save the region definitions with
! the variable number.
IF ($region_x"0|*>1") THEN DEFINE SYMBOL region_($num)_x = ($region_x)
IF ($region_y"0|*>1") THEN DEFINE SYMBOL region_($num)_y = ($region_y)
IF ($region_z"0|*>1") THEN DEFINE SYMBOL region_($num)_z = ($region_z)
IF ($region_t"0|*>1") THEN DEFINE SYMBOL region_($num)_t = ($region_t)
IF ($region_xt"0|*>1") THEN DEFINE SYMBOL region_($num)_xt = ($region_xt)
IF ($region_xy"0|*>1") THEN DEFINE SYMBOL region_($num)_xy = ($region_xy)
IF ($region_xz"0|*>1") THEN DEFINE SYMBOL region_($num)_xz = ($region_xz)
IF ($region_yt"0|*>1") THEN DEFINE SYMBOL region_($num)_yt = ($region_yt)
IF ($region_yz"0|*>1") THEN DEFINE SYMBOL region_($num)_yz = ($region_yz)
IF ($region_zt"0|*>1") THEN DEFINE SYMBOL region_($num)_zt = ($region_zt)

! If time is in units of day, make time symbols w/o the trailing 00:00:00

IF ($region_0_t_lo"0|*>1") THEN
   DEFINE SYMBOL t_lab_lo = ($region_0_t_lo)
   LET iz = STRINDEX("($region_0_t_lo)", " 00:00:00")
   IF `iz GT 0` THEN 
      DEFINE SYMBOL t_lab_lo = `SUBSTRING("($region_0_t_lo)", 1, iz-1)`
   ELSE
      LET iz = STRINDEX("($region_0_t_lo)", ":00:00")
      IF `iz GT 0` THEN DEFINE SYMBOL t_lab_lo = `SUBSTRING("($region_0_t_lo)", 1, iz-1)`
   ENDIF
ENDIF 

IF ($region_0_t_hi"0|*>1") THEN
   DEFINE SYMBOL t_lab_hi = ($region_0_t_hi)
   LET iz = STRINDEX("($region_0_t_hi)", " 00:00:00")
   IF `iz GT 0` THEN 
      DEFINE SYMBOL t_lab_hi = `SUBSTRING("($region_0_t_hi)", 1, iz-1)`
   ELSE
      LET iz = STRINDEX("($region_0_t_hi)", ":00:00")
      IF `iz GT 0` THEN DEFINE SYMBOL t_lab_hi = `SUBSTRING("($region_0_t_hi)", 1, iz-1)`
   ENDIF
ENDIF

! If no regions have been defined then we have an error.

IF ($region_full"1|*>0")  THEN
  DEFINE SYMBOL error_status = ERROR
  DEFINE SYMBOL error_type = REGION
  DEFINE SYMBOL error_string = Region ($region) associated with data_($num) has not been defined.
  EXIT/SCRIPT
ENDIF

! End of LAS_initialize_region.jnl ------------------------------------------------------
