\cancel mode verify

! curvi_sample_slice.jnl
! 3/07 ACM

! Description: define variables for a line of constant Latitude or Longitude 
! of a curvilinear data set.
! This script assumes 
! The curvilinear data set has been opened, region defined, grid has coord variables
!    ($ferret_curvi_coord_lon), ($ferret_curvi_coord_lat)

! Note a new function SAMPLEXY_CURV_AVG_AVG samples with less-strict unweighted averaging.
! Note a new function SAMPLEXY_CURV_NRST returns value at ij point representing nearest xy.

IF ($region_z"0|*>1") THEN DEFINE SYMBOL region_sample = ,($region_z)
IF ($region_t"0|*>1") THEN DEFINE SYMBOL region_sample = ,($region_t)
IF ($region_zt"0|*>1") THEN DEFINE SYMBOL region_sample = ,($region_zt)


! default value; may be redefined in this script
LET/units="($data_($var_num)_units)" sampled_var($var_num) = ($ferret_plot_var)


! Call CURV_RANGE to get the I,J range for the requested output rectangle

GO get_curv_coord_bbox

! Y line, or YZ, YT slice
LET yslice = ($ferret_view"|y>1|*>0") + ($ferret_view"|yz>1|*>0") + ($ferret_view"|yt>1|*>0")
IF `yslice GT 0` THEN GO curvi_sample_yslice.jnl

! X line, or XZ, XT slice
LET xslice = ($ferret_view"|x>1|*>0") + ($ferret_view"|xz>1|*>0") + ($ferret_view"|xt>1|*>0")
IF `xslice GT 0` THEN GO curvi_sample_xslice.jnl

! T line
LET tslice = ($ferret_view"|t>1|*>0") 
IF `tslice GT 0` THEN GO curvi_sample_tslice.jnl


! Z line
LET zslice = ($ferret_view"|z>1|*>0") 
IF `zslice GT 0` GO curvi_sample_zslice.jnl

DEFINE SYMBOL ferret_plot_var = ($expr1)sampled_var($var_num)($expr2)
DEFINE SYMBOL ferret_plot_title "($expr1) ($data_title) ($expr2) ($data_units) Sampled from curvilinear grid"

IF ($expr3"0|*>1") THEN
   DEFINE SYMBOL ferret_plot_var ($expr1)sampled_var($var_num)($expr3)sampled_var($var_num)($expr4)
   DEFINE SYMBOL ferret_plot_title "($expr1) ($data_title) ($expr3) ($data_title) ($expr4) Sampled from curvilinear grid"
ENDIF


! End of file ------------curvi_sample_slice.jnl--------------------------

