!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!  xy_insitu_plot.jnl
! 
! Author: ansley 
! Date: 2016/02/24
!
! xy_insitu_plot.jnl creates an XY ribbon plot for insitu data, for use with the 
! Back End Server code that sits behind a Live Access Server (LAS). 
!
! Plots data locations xpts, ypts, with an optional variable at those
! locations whose values color the points.

! Define symbols associated with the regign and data and perform any
! initialization needed for this dataset.

DEFINE SYMBOL data_initialization_script = LAS_initialize_data_insitu

GO LAS_initialize_region 0
GO LAS_initialize_data 0
set mem/siz=300

! Check for errors (They often occur during dataset initialization.)

IF ($error_status"0|*>1") THEN
  MESSAGE/ERROR **ERROR ($error_string)
  EXIT/PROGRAM
ENDIF

! Set up upper-left labels with the region info
GO LAS_pp_set_region_labels

GO LAS_url_label 0

! Open the window, apply size, scale options
GO LAS_open_window

GO LAS_set_plot_qualifiers

! Make a basemap plot similar to refmap.
! Graticule and axis settings are applied in this step.

DEFINE SYMBOL basemap_palette = grayscale

DEFINE SYMBOL basemap_levels = (-inf)(-20000,-1000,500)(-1000,0,100)

IF ($ferret_bathymetry_style"1|grayscale>1|*>0") THEN 
   PPL COLOR,7,55,55,55     ! gray
ENDIF

IF ($ferret_bathymetry_style"0|white>1|*>0") THEN DEFINE SYMBOL basemap_palette = white
IF ($ferret_bathymetry_style"0|blue>1|*>0") THEN 
   DEFINE SYMBOL basemap_palette = osmc_bluescale
   DEFINE SYMBOL basemap_levels = (-20000)(-10000,0,10000)
ENDIF

GO LAS_XY_overlay_basemap
GO LAS_fland ($ferret_land_palette"gray_dark") 
PPL SHASET PROTECT

! Define plot qualifiers for polygon overlay  (these should go into LAS_set_plot_qualifiers?)

DEFINE SYMBOL qualifiers = ($qualifiers)/OVER/NOLAB

! Could se the colorBarMaximum, colorBarMinimum attributes to set color levels

IF ($ferret_fill_levels"0|*>1") THEN 
  DEFINE SYMBOL qualifiers = ($qualifiers)/LEVELS=($ferret_fill_levels"0")
ELIF ($ferret_poly_levels"0|*>1") THEN 
  DEFINE SYMBOL qualifiers = ($qualifiers)/LEVELS=($ferret_poly_levels"0")
ELSE
  DEFINE SYMBOL qualifiers = ($qualifiers)/LEVELS=v
ENDIF

! Color key for the variable values. If no variable only locations, plot no
! key. Otherwise controlled by ferret_plot_key, or key turned on by default.

IF ($ferret_plot_var"1|*>0") THEN DEFINE SYMBOL ferret_plot_key "0"

IF ($ferret_plot_key"1|0|1|nokey>0|key>1|default>1|*>1") THEN
   DEFINE SYMBOL qualifiers = ($qualifiers)/KEY
ELSE
   DEFINE SYMBOL qualifiers = ($qualifiers)/NOKEY
ENDIF

! If there is no variable, only locations, and if a palette is not 
! defined by ferret_palette, use black.

IF ($ferret_plot_var"0|*>1") THEN
   DEFINE SYMBOL qualifiers = ($qualifiers)/PALETTE=($ferret_palette"default|*>*)
ELSE
   DEFINE SYMBOL qualifiers = ($qualifiers)/PALETTE=($ferret_palette"black|default>black|*>*)
ENDIF

LET numpts = `($data_x_var),RETURN=isize`
IF `numpts LT 10000` THEN 
   DEFINE SYMBOL qualifiers = ($qualifiers)/sym=19/thick/siz=0.2
ELIF `numpts LT 200000` THEN 
   DEFINE SYMBOL qualifiers = ($qualifiers)/sym=19/thick/siz=0.1
ELSE
   DEFINE SYMBOL qualifiers = ($qualifiers)/sym=19/siz=0.05
ENDIF

DEFINE SYMBOL qualifiers =($qualifiers)/KEY=HORIZ


DEFINE SYMBOL plot_command plot/vs/ribbon/miss=black($qualifiers)/KEY=HORIZ

IF `($ferret_annotations%0|*>1%) EQ 0` THEN \
 LABEL/NOUSER `($ppl$xlen)/2`, `-1*($ppl$yorg)/2`, 0, 0, 0.14, ($ferret_plot_title)

! Make the ribbon overlay plot

IF ($ferret_plot_var"0|*>1") THEN
   ($plot_command)/SET ($data_x_var), ($data_y_var), ($ferret_plot_var) 
   IF `($ferret_annotations%0|*>1%) AND ($ferret_plot_key"0|date_key>1|*>0") ` THEN GO key_at_top
   PPL RIBBON/OVER
ELSE
   ($plot_command)($plot_command)/SET ($data_x_var), ($data_y_var) " " 
!   IF ($ferret_annotations%0|*>1%) THEN GO key_at_top
   PPL RIBBON/OVER
ENDIF


! Save the results

GO LAS_results box

